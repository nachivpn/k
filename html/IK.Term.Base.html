<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>IK.Term.Base</title><link rel="stylesheet" href="Agda.css"></head><body><pre class="Agda"><a id="1" class="Symbol">{-#</a> <a id="5" class="Keyword">OPTIONS</a> <a id="13" class="Pragma">--safe</a> <a id="20" class="Pragma">--without-K</a> <a id="32" class="Symbol">#-}</a>
<a id="36" class="Keyword">module</a> <a id="43" href="IK.Term.Base.html" class="Module">IK.Term.Base</a> <a id="56" class="Keyword">where</a>

<a id="63" class="Comment">--</a>
<a id="66" class="Comment">-- Implementation of the IK (Intuitionistic K) calculus from</a>
<a id="127" class="Comment">-- &quot;Fitch-Style Modal Lambda Calculi&quot; by Ranald Clouston (2018)</a>
<a id="191" class="Comment">--</a>

<a id="195" class="Keyword">open</a> <a id="200" class="Keyword">import</a> <a id="207" href="Data.Product.html" class="Module">Data.Product</a> <a id="220" class="Keyword">using</a> <a id="226" class="Symbol">(</a><a id="227" href="Agda.Builtin.Sigma.html#166" class="Record">Σ</a> <a id="229" class="Symbol">;</a> <a id="231" href="Data.Product.html#1369" class="Function">∃</a> <a id="233" class="Symbol">;</a> <a id="235" href="Data.Product.html#1167" class="Function Operator">_×_</a> <a id="239" class="Symbol">;</a> <a id="241" href="Agda.Builtin.Sigma.html#236" class="InductiveConstructor Operator">_,_</a> <a id="245" class="Symbol">;</a> <a id="247" href="Agda.Builtin.Sigma.html#252" class="Field">proj₁</a> <a id="253" class="Symbol">;</a> <a id="255" href="Agda.Builtin.Sigma.html#264" class="Field">proj₂</a><a id="260" class="Symbol">)</a>

<a id="263" class="Keyword">open</a> <a id="268" class="Keyword">import</a> <a id="275" href="Type.html" class="Module">Type</a> <a id="280" class="Symbol">as</a> <a id="283" class="Module">Type</a> <a id="288" class="Keyword">using</a> <a id="294" class="Symbol">(</a><a id="295" href="Type.html#284" class="Datatype">Ty</a> <a id="298" class="Symbol">;</a> <a id="300" href="Type.html#393" class="Function">Ty-Decidable</a><a id="312" class="Symbol">)</a>

<a id="315" class="Keyword">open</a> <a id="320" class="Keyword">import</a> <a id="327" href="Context.html" class="Module">Context</a> <a id="335" href="Type.html#284" class="Datatype">Ty</a> <a id="338" href="Type.html#393" class="Function">Ty-Decidable</a> as <a id="Context"></a><a id="354" href="IK.Term.Base.html#354" class="Module">Context</a>

<a id="363" class="Keyword">open</a> <a id="368" href="IK.Term.Base.html#354" class="Module">Context</a> <a id="376" class="Keyword">public</a>
<a id="383" class="Keyword">open</a> <a id="388" href="Type.html" class="Module">Type</a>    <a id="396" class="Keyword">public</a>

<a id="404" class="Comment">-------------------------------------</a>
<a id="442" class="Comment">-- Variables, terms and substitutions</a>
<a id="480" class="Comment">-------------------------------------</a>

<a id="519" class="Keyword">data</a> <a id="Tm"></a><a id="524" href="IK.Term.Base.html#524" class="Datatype">Tm</a> <a id="527" class="Symbol">:</a> <a id="529" href="Context.html#719" class="Datatype">Ctx</a> <a id="533" class="Symbol">→</a> <a id="535" href="Type.html#284" class="Datatype">Ty</a> <a id="538" class="Symbol">→</a> <a id="540" href="Agda.Primitive.html#326" class="Primitive">Set</a> <a id="544" class="Keyword">where</a>

  <a id="Tm.var"></a><a id="553" href="IK.Term.Base.html#553" class="InductiveConstructor">var</a>  <a id="558" class="Symbol">:</a> <a id="560" class="Symbol">(</a><a id="561" href="IK.Term.Base.html#561" class="Bound">v</a> <a id="563" class="Symbol">:</a> <a id="565" href="Context.html#3811" class="Datatype">Var</a> <a id="569" href="Context.html#898" class="Generalizable">Γ</a> <a id="571" href="Type.html#379" class="Generalizable">a</a><a id="572" class="Symbol">)</a>
       <a id="581" class="Comment">---------------</a>
       <a id="604" class="Symbol">→</a> <a id="606" href="IK.Term.Base.html#524" class="Datatype">Tm</a> <a id="609" href="Context.html#898" class="Generalizable">Γ</a> <a id="611" href="Type.html#379" class="Generalizable">a</a>

  <a id="Tm.lam"></a><a id="616" href="IK.Term.Base.html#616" class="InductiveConstructor">lam</a>  <a id="621" class="Symbol">:</a> <a id="623" class="Symbol">(</a><a id="624" href="IK.Term.Base.html#624" class="Bound">t</a> <a id="626" class="Symbol">:</a> <a id="628" href="IK.Term.Base.html#524" class="Datatype">Tm</a> <a id="631" class="Symbol">(</a><a id="632" href="Context.html#898" class="Generalizable">Γ</a> <a id="634" href="Context.html#750" class="InductiveConstructor Operator">`,</a> <a id="637" href="Type.html#379" class="Generalizable">a</a><a id="638" class="Symbol">)</a> <a id="640" href="Type.html#381" class="Generalizable">b</a><a id="641" class="Symbol">)</a>
         <a id="652" class="Comment">-------------------</a>
       <a id="679" class="Symbol">→</a> <a id="681" href="IK.Term.Base.html#524" class="Datatype">Tm</a> <a id="684" href="Context.html#898" class="Generalizable">Γ</a> <a id="686" class="Symbol">(</a><a id="687" href="Type.html#379" class="Generalizable">a</a> <a id="689" href="Type.html#312" class="InductiveConstructor Operator">⇒</a> <a id="691" href="Type.html#381" class="Generalizable">b</a><a id="692" class="Symbol">)</a>

  <a id="Tm.app"></a><a id="697" href="IK.Term.Base.html#697" class="InductiveConstructor">app</a>  <a id="702" class="Symbol">:</a> <a id="704" class="Symbol">(</a><a id="705" href="IK.Term.Base.html#705" class="Bound">t</a> <a id="707" class="Symbol">:</a> <a id="709" href="IK.Term.Base.html#524" class="Datatype">Tm</a> <a id="712" href="Context.html#898" class="Generalizable">Γ</a> <a id="714" class="Symbol">(</a><a id="715" href="Type.html#379" class="Generalizable">a</a> <a id="717" href="Type.html#312" class="InductiveConstructor Operator">⇒</a> <a id="719" href="Type.html#381" class="Generalizable">b</a><a id="720" class="Symbol">))</a>
       <a id="730" class="Symbol">→</a> <a id="732" class="Symbol">(</a><a id="733" href="IK.Term.Base.html#733" class="Bound">u</a> <a id="735" class="Symbol">:</a> <a id="737" href="IK.Term.Base.html#524" class="Datatype">Tm</a> <a id="740" href="Context.html#898" class="Generalizable">Γ</a> <a id="742" href="Type.html#379" class="Generalizable">a</a><a id="743" class="Symbol">)</a>
         <a id="754" class="Comment">------------------</a>
       <a id="780" class="Symbol">→</a> <a id="782" href="IK.Term.Base.html#524" class="Datatype">Tm</a> <a id="785" href="Context.html#898" class="Generalizable">Γ</a> <a id="787" href="Type.html#381" class="Generalizable">b</a>

  <a id="Tm.box"></a><a id="792" href="IK.Term.Base.html#792" class="InductiveConstructor">box</a>   <a id="798" class="Symbol">:</a> <a id="800" class="Symbol">(</a><a id="801" href="IK.Term.Base.html#801" class="Bound">t</a> <a id="803" class="Symbol">:</a> <a id="805" href="IK.Term.Base.html#524" class="Datatype">Tm</a> <a id="808" class="Symbol">(</a><a id="809" href="Context.html#898" class="Generalizable">Γ</a> <a id="811" href="Context.html#786" class="InductiveConstructor Operator">#</a><a id="812" class="Symbol">)</a> <a id="814" href="Type.html#379" class="Generalizable">a</a><a id="815" class="Symbol">)</a>
        <a id="825" class="Comment">------------------</a>
        <a id="852" class="Symbol">→</a> <a id="854" href="IK.Term.Base.html#524" class="Datatype">Tm</a> <a id="857" href="Context.html#898" class="Generalizable">Γ</a> <a id="859" class="Symbol">(</a><a id="860" href="Type.html#345" class="InductiveConstructor Operator">□</a> <a id="862" href="Type.html#379" class="Generalizable">a</a><a id="863" class="Symbol">)</a>

  <a id="Tm.unbox"></a><a id="868" href="IK.Term.Base.html#868" class="InductiveConstructor">unbox</a> <a id="874" class="Symbol">:</a> <a id="876" class="Symbol">(</a><a id="877" href="IK.Term.Base.html#877" class="Bound">t</a> <a id="879" class="Symbol">:</a> <a id="881" href="IK.Term.Base.html#524" class="Datatype">Tm</a> <a id="884" href="Context.html#907" class="Generalizable">ΓL</a> <a id="887" class="Symbol">(</a><a id="888" href="Type.html#345" class="InductiveConstructor Operator">□</a> <a id="890" href="Type.html#379" class="Generalizable">a</a><a id="891" class="Symbol">))</a>
        <a id="902" class="Symbol">→</a> <a id="904" class="Symbol">(</a><a id="905" href="IK.Term.Base.html#905" class="Bound">e</a> <a id="907" class="Symbol">:</a> <a id="909" href="Context.html#8096" class="Function">LFExt</a> <a id="915" href="Context.html#898" class="Generalizable">Γ</a> <a id="917" class="Symbol">(</a><a id="918" href="Context.html#907" class="Generalizable">ΓL</a> <a id="921" href="Context.html#786" class="InductiveConstructor Operator">#</a><a id="922" class="Symbol">)</a> <a id="924" href="Context.html#910" class="Generalizable">ΓR</a><a id="926" class="Symbol">)</a>
        <a id="936" class="Comment">-------------------------</a>
        <a id="970" class="Symbol">→</a> <a id="972" href="IK.Term.Base.html#524" class="Datatype">Tm</a> <a id="975" href="Context.html#898" class="Generalizable">Γ</a> <a id="977" href="Type.html#379" class="Generalizable">a</a>

<a id="980" class="Keyword">variable</a>
  <a id="991" href="IK.Term.Base.html#991" class="Generalizable">t</a> <a id="993" href="IK.Term.Base.html#993" class="Generalizable">t&#39;</a> <a id="996" href="IK.Term.Base.html#996" class="Generalizable">t&#39;&#39;</a> <a id="1000" href="IK.Term.Base.html#1000" class="Generalizable">t&#39;&#39;&#39;</a> <a id="1005" class="Symbol">:</a> <a id="1007" href="IK.Term.Base.html#524" class="Datatype">Tm</a> <a id="1010" href="Context.html#898" class="Generalizable">Γ</a> <a id="1012" href="Type.html#379" class="Generalizable">a</a>
  <a id="1016" href="IK.Term.Base.html#1016" class="Generalizable">u</a> <a id="1018" href="IK.Term.Base.html#1018" class="Generalizable">u&#39;</a> <a id="1021" href="IK.Term.Base.html#1021" class="Generalizable">u&#39;&#39;</a> <a id="1025" href="IK.Term.Base.html#1025" class="Generalizable">u&#39;&#39;&#39;</a> <a id="1030" class="Symbol">:</a> <a id="1032" href="IK.Term.Base.html#524" class="Datatype">Tm</a> <a id="1035" href="Context.html#898" class="Generalizable">Γ</a> <a id="1037" href="Type.html#379" class="Generalizable">a</a>

<a id="wkTm"></a><a id="1040" href="IK.Term.Base.html#1040" class="Function">wkTm</a> <a id="1045" class="Symbol">:</a> <a id="1047" href="Context.html#898" class="Generalizable">Γ</a> <a id="1049" href="Context.html#2479" class="Datatype Operator">⊆</a> <a id="1051" href="Context.html#900" class="Generalizable">Γ&#39;</a> <a id="1054" class="Symbol">→</a> <a id="1056" href="IK.Term.Base.html#524" class="Datatype">Tm</a> <a id="1059" href="Context.html#898" class="Generalizable">Γ</a> <a id="1061" href="Type.html#379" class="Generalizable">a</a> <a id="1063" class="Symbol">→</a> <a id="1065" href="IK.Term.Base.html#524" class="Datatype">Tm</a> <a id="1068" href="Context.html#900" class="Generalizable">Γ&#39;</a> <a id="1071" href="Type.html#379" class="Generalizable">a</a>
<a id="1073" href="IK.Term.Base.html#1040" class="Function">wkTm</a> <a id="1078" href="IK.Term.Base.html#1078" class="Bound">w</a> <a id="1080" class="Symbol">(</a><a id="1081" href="IK.Term.Base.html#553" class="InductiveConstructor">var</a> <a id="1085" href="IK.Term.Base.html#1085" class="Bound">x</a><a id="1086" class="Symbol">)</a>                <a id="1103" class="Symbol">=</a> <a id="1105" href="IK.Term.Base.html#553" class="InductiveConstructor">var</a> <a id="1109" class="Symbol">(</a><a id="1110" href="Context.html#3964" class="Function">wkVar</a> <a id="1116" href="IK.Term.Base.html#1078" class="Bound">w</a> <a id="1118" href="IK.Term.Base.html#1085" class="Bound">x</a><a id="1119" class="Symbol">)</a>
<a id="1121" href="IK.Term.Base.html#1040" class="Function">wkTm</a> <a id="1126" href="IK.Term.Base.html#1126" class="Bound">w</a> <a id="1128" class="Symbol">(</a><a id="1129" href="IK.Term.Base.html#616" class="InductiveConstructor">lam</a> <a id="1133" href="IK.Term.Base.html#1133" class="Bound">t</a><a id="1134" class="Symbol">)</a>                <a id="1151" class="Symbol">=</a> <a id="1153" href="IK.Term.Base.html#616" class="InductiveConstructor">lam</a> <a id="1157" class="Symbol">(</a><a id="1158" href="IK.Term.Base.html#1040" class="Function">wkTm</a> <a id="1163" class="Symbol">(</a><a id="1164" href="Context.html#2559" class="InductiveConstructor">keep</a> <a id="1169" href="IK.Term.Base.html#1126" class="Bound">w</a><a id="1170" class="Symbol">)</a> <a id="1172" href="IK.Term.Base.html#1133" class="Bound">t</a><a id="1173" class="Symbol">)</a>
<a id="1175" href="IK.Term.Base.html#1040" class="Function">wkTm</a> <a id="1180" href="IK.Term.Base.html#1180" class="Bound">w</a> <a id="1182" class="Symbol">(</a><a id="1183" href="IK.Term.Base.html#697" class="InductiveConstructor">app</a> <a id="1187" href="IK.Term.Base.html#1187" class="Bound">t</a> <a id="1189" href="IK.Term.Base.html#1189" class="Bound">u</a><a id="1190" class="Symbol">)</a>              <a id="1205" class="Symbol">=</a> <a id="1207" href="IK.Term.Base.html#697" class="InductiveConstructor">app</a> <a id="1211" class="Symbol">(</a><a id="1212" href="IK.Term.Base.html#1040" class="Function">wkTm</a> <a id="1217" href="IK.Term.Base.html#1180" class="Bound">w</a> <a id="1219" href="IK.Term.Base.html#1187" class="Bound">t</a><a id="1220" class="Symbol">)</a> <a id="1222" class="Symbol">(</a><a id="1223" href="IK.Term.Base.html#1040" class="Function">wkTm</a> <a id="1228" href="IK.Term.Base.html#1180" class="Bound">w</a> <a id="1230" href="IK.Term.Base.html#1189" class="Bound">u</a><a id="1231" class="Symbol">)</a>
<a id="1233" href="IK.Term.Base.html#1040" class="Function">wkTm</a> <a id="1238" href="IK.Term.Base.html#1238" class="Bound">w</a> <a id="1240" class="Symbol">(</a><a id="1241" href="IK.Term.Base.html#792" class="InductiveConstructor">box</a> <a id="1245" href="IK.Term.Base.html#1245" class="Bound">t</a><a id="1246" class="Symbol">)</a>                <a id="1263" class="Symbol">=</a> <a id="1265" href="IK.Term.Base.html#792" class="InductiveConstructor">box</a> <a id="1269" class="Symbol">(</a><a id="1270" href="IK.Term.Base.html#1040" class="Function">wkTm</a> <a id="1275" class="Symbol">(</a><a id="1276" href="Context.html#2594" class="InductiveConstructor">keep#</a> <a id="1282" href="IK.Term.Base.html#1238" class="Bound">w</a><a id="1283" class="Symbol">)</a> <a id="1285" href="IK.Term.Base.html#1245" class="Bound">t</a><a id="1286" class="Symbol">)</a>
<a id="1288" href="IK.Term.Base.html#1040" class="Function">wkTm</a> <a id="1293" href="IK.Term.Base.html#1293" class="Bound">w</a> <a id="1295" class="Symbol">(</a><a id="1296" href="IK.Term.Base.html#868" class="InductiveConstructor">unbox</a> <a id="1302" href="IK.Term.Base.html#1302" class="Bound">t</a> <a id="1304" href="IK.Term.Base.html#1304" class="Bound">e</a><a id="1305" class="Symbol">)</a>            <a id="1318" class="Symbol">=</a> <a id="1320" href="IK.Term.Base.html#868" class="InductiveConstructor">unbox</a> <a id="1326" class="Symbol">(</a><a id="1327" href="IK.Term.Base.html#1040" class="Function">wkTm</a> <a id="1332" class="Symbol">(</a><a id="1333" href="Context.html#15618" class="Function">sliceLeft</a> <a id="1343" href="IK.Term.Base.html#1304" class="Bound">e</a> <a id="1345" href="IK.Term.Base.html#1293" class="Bound">w</a><a id="1346" class="Symbol">)</a> <a id="1348" href="IK.Term.Base.html#1302" class="Bound">t</a><a id="1349" class="Symbol">)</a> <a id="1351" class="Symbol">(</a><a id="1352" href="Context.html#14854" class="Function">wkLFExt</a> <a id="1360" href="IK.Term.Base.html#1304" class="Bound">e</a> <a id="1362" href="IK.Term.Base.html#1293" class="Bound">w</a><a id="1363" class="Symbol">)</a>

<a id="leftWkTm"></a><a id="1366" href="IK.Term.Base.html#1366" class="Function">leftWkTm</a> <a id="1375" class="Symbol">:</a> <a id="1377" class="Symbol">(</a><a id="1378" href="IK.Term.Base.html#1378" class="Bound">t</a> <a id="1380" class="Symbol">:</a> <a id="1382" href="IK.Term.Base.html#524" class="Datatype">Tm</a> <a id="1385" href="Context.html#898" class="Generalizable">Γ</a> <a id="1387" href="Type.html#379" class="Generalizable">a</a><a id="1388" class="Symbol">)</a> <a id="1390" class="Symbol">→</a> <a id="1392" href="IK.Term.Base.html#524" class="Datatype">Tm</a> <a id="1395" class="Symbol">(</a><a id="1396" href="Context.html#925" class="Generalizable">Δ</a> <a id="1398" href="Context.html#1029" class="Function Operator">,,</a> <a id="1401" href="Context.html#898" class="Generalizable">Γ</a><a id="1402" class="Symbol">)</a> <a id="1404" href="Type.html#379" class="Generalizable">a</a>
<a id="1406" href="IK.Term.Base.html#1366" class="Function">leftWkTm</a> <a id="1415" class="Symbol">(</a><a id="1416" href="IK.Term.Base.html#553" class="InductiveConstructor">var</a> <a id="1420" href="IK.Term.Base.html#1420" class="Bound">v</a><a id="1421" class="Symbol">)</a>     <a id="1427" class="Symbol">=</a> <a id="1429" href="IK.Term.Base.html#553" class="InductiveConstructor">var</a> <a id="1433" class="Symbol">(</a><a id="1434" href="Context.html#4149" class="Function">leftWkVar</a> <a id="1444" href="IK.Term.Base.html#1420" class="Bound">v</a><a id="1445" class="Symbol">)</a>
<a id="1447" href="IK.Term.Base.html#1366" class="Function">leftWkTm</a> <a id="1456" class="Symbol">(</a><a id="1457" href="IK.Term.Base.html#616" class="InductiveConstructor">lam</a> <a id="1461" href="IK.Term.Base.html#1461" class="Bound">t</a><a id="1462" class="Symbol">)</a>     <a id="1468" class="Symbol">=</a> <a id="1470" href="IK.Term.Base.html#616" class="InductiveConstructor">lam</a> <a id="1474" class="Symbol">(</a><a id="1475" href="IK.Term.Base.html#1366" class="Function">leftWkTm</a> <a id="1484" href="IK.Term.Base.html#1461" class="Bound">t</a><a id="1485" class="Symbol">)</a>
<a id="1487" href="IK.Term.Base.html#1366" class="Function">leftWkTm</a> <a id="1496" class="Symbol">(</a><a id="1497" href="IK.Term.Base.html#697" class="InductiveConstructor">app</a> <a id="1501" href="IK.Term.Base.html#1501" class="Bound">t</a> <a id="1503" href="IK.Term.Base.html#1503" class="Bound">u</a><a id="1504" class="Symbol">)</a>   <a id="1508" class="Symbol">=</a> <a id="1510" href="IK.Term.Base.html#697" class="InductiveConstructor">app</a> <a id="1514" class="Symbol">(</a><a id="1515" href="IK.Term.Base.html#1366" class="Function">leftWkTm</a> <a id="1524" href="IK.Term.Base.html#1501" class="Bound">t</a><a id="1525" class="Symbol">)</a> <a id="1527" class="Symbol">(</a><a id="1528" href="IK.Term.Base.html#1366" class="Function">leftWkTm</a> <a id="1537" href="IK.Term.Base.html#1503" class="Bound">u</a><a id="1538" class="Symbol">)</a>
<a id="1540" href="IK.Term.Base.html#1366" class="Function">leftWkTm</a> <a id="1549" class="Symbol">(</a><a id="1550" href="IK.Term.Base.html#792" class="InductiveConstructor">box</a> <a id="1554" href="IK.Term.Base.html#1554" class="Bound">t</a><a id="1555" class="Symbol">)</a>     <a id="1561" class="Symbol">=</a> <a id="1563" href="IK.Term.Base.html#792" class="InductiveConstructor">box</a> <a id="1567" class="Symbol">(</a><a id="1568" href="IK.Term.Base.html#1366" class="Function">leftWkTm</a> <a id="1577" href="IK.Term.Base.html#1554" class="Bound">t</a><a id="1578" class="Symbol">)</a>
<a id="1580" href="IK.Term.Base.html#1366" class="Function">leftWkTm</a> <a id="1589" class="Symbol">(</a><a id="1590" href="IK.Term.Base.html#868" class="InductiveConstructor">unbox</a> <a id="1596" href="IK.Term.Base.html#1596" class="Bound">t</a> <a id="1598" href="IK.Term.Base.html#1598" class="Bound">e</a><a id="1599" class="Symbol">)</a> <a id="1601" class="Symbol">=</a> <a id="1603" href="IK.Term.Base.html#868" class="InductiveConstructor">unbox</a> <a id="1609" class="Symbol">(</a><a id="1610" href="IK.Term.Base.html#1366" class="Function">leftWkTm</a> <a id="1619" href="IK.Term.Base.html#1596" class="Bound">t</a><a id="1620" class="Symbol">)</a> <a id="1622" class="Symbol">(</a><a id="1623" href="Context.html#15113" class="Function">leftWkLFExt</a> <a id="1635" href="IK.Term.Base.html#1598" class="Bound">e</a><a id="1636" class="Symbol">)</a>

<a id="1639" class="Comment">-- extension that &quot;generates a new context frame&quot;</a>
<a id="1689" class="Keyword">pattern</a> <a id="new"></a><a id="1697" href="IK.Term.Base.html#1697" class="InductiveConstructor">new</a>      <a id="1706" class="Symbol">=</a> <a id="1708" href="Context.html#7432" class="InductiveConstructor">nil</a>
<a id="1712" class="Keyword">pattern</a> <a id="new[_]"></a><a id="1720" href="IK.Term.Base.html#1720" class="InductiveConstructor Operator">new[_]</a> <a id="1727" href="IK.Term.Base.html#1736" class="Bound">Γ</a> <a id="1729" class="Symbol">=</a> <a id="1731" href="Context.html#7432" class="InductiveConstructor">nil</a> <a id="1735" class="Symbol">{</a><a id="1736" href="IK.Term.Base.html#1736" class="Bound">Γ</a><a id="1737" class="Symbol">}</a>

<a id="1740" class="Keyword">open</a> <a id="1745" href="Context.html#31808" class="Module">Substitution</a> <a id="1758" href="IK.Term.Base.html#524" class="Datatype">Tm</a> <a id="1761" href="IK.Term.Base.html#553" class="InductiveConstructor">var</a> <a id="1765" href="IK.Term.Base.html#1040" class="Function">wkTm</a> <a id="1770" class="Symbol">(λ</a> <a id="1773" href="IK.Term.Base.html#1773" class="Bound">Γ</a> <a id="1775" href="IK.Term.Base.html#1775" class="Bound">ΓL</a> <a id="1778" href="IK.Term.Base.html#1778" class="Bound">ΓR</a> <a id="1781" class="Symbol">→</a> <a id="1783" href="Context.html#8096" class="Function">LFExt</a> <a id="1789" href="IK.Term.Base.html#1773" class="Bound">Γ</a> <a id="1791" class="Symbol">(</a><a id="1792" href="IK.Term.Base.html#1775" class="Bound">ΓL</a> <a id="1795" href="Context.html#786" class="InductiveConstructor Operator">#</a><a id="1796" class="Symbol">)</a> <a id="1798" href="IK.Term.Base.html#1778" class="Bound">ΓR</a><a id="1800" class="Symbol">)</a> <a id="1802" href="IK.Term.Base.html#1697" class="InductiveConstructor">new</a> <a id="1806" class="Symbol">(λ</a> <a id="1809" class="Symbol">{</a><a id="1810" class="Argument">Δ&#39;</a> <a id="1813" class="Symbol">=</a> <a id="1815" href="IK.Term.Base.html#1815" class="Bound">Δ&#39;</a><a id="1817" class="Symbol">}</a> <a id="1819" href="IK.Term.Base.html#1819" class="Bound Operator">_e</a> <a id="1822" href="IK.Term.Base.html#1822" class="Bound Operator">_w</a> <a id="1825" class="Symbol">→</a> <a id="1827" href="Context.html#1277" class="Function">←#</a> <a id="1830" href="IK.Term.Base.html#1815" class="Bound">Δ&#39;</a><a id="1832" class="Symbol">)</a> <a id="1834" href="Context.html#15618" class="Function">sliceLeft</a> <a id="1844" class="Symbol">(λ</a> <a id="1847" class="Symbol">{</a><a id="1848" class="Argument">Δ&#39;</a> <a id="1851" class="Symbol">=</a> <a id="1853" href="IK.Term.Base.html#1853" class="Bound">Δ&#39;</a><a id="1855" class="Symbol">}</a> <a id="1857" href="IK.Term.Base.html#1857" class="Bound Operator">_e</a> <a id="1860" href="IK.Term.Base.html#1860" class="Bound Operator">_w</a> <a id="1863" class="Symbol">→</a> <a id="1865" href="Context.html#1386" class="Function">#→</a> <a id="1868" href="IK.Term.Base.html#1853" class="Bound">Δ&#39;</a><a id="1870" class="Symbol">)</a> <a id="1872" href="Context.html#14854" class="Function">wkLFExt</a> <a id="1880" class="Keyword">public</a>
  <a id="1889" class="Keyword">renaming</a> <a id="1898" class="Symbol">(</a><a id="1899" class="Keyword">module</a> <a id="1906" href="Context.html#34189" class="Module">Composition</a> <a id="1918" class="Symbol">to</a> <a id="1921" class="Module">SubstitutionComposition</a><a id="1944" class="Symbol">)</a>

<a id="1947" class="Comment">-- &quot;Left&quot; context of factoring with a substitution (see factorSubₛ and factorExtₛ)</a>
<a id="lCtxₛ"></a><a id="2030" href="IK.Term.Base.html#2030" class="Function">lCtxₛ</a> <a id="2036" class="Symbol">:</a> <a id="2038" class="Symbol">(</a><a id="2039" href="IK.Term.Base.html#2039" class="Bound">e</a> <a id="2041" class="Symbol">:</a> <a id="2043" href="Context.html#8096" class="Function">LFExt</a> <a id="2049" href="Context.html#898" class="Generalizable">Γ</a> <a id="2051" class="Symbol">(</a><a id="2052" href="Context.html#907" class="Generalizable">ΓL</a> <a id="2055" href="Context.html#786" class="InductiveConstructor Operator">#</a><a id="2056" class="Symbol">)</a> <a id="2058" href="Context.html#910" class="Generalizable">ΓR</a><a id="2060" class="Symbol">)</a> <a id="2062" class="Symbol">(</a><a id="2063" href="IK.Term.Base.html#2063" class="Bound">s</a> <a id="2065" class="Symbol">:</a> <a id="2067" href="Context.html#32488" class="Datatype">Sub</a> <a id="2071" href="Context.html#925" class="Generalizable">Δ</a> <a id="2073" href="Context.html#898" class="Generalizable">Γ</a><a id="2074" class="Symbol">)</a> <a id="2076" class="Symbol">→</a> <a id="2078" href="Context.html#719" class="Datatype">Ctx</a>
<a id="2082" href="IK.Term.Base.html#2030" class="Function">lCtxₛ</a> <a id="2088" href="Context.html#7432" class="InductiveConstructor">nil</a>     <a id="2096" class="Symbol">(</a><a id="2097" href="Context.html#32597" class="InductiveConstructor">lock</a> <a id="2102" class="Symbol">{</a><a id="2103" class="Argument">ΔL</a> <a id="2106" class="Symbol">=</a> <a id="2108" href="IK.Term.Base.html#2108" class="Bound">ΔL</a><a id="2110" class="Symbol">}</a> <a id="2112" href="IK.Term.Base.html#2112" class="Bound">s</a> <a id="2114" href="IK.Term.Base.html#2114" class="Bound">e</a><a id="2115" class="Symbol">)</a> <a id="2117" class="Symbol">=</a> <a id="2119" href="IK.Term.Base.html#2108" class="Bound">ΔL</a>
<a id="2122" href="IK.Term.Base.html#2030" class="Function">lCtxₛ</a> <a id="2128" class="Symbol">(</a><a id="2129" href="Context.html#7454" class="InductiveConstructor">ext</a> <a id="2133" href="IK.Term.Base.html#2133" class="Bound">e</a><a id="2134" class="Symbol">)</a> <a id="2136" class="Symbol">(</a><a id="2137" href="IK.Term.Base.html#2137" class="Bound">s</a> <a id="2139" href="Context.html#32540" class="InductiveConstructor Operator">`,</a> <a id="2142" href="IK.Term.Base.html#2142" class="Bound">t</a><a id="2143" class="Symbol">)</a>             <a id="2157" class="Symbol">=</a> <a id="2159" href="IK.Term.Base.html#2030" class="Function">lCtxₛ</a> <a id="2165" href="IK.Term.Base.html#2133" class="Bound">e</a> <a id="2167" href="IK.Term.Base.html#2137" class="Bound">s</a>

<a id="factorSubₛ"></a><a id="2170" href="IK.Term.Base.html#2170" class="Function">factorSubₛ</a> <a id="2181" class="Symbol">:</a> <a id="2183" class="Symbol">∀</a> <a id="2185" class="Symbol">(</a><a id="2186" href="IK.Term.Base.html#2186" class="Bound">e</a> <a id="2188" class="Symbol">:</a> <a id="2190" href="Context.html#8096" class="Function">LFExt</a> <a id="2196" href="Context.html#898" class="Generalizable">Γ</a> <a id="2198" class="Symbol">(</a><a id="2199" href="Context.html#907" class="Generalizable">ΓL</a> <a id="2202" href="Context.html#786" class="InductiveConstructor Operator">#</a><a id="2203" class="Symbol">)</a> <a id="2205" href="Context.html#910" class="Generalizable">ΓR</a><a id="2207" class="Symbol">)</a> <a id="2209" class="Symbol">(</a><a id="2210" href="IK.Term.Base.html#2210" class="Bound">s</a> <a id="2212" class="Symbol">:</a> <a id="2214" href="Context.html#32488" class="Datatype">Sub</a> <a id="2218" href="Context.html#925" class="Generalizable">Δ</a> <a id="2220" href="Context.html#898" class="Generalizable">Γ</a><a id="2221" class="Symbol">)</a> <a id="2223" class="Symbol">→</a> <a id="2225" href="Context.html#32488" class="Datatype">Sub</a> <a id="2229" class="Symbol">(</a><a id="2230" href="IK.Term.Base.html#2030" class="Function">lCtxₛ</a> <a id="2236" href="IK.Term.Base.html#2186" class="Bound">e</a> <a id="2238" href="IK.Term.Base.html#2210" class="Bound">s</a><a id="2239" class="Symbol">)</a> <a id="2241" href="Context.html#907" class="Generalizable">ΓL</a>
<a id="2244" href="IK.Term.Base.html#2170" class="Function">factorSubₛ</a> <a id="2255" href="Context.html#7432" class="InductiveConstructor">nil</a>     <a id="2263" class="Symbol">(</a><a id="2264" href="Context.html#32597" class="InductiveConstructor">lock</a> <a id="2269" href="IK.Term.Base.html#2269" class="Bound">s</a> <a id="2271" href="IK.Term.Base.html#2271" class="Bound">e</a><a id="2272" class="Symbol">)</a> <a id="2274" class="Symbol">=</a> <a id="2276" href="IK.Term.Base.html#2269" class="Bound">s</a>
<a id="2278" href="IK.Term.Base.html#2170" class="Function">factorSubₛ</a> <a id="2289" class="Symbol">(</a><a id="2290" href="Context.html#7454" class="InductiveConstructor">ext</a> <a id="2294" href="IK.Term.Base.html#2294" class="Bound">e</a><a id="2295" class="Symbol">)</a> <a id="2297" class="Symbol">(</a><a id="2298" href="IK.Term.Base.html#2298" class="Bound">s</a> <a id="2300" href="Context.html#32540" class="InductiveConstructor Operator">`,</a> <a id="2303" href="IK.Term.Base.html#2303" class="Bound">t</a><a id="2304" class="Symbol">)</a>   <a id="2308" class="Symbol">=</a> <a id="2310" href="IK.Term.Base.html#2170" class="Function">factorSubₛ</a> <a id="2321" href="IK.Term.Base.html#2294" class="Bound">e</a> <a id="2323" href="IK.Term.Base.html#2298" class="Bound">s</a>

<a id="2326" class="Comment">-- &quot;Right&quot; context of factoring with a substitution (see factorExtₛ)</a>
<a id="rCtxₛ"></a><a id="2395" href="IK.Term.Base.html#2395" class="Function">rCtxₛ</a> <a id="2401" class="Symbol">:</a> <a id="2403" class="Symbol">(</a><a id="2404" href="IK.Term.Base.html#2404" class="Bound">e</a> <a id="2406" class="Symbol">:</a> <a id="2408" href="Context.html#8096" class="Function">LFExt</a> <a id="2414" href="Context.html#898" class="Generalizable">Γ</a> <a id="2416" class="Symbol">(</a><a id="2417" href="Context.html#907" class="Generalizable">ΓL</a> <a id="2420" href="Context.html#786" class="InductiveConstructor Operator">#</a><a id="2421" class="Symbol">)</a> <a id="2423" href="Context.html#910" class="Generalizable">ΓR</a><a id="2425" class="Symbol">)</a> <a id="2427" class="Symbol">(</a><a id="2428" href="IK.Term.Base.html#2428" class="Bound">s</a> <a id="2430" class="Symbol">:</a> <a id="2432" href="Context.html#32488" class="Datatype">Sub</a> <a id="2436" href="Context.html#925" class="Generalizable">Δ</a> <a id="2438" href="Context.html#898" class="Generalizable">Γ</a><a id="2439" class="Symbol">)</a> <a id="2441" class="Symbol">→</a> <a id="2443" href="Context.html#719" class="Datatype">Ctx</a>
<a id="2447" href="IK.Term.Base.html#2395" class="Function">rCtxₛ</a> <a id="2453" href="Context.html#7432" class="InductiveConstructor">nil</a>     <a id="2461" class="Symbol">(</a><a id="2462" href="Context.html#32597" class="InductiveConstructor">lock</a> <a id="2467" class="Symbol">{</a><a id="2468" class="Argument">ΔR</a> <a id="2471" class="Symbol">=</a> <a id="2473" href="IK.Term.Base.html#2473" class="Bound">ΔR</a><a id="2475" class="Symbol">}</a> <a id="2477" href="IK.Term.Base.html#2477" class="Bound">s</a> <a id="2479" href="IK.Term.Base.html#2479" class="Bound">e</a><a id="2480" class="Symbol">)</a> <a id="2482" class="Symbol">=</a> <a id="2484" href="IK.Term.Base.html#2473" class="Bound">ΔR</a>
<a id="2487" href="IK.Term.Base.html#2395" class="Function">rCtxₛ</a> <a id="2493" class="Symbol">(</a><a id="2494" href="Context.html#7454" class="InductiveConstructor">ext</a> <a id="2498" href="IK.Term.Base.html#2498" class="Bound">e</a><a id="2499" class="Symbol">)</a> <a id="2501" class="Symbol">(</a><a id="2502" href="IK.Term.Base.html#2502" class="Bound">s</a> <a id="2504" href="Context.html#32540" class="InductiveConstructor Operator">`,</a> <a id="2507" href="IK.Term.Base.html#2507" class="Bound">t</a><a id="2508" class="Symbol">)</a>             <a id="2522" class="Symbol">=</a> <a id="2524" href="IK.Term.Base.html#2395" class="Function">rCtxₛ</a> <a id="2530" href="IK.Term.Base.html#2498" class="Bound">e</a> <a id="2532" href="IK.Term.Base.html#2502" class="Bound">s</a>

<a id="factorExtₛ"></a><a id="2535" href="IK.Term.Base.html#2535" class="Function">factorExtₛ</a> <a id="2546" class="Symbol">:</a> <a id="2548" class="Symbol">∀</a> <a id="2550" class="Symbol">(</a><a id="2551" href="IK.Term.Base.html#2551" class="Bound">e</a> <a id="2553" class="Symbol">:</a> <a id="2555" href="Context.html#8096" class="Function">LFExt</a> <a id="2561" href="Context.html#898" class="Generalizable">Γ</a> <a id="2563" class="Symbol">(</a><a id="2564" href="Context.html#907" class="Generalizable">ΓL</a> <a id="2567" href="Context.html#786" class="InductiveConstructor Operator">#</a><a id="2568" class="Symbol">)</a> <a id="2570" href="Context.html#910" class="Generalizable">ΓR</a><a id="2572" class="Symbol">)</a> <a id="2574" class="Symbol">(</a><a id="2575" href="IK.Term.Base.html#2575" class="Bound">s</a> <a id="2577" class="Symbol">:</a> <a id="2579" href="Context.html#32488" class="Datatype">Sub</a> <a id="2583" href="Context.html#925" class="Generalizable">Δ</a> <a id="2585" href="Context.html#898" class="Generalizable">Γ</a><a id="2586" class="Symbol">)</a> <a id="2588" class="Symbol">→</a> <a id="2590" href="Context.html#8096" class="Function">LFExt</a> <a id="2596" href="Context.html#925" class="Generalizable">Δ</a> <a id="2598" class="Symbol">(</a><a id="2599" href="IK.Term.Base.html#2030" class="Function">lCtxₛ</a> <a id="2605" href="IK.Term.Base.html#2551" class="Bound">e</a> <a id="2607" href="IK.Term.Base.html#2575" class="Bound">s</a> <a id="2609" href="Context.html#786" class="InductiveConstructor Operator">#</a><a id="2610" class="Symbol">)</a> <a id="2612" class="Symbol">(</a><a id="2613" href="IK.Term.Base.html#2395" class="Function">rCtxₛ</a> <a id="2619" href="IK.Term.Base.html#2551" class="Bound">e</a> <a id="2621" href="IK.Term.Base.html#2575" class="Bound">s</a><a id="2622" class="Symbol">)</a>
<a id="2624" href="IK.Term.Base.html#2535" class="Function">factorExtₛ</a> <a id="2635" href="Context.html#7432" class="InductiveConstructor">nil</a>     <a id="2643" class="Symbol">(</a><a id="2644" href="Context.html#32597" class="InductiveConstructor">lock</a> <a id="2649" href="IK.Term.Base.html#2649" class="Bound">s</a> <a id="2651" href="IK.Term.Base.html#2651" class="Bound">e</a><a id="2652" class="Symbol">)</a> <a id="2654" class="Symbol">=</a> <a id="2656" href="IK.Term.Base.html#2651" class="Bound">e</a>
<a id="2658" href="IK.Term.Base.html#2535" class="Function">factorExtₛ</a> <a id="2669" class="Symbol">(</a><a id="2670" href="Context.html#7454" class="InductiveConstructor">ext</a> <a id="2674" href="IK.Term.Base.html#2674" class="Bound">e</a><a id="2675" class="Symbol">)</a> <a id="2677" class="Symbol">(</a><a id="2678" href="IK.Term.Base.html#2678" class="Bound">s</a> <a id="2680" href="Context.html#32540" class="InductiveConstructor Operator">`,</a> <a id="2683" class="Symbol">_)</a>   <a id="2688" class="Symbol">=</a> <a id="2690" href="IK.Term.Base.html#2535" class="Function">factorExtₛ</a> <a id="2701" href="IK.Term.Base.html#2674" class="Bound">e</a> <a id="2703" href="IK.Term.Base.html#2678" class="Bound">s</a>

<a id="2706" class="Comment">-- apply substitution to a term</a>
<a id="substTm"></a><a id="2738" href="IK.Term.Base.html#2738" class="Function">substTm</a> <a id="2746" class="Symbol">:</a> <a id="2748" href="Context.html#32488" class="Datatype">Sub</a> <a id="2752" href="Context.html#925" class="Generalizable">Δ</a> <a id="2754" href="Context.html#898" class="Generalizable">Γ</a> <a id="2756" class="Symbol">→</a> <a id="2758" href="IK.Term.Base.html#524" class="Datatype">Tm</a> <a id="2761" href="Context.html#898" class="Generalizable">Γ</a> <a id="2763" href="Type.html#379" class="Generalizable">a</a> <a id="2765" class="Symbol">→</a> <a id="2767" href="IK.Term.Base.html#524" class="Datatype">Tm</a> <a id="2770" href="Context.html#925" class="Generalizable">Δ</a> <a id="2772" href="Type.html#379" class="Generalizable">a</a>
<a id="2774" href="IK.Term.Base.html#2738" class="Function">substTm</a> <a id="2782" href="IK.Term.Base.html#2782" class="Bound">s</a> <a id="2784" class="Symbol">(</a><a id="2785" href="IK.Term.Base.html#553" class="InductiveConstructor">var</a> <a id="2789" href="IK.Term.Base.html#2789" class="Bound">x</a><a id="2790" class="Symbol">)</a>     <a id="2796" class="Symbol">=</a> <a id="2798" href="Context.html#33110" class="Function">substVar</a> <a id="2807" href="IK.Term.Base.html#2782" class="Bound">s</a> <a id="2809" href="IK.Term.Base.html#2789" class="Bound">x</a>
<a id="2811" href="IK.Term.Base.html#2738" class="Function">substTm</a> <a id="2819" href="IK.Term.Base.html#2819" class="Bound">s</a> <a id="2821" class="Symbol">(</a><a id="2822" href="IK.Term.Base.html#616" class="InductiveConstructor">lam</a> <a id="2826" href="IK.Term.Base.html#2826" class="Bound">t</a><a id="2827" class="Symbol">)</a>     <a id="2833" class="Symbol">=</a> <a id="2835" href="IK.Term.Base.html#616" class="InductiveConstructor">lam</a> <a id="2839" class="Symbol">(</a><a id="2840" href="IK.Term.Base.html#2738" class="Function">substTm</a> <a id="2848" class="Symbol">(</a><a id="2849" href="Context.html#33653" class="Function">keepₛ</a> <a id="2855" href="IK.Term.Base.html#2819" class="Bound">s</a><a id="2856" class="Symbol">)</a> <a id="2858" href="IK.Term.Base.html#2826" class="Bound">t</a><a id="2859" class="Symbol">)</a>
<a id="2861" href="IK.Term.Base.html#2738" class="Function">substTm</a> <a id="2869" href="IK.Term.Base.html#2869" class="Bound">s</a> <a id="2871" class="Symbol">(</a><a id="2872" href="IK.Term.Base.html#697" class="InductiveConstructor">app</a> <a id="2876" href="IK.Term.Base.html#2876" class="Bound">t</a> <a id="2878" href="IK.Term.Base.html#2878" class="Bound">u</a><a id="2879" class="Symbol">)</a>   <a id="2883" class="Symbol">=</a> <a id="2885" href="IK.Term.Base.html#697" class="InductiveConstructor">app</a> <a id="2889" class="Symbol">(</a><a id="2890" href="IK.Term.Base.html#2738" class="Function">substTm</a> <a id="2898" href="IK.Term.Base.html#2869" class="Bound">s</a> <a id="2900" href="IK.Term.Base.html#2876" class="Bound">t</a><a id="2901" class="Symbol">)</a> <a id="2903" class="Symbol">(</a><a id="2904" href="IK.Term.Base.html#2738" class="Function">substTm</a> <a id="2912" href="IK.Term.Base.html#2869" class="Bound">s</a> <a id="2914" href="IK.Term.Base.html#2878" class="Bound">u</a><a id="2915" class="Symbol">)</a>
<a id="2917" href="IK.Term.Base.html#2738" class="Function">substTm</a> <a id="2925" href="IK.Term.Base.html#2925" class="Bound">s</a> <a id="2927" class="Symbol">(</a><a id="2928" href="IK.Term.Base.html#792" class="InductiveConstructor">box</a> <a id="2932" href="IK.Term.Base.html#2932" class="Bound">t</a><a id="2933" class="Symbol">)</a>     <a id="2939" class="Symbol">=</a> <a id="2941" href="IK.Term.Base.html#792" class="InductiveConstructor">box</a> <a id="2945" class="Symbol">(</a><a id="2946" href="IK.Term.Base.html#2738" class="Function">substTm</a> <a id="2954" class="Symbol">(</a><a id="2955" href="Context.html#33764" class="Function">keep#ₛ</a> <a id="2962" href="IK.Term.Base.html#2925" class="Bound">s</a><a id="2963" class="Symbol">)</a> <a id="2965" href="IK.Term.Base.html#2932" class="Bound">t</a><a id="2966" class="Symbol">)</a>
<a id="2968" href="IK.Term.Base.html#2738" class="Function">substTm</a> <a id="2976" href="IK.Term.Base.html#2976" class="Bound">s</a> <a id="2978" class="Symbol">(</a><a id="2979" href="IK.Term.Base.html#868" class="InductiveConstructor">unbox</a> <a id="2985" href="IK.Term.Base.html#2985" class="Bound">t</a> <a id="2987" href="IK.Term.Base.html#2987" class="Bound">e</a><a id="2988" class="Symbol">)</a> <a id="2990" class="Symbol">=</a> <a id="2992" href="IK.Term.Base.html#868" class="InductiveConstructor">unbox</a> <a id="2998" class="Symbol">(</a><a id="2999" href="IK.Term.Base.html#2738" class="Function">substTm</a> <a id="3007" class="Symbol">(</a><a id="3008" href="IK.Term.Base.html#2170" class="Function">factorSubₛ</a> <a id="3019" href="IK.Term.Base.html#2987" class="Bound">e</a> <a id="3021" href="IK.Term.Base.html#2976" class="Bound">s</a><a id="3022" class="Symbol">)</a> <a id="3024" href="IK.Term.Base.html#2985" class="Bound">t</a><a id="3025" class="Symbol">)</a> <a id="3027" class="Symbol">(</a><a id="3028" href="IK.Term.Base.html#2535" class="Function">factorExtₛ</a> <a id="3039" href="IK.Term.Base.html#2987" class="Bound">e</a> <a id="3041" href="IK.Term.Base.html#2976" class="Bound">s</a><a id="3042" class="Symbol">)</a>

<a id="3045" class="Keyword">open</a> <a id="3050" href="IK.Term.Base.html#1921" class="Module">SubstitutionComposition</a> <a id="3074" href="IK.Term.Base.html#2738" class="Function">substTm</a> <a id="3082" href="IK.Term.Base.html#2030" class="Function">lCtxₛ</a> <a id="3088" href="IK.Term.Base.html#2170" class="Function">factorSubₛ</a> <a id="3099" href="IK.Term.Base.html#2395" class="Function">rCtxₛ</a> <a id="3105" href="IK.Term.Base.html#2535" class="Function">factorExtₛ</a> <a id="3116" class="Keyword">public</a>
</pre></body></html>