module IK.Soundness.HellOfSemanticLemmas where

open import Data.Unit  using (âŠ¤ ; tt)
open import Data.Product  using (Î£ ; _Ã—_ ; _,_ ; projâ‚ ; projâ‚‚)
open import Relation.Binary.PropositionalEquality

import Context
open import IK.Term
open import IK.Norm
open import IK.HellOfSyntacticLemmas

postulate
  funext  : âˆ€{i j}{A : Set i}{B : A â†’ Set j}{f g : (x : A) â†’ B x}
          â†’ ((x : A) â†’ f x â‰¡ g x) â†’ f â‰¡ g

  funexti : âˆ€{i j}{A : Set i}{B : A â†’ Set j}{f g : {x : A} â†’ B x}
          â†’ ((x : A) â†’ f {x} â‰¡ g {x}) â†’ _â‰¡_ {A = {x : A} â†’ B x} f g
          
-- semantic counterpart of trimSub
trimSub' : Î“' â‰¤ Î“ â†’ Sub'- Î“' â†’Ì‡ Sub'- Î“
trimSub' base      tt         = tt
trimSub' (drop w)  (s , _)    = trimSub' w s
trimSub' (keep w)  (s , x)    = trimSub' w s , x
trimSub' (keepğŸ”’ w) (lock s e) = lock (trimSub' w s) e

-- naturality of trimSub'
nat-trimSub' : (w' : Î” â‰¤ Î”') (w : Î“' â‰¤ Î“) (s : Sub' Î“ Î”)
  â†’ trimSub' w' (wkSub' w s) â‰¡ wkSub' w (trimSub' w' s)
nat-trimSub' base       w s          = refl
nat-trimSub' (drop w')  w (s , _)    = nat-trimSub' w' w s
nat-trimSub' (keep w')  w (s , x)    = congâ‚‚ _,_ (nat-trimSub' w' w s) refl
nat-trimSub' (keepğŸ”’ w') w (lock s e) = congâ‚‚ lock (nat-trimSub' w' (sliceLeft e w) s) refl

-- trimSub' preserves identity
trimSub'PresId : (s : Sub' Î“ Î”) â†’ trimSub' idWk s â‰¡ s
trimSub'PresId {Î” = []}     tt         = refl
trimSub'PresId {Î” = Î” `, _} (s , _)    = congâ‚‚ _,_ (trimSub'PresId s) refl
trimSub'PresId {Î” = Î” ğŸ”’}    (lock s e) = congâ‚‚ lock (trimSub'PresId s) refl

-- semantic counterpart of coh-trimSub-wkVar in Substitution.agda
coh-trimSub'-wkVar' : (w : Î“' â‰¤ Î“) (s : Sub' Î” Î“') (x : Var Î“ a)
  â†’ substVar' (wkVar w x) s â‰¡ substVar' x (trimSub' w s)
coh-trimSub'-wkVar' (drop w) (s , _) ze     = coh-trimSub'-wkVar' w s ze
coh-trimSub'-wkVar' (drop w) (s , _) (su x) = coh-trimSub'-wkVar' w s (su x)
coh-trimSub'-wkVar' (keep w) (s , _) ze     = refl
coh-trimSub'-wkVar' (keep w) (s , _) (su x) = coh-trimSub'-wkVar' w s x

-- semantic counterpart of coh-trimSub-wkTm in HellOfSyntacticLemmas.agda
coh-trimSub'-wkTm : (w : Î“' â‰¤ Î“) (s : Sub' Î” Î“') (t : Tm Î“ a)
  â†’ eval (wkTm w t) s â‰¡ eval t (trimSub' w s)
coh-trimSub'-wkTm w s (var x)
  = coh-trimSub'-wkVar' w s x
coh-trimSub'-wkTm w s (lam t)
  = funexti (Î» _ â†’ funext (Î» w' â†’ funext (Î» x â†’
      trans
        (coh-trimSub'-wkTm (keep w) (wkSub' w' s , x) t)
        (cong (Î» z â†’ eval t (z , x)) (nat-trimSub' w w' s)))))
coh-trimSub'-wkTm w s (app t u)
  = trans
      (cong (Î» f â†’ f idWk (eval (wkTm w u) s)) (coh-trimSub'-wkTm w s t))
      (cong (eval t (trimSub' w s) idWk) (coh-trimSub'-wkTm w s u))
coh-trimSub'-wkTm w s (box t)
  = cong box (coh-trimSub'-wkTm (keepğŸ”’ w) (lock s nil) t)
coh-trimSub'-wkTm (drop w) (s , _) (unbox t e)
  = coh-trimSub'-wkTm w s (unbox t e)
coh-trimSub'-wkTm (keepğŸ”’ w) (lock s e) (unbox t nil)
  = congâ‚‚ unbox' (coh-trimSub'-wkTm w s t) refl
coh-trimSub'-wkTm (keep w) (s , _) (unbox t (ext e))
  = coh-trimSub'-wkTm w s (unbox t e)

-- semantic counterpart of coh-trimSub-wkSub in `HellOfSyntacticLemmas.agda`
coh-trimSub'-wkSub : (w : Î“' â‰¤ Î“) (s : Sub Î“ Î”) (s' : Sub' Î”' Î“')
  â†’ evalâ‚› (wkSub w s) s' â‰¡ evalâ‚› s (trimSub' w s')
coh-trimSub'-wkSub w [] s'
  = refl
coh-trimSub'-wkSub w (s `, t) s'
  = congâ‚‚ _,_ (coh-trimSub'-wkSub w s s') (coh-trimSub'-wkTm w s' t)
coh-trimSub'-wkSub (drop w) (lock s e) (s' , _)
  = coh-trimSub'-wkSub w (lock s e) s'
coh-trimSub'-wkSub (keep w) (lock s (ext e)) (s' , _)
  = coh-trimSub'-wkSub w (lock s e) s'
coh-trimSub'-wkSub (keepğŸ”’ w) (lock s nil) (lock s' e')
  = congâ‚‚ lock (coh-trimSub'-wkSub w s s') refl

-- evalâ‚› preserves identity
evalâ‚›PresId : (s' : Sub' Î“ Î”) â†’ evalâ‚› idâ‚› s' â‰¡ s'
evalâ‚›PresId {Î” = []}     tt
  = refl
evalâ‚›PresId {Î” = Î” `, _} (s' , x)
  = congâ‚‚ (_,_)
          (trans
            (coh-trimSub'-wkSub fresh idâ‚› (s' , x))
            (trans
              (cong (evalâ‚› idâ‚›) (trimSub'PresId s'))
              (evalâ‚›PresId s')))
          refl
evalâ‚›PresId {Î” = Î” ğŸ”’} (lock s' e)
  = congâ‚‚ lock (evalâ‚›PresId s') refl
