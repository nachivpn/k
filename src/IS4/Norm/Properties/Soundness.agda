{-# OPTIONS --safe --with-K #-}
module IS4.Norm.Properties.Soundness where

open import Data.Unit    using (âŠ¤ ; tt)
open import Data.Product using (Î£ ; _Ã—_ ; _,_ ; -,_)

open import Relation.Binary.PropositionalEquality
  using (_â‰¡_ ; refl ; sym ; trans ; subst ; substâ‚‚ ; cong ; congâ‚‚ ; module â‰¡-Reasoning)

open import HEUtil

open import IS4.Norm.Base

open import IS4.Norm.NbE.Model
open import IS4.Norm.NbE.Reification

open import IS4.Term

quotTm : Tm' Î“ a â†’ Tm Î“ a
quotTm x = embNf (reify _ x)

-----------------------
-- Logical Relations --
-----------------------

Rt : {a : Ty} {Î“ : Ctx} â†’ (t : Tm Î“ a) â†’ (x : Tm' Î“ a) â†’ Set
Rt {ğ•“}          t x =
  t â‰ˆ quotTm x
Rt {a â‡’ b} {Î“}  t f =
  {Î“' : Ctx} {u : Tm Î“' a} {x : Tm' Î“' a}
    â†’ (e : Î“ âŠ† Î“') â†’ Rt u x â†’ Rt (app (wkTm e t) u) (f .apply e x)
Rt {â–¡ a}  {Î“L} t bx =
  {Î“L' Î“ Î“R : Ctx}
    â†’ (w : Î“L âŠ† Î“L') â†’ (e : CExt Î“ Î“L' Î“R) â†’ Rt (unbox (wkTm w t) e) (bx .apply w (-, e))

data Rs : Sub Î“ Î” â†’ Sub' Î“ Î” â†’ Set where
  []   : Rs {Î“} [] tt
  _`,_ : {s : Sub Î” Î“} {s' : Sub' Î” Î“} {t : Tm Î” a} {x : Tm' Î” a}
       â†’ Rs s s' â†’ Rt t x â†’ Rs (s `, t)  (elem (s' , x))
  lock : {s : Sub Î”L Î“} {s' : Sub' Î”L Î“}
    â†’ Rs s s' â†’ (e : CExt Î” Î”L (Î”R)) â†’ Rs (lock s e) (elem (Î”L , (Î”R , e) , s'))

----------------------------
-- Standard LR properties --
----------------------------

-- prepend a reduction trace to the "trace builder" Rt
Rt-prepend : {t u : Tm Î“ a} {x : Tm' Î“ a}
  â†’ t â‰ˆ u
  â†’ Rt u x
  â†’ Rt t x
Rt-prepend {a = ğ•“} r uRx
  = â‰ˆ-trans r uRx
Rt-prepend {a = a â‡’ b} r uRx
  = Î» w uRy â†’ Rt-prepend (cong-appâ‰ˆ (wkTmPresâ‰ˆ w r) â‰ˆ-refl) (uRx w uRy)
Rt-prepend {a = â–¡ a} {t = t} {u} {x = bx} r uRbx
  = Î» w e â†’ Rt-prepend (cong-unboxâ‰ˆ (wkTmPresâ‰ˆ w r)) (uRbx w e)

-- reduction-free version of Rt-prepend
Rt-cast : {t u : Tm Î“ a} {x y : Tm' Î“ a}
  â†’ t â‰¡ u
  â†’ y â‰¡ x
  â†’ Rt u x
  â†’ Rt t y
Rt-cast p refl uRx = Rt-prepend (â‰ˆ-reflexive p) uRx

-- extract reduction trace from Rt
Rt-build : {t : Tm Î“ a} {x : Tm' Î“ a}
  â†’ Rt t x â†’ t â‰ˆ quotTm x
-- a neutral element is related to its reflection
Rt-reflect : (n : Ne Î“ a)
  â†’ Rt (embNe n) (reflect a n)

Rt-build {a = ğ•“}     r
  = r
Rt-build {a = a â‡’ b} tRx
  = â‰ˆ-trans (âŸ¶-to-â‰ˆ (exp-fun _)) (cong-lamâ‰ˆ (Rt-build (tRx _ (Rt-reflect (var ze)))))
Rt-build {a = â–¡ a}  tRx
  = â‰ˆ-trans (âŸ¶-to-â‰ˆ (exp-box _)) (cong-boxâ‰ˆ (Rt-build (Rt-cast (congâ‚‚ unbox (sym (wkTmPresId _)) refl) refl (tRx idWk new))))

Rt-reflect {a = ğ•“}     n
  = â‰ˆ-refl
Rt-reflect {a = a â‡’ b} n
  = Î» w y â†’ Rt-prepend (cong-appâ‰ˆ (â‰ˆ-reflexive (nat-embNe _ _)) (Rt-build y)) (Rt-reflect _ )
Rt-reflect {a = â–¡ a}   n
  = Î» w e â†’ Rt-prepend (cong-unboxâ‰ˆ (â‰ˆ-reflexive (nat-embNe _ _))) (Rt-reflect _)

-- Rt is invariant under weakening
wkTmPresRt : {t : Tm Î“ a} {x : Tm' Î“ a}
  â†’ (w : Î“ âŠ† Î”)
  â†’ Rt t x
  â†’ Rt (wkTm w t) (wkTm' a w x)
wkTmPresRt {a = ğ•“}  {x = x}       w tRx
  = â‰ˆ-trans (wkTmPresâ‰ˆ _ tRx) (â‰ˆ-reflexive (nat-embNf _ (reify _ x)))
wkTmPresRt {a = a â‡’ b}            w tRx
  = Î» w' y â†’ Rt-cast (congâ‚‚ app (wkTmPresâˆ™ _ _ _) refl) refl (tRx (w âˆ™ w') y)
wkTmPresRt {a = â–¡ a} w tRx
  = Î» w' e â†’ Rt-cast (congâ‚‚ unbox (wkTmPresâˆ™ _ _ _) refl) refl (tRx (w âˆ™ w') e)

-- Rs is invariant under weakening
wkSubPresRs : {s : Sub Î” Î“} {s' : Sub' Î” Î“}
  â†’ (w : Î” âŠ† Î”')
  â†’ Rs s s'
  â†’ Rs (wkSub w s) (wkSub' Î“ w s')
wkSubPresRs {Î“ = []}     {s = []}      {tt}     w sRs'
  = []
wkSubPresRs {Î“ = Î“ `, _} {s = s `, t} {elem (s' , x)} w (sRs' `, tRx)
  = wkSubPresRs {Î“ = Î“} w sRs' `, wkTmPresRt w tRx
wkSubPresRs {Î“ = Î“ ğŸ”’} {s = lock s e} {elem (Î“L , (Î“R , .e) , s')} w (lock x .e)
  = lock (wkSubPresRs (factorWk e w) x) (factorExt e w)

-- syntactic identity is related to semantic identity
idRs : Rs {Î“} idâ‚› (idâ‚›' Î“)
idRs {[]}     = []
idRs {Î“ `, x} = wkSubPresRs fresh idRs `, Rt-reflect (var ze)
idRs {Î“ ğŸ”’}    = lock idRs new

-----------------------------
-- The Fundamental Theorem --
-----------------------------

-- local lemmas for the proof of fundamental theorem
private

  substVarPresRt : (x : Var Î“ a) {s : Sub Î” Î“} {s'  : Sub' Î” Î“}
    â†’ Rs s s'
    â†’ Rt (substVar s x) (substVar' x s')
  substVarPresRt ze {_ `, x} {elem (_ , x')} (_ `, xRx')
    = xRx'
  substVarPresRt (su x) {s `, _} {elem (s' , _)} (sRs' `, _)
    = substVarPresRt x sRs'

  beta-lemma : (w : Î” âŠ† Î“')  (s : Sub Î” Î“) (t : Tm (Î“ `, a) b) (u : Tm Î“' a)
    â†’ app (wkTm w (substTm s (lam t))) u â‰ˆ substTm (wkSub w s `, u) t
  beta-lemma w s t u = â‰ˆ-trans (â‰ˆ-reflexive (congâ‚‚ app (cong lam (trans
    (sym (nat-substTm t (keepâ‚› s) (keep w)))
    (cong (Î» p â†’ substTm (p `, var ze) t)
      (trans
        (wkSubPresâˆ™ (fresh) (keep w) s)
        (congâ‚‚ wkSub (cong drop (leftIdWk w)) refl))))) refl))
    (â‰ˆ-trans
      (âŸ¶-to-â‰ˆ (red-fun _ _))
      (â‰ˆ-trans
        (â‰ˆ-reflexive (substTmPresâˆ™ _ _ t))
        (substTmPresâ‰ˆ t
          (cong-`,â‰ˆâ‚›
            (â‰ˆâ‚›-trans
              (â‰ˆâ‚›-reflexiveË˜ (coh-trimSub-wkSub s _ _))
              (â‰ˆâ‚›-trans (â‰ˆâ‚›-reflexive (coh-trimSub-wkSub s idâ‚› w)) (â‰ˆâ‚›-sym (rightIdSub _))))
            â‰ˆ-refl))))

  unboxPresRt : {t : Tm Î“ (â–¡ a)} {x : (Tm'- (â–¡ a)) Î“}
    â†’ (e : CExt Î“' Î“ Î“R)
    â†’ (e' : CExt Î“' Î“ Î“R)
    â†’ Rt t x
    â†’ Rt (unbox t e) (unbox' {a = a} x e')
  unboxPresRt {t = t} {bx} e e' r rewrite ExtIsProp e' e
    = Rt-cast (congâ‚‚ unbox (sym (wkTmPresId t)) refl) refl (r idWk e)

-- The Fundamental theorem, for terms


Fund : Tm Î“ a â†’ (Sub'- Î“ â†’Ì‡ Tm'- a) â†’ Set
Fund {Î“} t f = âˆ€ {Î”} {s : Sub Î” Î“} {s' : Sub' Î” Î“}
    â†’ Rs s s'
    â†’ Rt (substTm s t) (f s')

lCtxâ‚›'âˆ¼lCtxâ‚› : (e : CExt Î“ Î“L Î“R) {s : Sub Î” Î“} {s' : Sub' Î” Î“} â†’ Rs s s' â†’ lCtxâ‚›' e s' â‰¡ lCtxâ‚› e s
lCtxâ‚›'âˆ¼lCtxâ‚› nil       sRs'          = refl
lCtxâ‚›'âˆ¼lCtxâ‚› (ext e)   (sRs' `, _)   = lCtxâ‚›'âˆ¼lCtxâ‚› e sRs'
lCtxâ‚›'âˆ¼lCtxâ‚› (extğŸ”’- e) (lock sRs' _) = lCtxâ‚›'âˆ¼lCtxâ‚› e sRs'

rCtxâ‚›'âˆ¼rCtxâ‚› : (e : CExt Î“ Î“L Î“R) {s : Sub Î” Î“} {s' : Sub' Î” Î“} â†’ Rs s s' â†’  rCtxâ‚›' e s' â‰¡ rCtxâ‚› e s
rCtxâ‚›'âˆ¼rCtxâ‚› nil       sRs'          = refl
rCtxâ‚›'âˆ¼rCtxâ‚› (ext e)   (sRs' `, x)   = rCtxâ‚›'âˆ¼rCtxâ‚› e sRs'
rCtxâ‚›'âˆ¼rCtxâ‚› (extğŸ”’- e) (lock sRs' _) = cong (_,, _) (rCtxâ‚›'âˆ¼rCtxâ‚› e sRs')

factorSubPresRs : (e : CExt Î“ Î“L Î“R) {s : Sub Î” Î“} {s' : Sub' Î” Î“}
    â†’ (sRs' : Rs s s')
    â†’ Rs (factorSubâ‚› e s) (subst (Î» Î”L â†’ Sub' Î”L Î“L) (lCtxâ‚›'âˆ¼lCtxâ‚› e sRs') (factorSubâ‚›' e s'))
factorSubPresRs nil       sRs'           = sRs'
factorSubPresRs (ext e)   (sRs' `, _)    = factorSubPresRs e sRs'
factorSubPresRs (extğŸ”’- e) (lock sRs' _) = factorSubPresRs e sRs'

factorExtâ‚›'âˆ¼factorExtâ‚› : (e : CExt Î“ Î“L Î“R) {s : Sub Î” Î“} {s' : Sub' Î” Î“}
  â†’ (sRs' : Rs s s')
  â†’ factorExtâ‚› e s â‰¡ substâ‚‚ (CExt _) (lCtxâ‚›'âˆ¼lCtxâ‚› e sRs') (rCtxâ‚›'âˆ¼rCtxâ‚› e sRs') (factorExtâ‚›' e s')
factorExtâ‚›'âˆ¼factorExtâ‚› _ _ = ExtIsProp _ _

fund : (t : Tm Î“ a) â†’ Fund t (eval t)
fund (var x)     {s = s} {s'} sRs'
  = substVarPresRt x sRs'
fund (lam t)     {s = s} {s'} sRs' {u = u}
  = Î» w uRx â†’ Rt-prepend (beta-lemma w s t u)
  (fund t {s = wkSub w s `, u} (wkSubPresRs w sRs' `, uRx))
fund (app t u)   {s = s} {s'} sRs'
  = Rt-cast
      (congâ‚‚ app (sym (wkTmPresId _)) refl)
      refl
      (fund t sRs' idWk (fund u sRs'))
fund {Î“ = Î“} (box {a = a} t)    {s = s} {s'} sRs' {Î“ = Î“'} {Î“R = Î“R} w e
  = Rt-prepend unbox-box-reduces (fund t (lock (wkSubPresRs w sRs') e))
  where
  --
  lockLemma : lock (wkSub w s âˆ™â‚› idâ‚›) (extRAssoc nil e) â‰ˆâ‚› lock (wkSub w s) e
  lockLemma = â‰ˆâ‚›-trans
    (cong-lockâ‰ˆâ‚› (â‰ˆâ‚›-sym (rightIdSub _)))
    (â‰ˆâ‚›-reflexive
      (trans
        (congâ‚‚ lock refl extLeftUnit)
        (â‰…-to-â‰¡ (â‰…-cong (CExt _ _) ,,-leftUnit (lock _) (â‰¡-subst-removable (CExt _ _) _ e)))))
  --
  unbox-box-reduces : unbox (wkTm w (substTm s (box t))) e â‰ˆ substTm (lock (wkSub w s) e) t
  unbox-box-reduces = begin
    unbox (wkTm w (substTm s (box t))) e
      â‰¡âŸ¨âŸ©
    unbox (box (wkTm (keepğŸ”’ w) (substTm (lock s new) t))) e
      â‰ˆâŸ¨ âŸ¶-to-â‰ˆ (red-box _ _) âŸ©
    substTm (lock idâ‚› e) (wkTm (keepğŸ”’ w) (substTm (lock s new) t))
      â‰¡âŸ¨ cong (substTm _) (sym (nat-substTm t _ _))  âŸ©
    substTm (lock idâ‚› e) (substTm (wkSub (keepğŸ”’ w) (lock s new)) t)
      â‰¡âŸ¨ substTmPresâˆ™ _ _ t âŸ©
    substTm ((wkSub (keepğŸ”’ w) (lock s new)) âˆ™â‚› (lock idâ‚› e) ) t
      â‰¡âŸ¨âŸ©
    substTm (lock (wkSub w s âˆ™â‚› idâ‚›) (extRAssoc nil e)) t
      â‰ˆâŸ¨ substTmPresâ‰ˆ t lockLemma âŸ©
    substTm (lock (wkSub w s) e) t âˆ
    where
    open import Relation.Binary.Reasoning.Setoid (Tm-setoid Î“' a)

fund (unbox {Î“L = Î“L} t e) {s = s} {s'} sRs'
  = Rt-cast
      (congâ‚‚ unbox (sym (wkTmPresId _)) (factorExtâ‚›'âˆ¼factorExtâ‚› e sRs'))
      sameEval
      (fund t
        {s = factorSubâ‚› e s}
        {s' = subst (Î» Î” â†’ Sub' Î” Î“L) (lCtxâ‚›'âˆ¼lCtxâ‚› e sRs') (factorSubâ‚›' e s')}
        (factorSubPresRs e sRs')
        idWk[ lCtxâ‚› e s ]
        (substâ‚‚ (CExt _) (lCtxâ‚›'âˆ¼lCtxâ‚› e sRs') (rCtxâ‚›'âˆ¼rCtxâ‚› e sRs') (factorExtâ‚›' e s')))
    where
    --
    sameEval : eval t _ .apply _ _ â‰¡ eval t _ .apply _ _
    sameEval = begin
      eval t
        (factorSubâ‚›' e s')
        .apply
        idWk[ lCtxâ‚›' e s' ]
        (-, factorExtâ‚›' e s')
        -- add substs
        â‰…âŸ¨ evalt-congâ‰… (lCtxâ‚›'âˆ¼lCtxâ‚› e sRs') (rCtxâ‚›'âˆ¼rCtxâ‚› e sRs')
          (â‰¡-subst-addable _ _ _)
          (â‰¡-substâ‚‚-addable _ _ _ _)
          (â‰¡-substâ‚‚-addable _ _ _ _) âŸ©
      eval t
        (subst (Î» Î”â‚ â†’ Sub' Î”â‚ Î“L) (lCtxâ‚›'âˆ¼lCtxâ‚› e sRs') (factorSubâ‚›' e s'))
        .apply
        (substâ‚‚ (_âŠ†_) (lCtxâ‚›'âˆ¼lCtxâ‚› e sRs') (lCtxâ‚›'âˆ¼lCtxâ‚› e sRs') idWk[ lCtxâ‚›' e s' ])
        (-, substâ‚‚ (CExt _) (lCtxâ‚›'âˆ¼lCtxâ‚› e sRs') (rCtxâ‚›'âˆ¼rCtxâ‚› e sRs') (factorExtâ‚›' e s'))
        -- remove substâ‚‚ from idWk
        â‰¡âŸ¨ evalt-congâ‰¡ refl remSubstFromIdWk refl âŸ©
      eval t
        (subst (Î» Î”â‚ â†’ Sub' Î”â‚ Î“L) (lCtxâ‚›'âˆ¼lCtxâ‚› e sRs') (factorSubâ‚›' e s'))
        .apply
        idWk[ lCtxâ‚› e s ]
        (-, substâ‚‚ (CExt _) (lCtxâ‚›'âˆ¼lCtxâ‚› e sRs') (rCtxâ‚›'âˆ¼rCtxâ‚› e sRs') (factorExtâ‚›' e s')) âˆ
      where
      open â‰¡-Reasoning
      --
      remSubstFromIdWk : substâ‚‚ (_âŠ†_) (lCtxâ‚›'âˆ¼lCtxâ‚› e sRs') (lCtxâ‚›'âˆ¼lCtxâ‚› e sRs') idWk[ lCtxâ‚›' e s' ] â‰¡ idWk[ lCtxâ‚› e s ]
      remSubstFromIdWk rewrite lCtxâ‚›'âˆ¼lCtxâ‚› e {s} {s'} sRs' = refl
      -- â‰…-congruence for `eval t`
      evalt-congâ‰… :  {Î”L1 Î”L2 Î”R1 Î”R2 : Ctx} â†’
        Î”L1 â‰¡ Î”L2 â†’ Î”R1 â‰¡ Î”R2 â†’
        {s1 : Sub' Î”L1 Î“L} {s2 : Sub' Î”L2 Î“L}
        {w1 : Î”L1 âŠ† Î”L1} {w2 : Î”L2 âŠ† Î”L2}
        {e1 : CExt Î” Î”L1 Î”R1} {e2 : CExt Î” Î”L2 Î”R2} â†’
        s1 â‰… s2 â†’
        w1 â‰… w2 â†’
        e1 â‰… e2 â†’
        eval t s1 .apply w1 (-, e1) â‰… eval t s2 .apply w2 (-, e2)
      evalt-congâ‰… refl refl â‰…-refl â‰…-refl â‰…-refl = â‰…-refl
      -- â‰¡-congruence for `eval t`
      evalt-congâ‰¡ :  {Î”L Î”R : Ctx} â†’
        {s1 s2 : Sub' Î”L Î“L} {w1 w2 : Î”L âŠ† Î”L}
        {e1 e2 : CExt Î” Î”L Î”R} â†’
        s1 â‰¡ s2 â†’
        w1 â‰¡ w2 â†’
        e1 â‰¡ e2 â†’
        eval t s1 .apply w1 (-, e1) â‰¡ eval t s2 .apply w2 (-, e2)
      evalt-congâ‰¡ refl refl refl = refl

-- reduction trace for norm
trace : (t : Tm Î“ a) â†’ t â‰ˆ embNf (norm t)
trace {Î“} t = Rt-build (Rt-prepend (substTmPresId t) (fund t {s = idâ‚›} {s' = idâ‚›' Î“} idRs))

norm-sound : norm t â‰¡ norm u â†’ t â‰ˆ u
norm-sound {t = t} {u} t'â‰¡u' = â‰ˆ-trans (trace t) (â‰¡-â‰ˆ-transË˜ (cong embNf t'â‰¡u') (trace u))
