module Context (Ty : Set) where

open import Relation.Binary.PropositionalEquality
  using (_â‰¡_ ; cong ; sym ; trans)

open _â‰¡_

private
  variable
    a b c d : Ty

infixl 4 _ğŸ”’
infix  3 _â‰¤_
infix  3 _,,_

open import Data.Empty using (âŠ¥)
open import Data.Unit  using (âŠ¤ ; tt)
open import Data.Product  using (Î£ ; _Ã—_ ; _,_ ; âˆƒ ; âˆƒâ‚‚)

-----------
-- Contexts
-----------

data Ctx : Set where
  []   : Ctx
  _`,_ : Ctx â†’ Ty â†’ Ctx
  _ğŸ”’   : Ctx â†’ Ctx

[ğŸ”’] : Ctx
[ğŸ”’] = [] ğŸ”’

variable
  Î“ Î” Î“' Î”' Î“L Î“R : Ctx

-- append contexts (++)
_,,_ : Ctx â†’ Ctx â†’ Ctx
Î“ ,, []       = Î“
Î“ ,, (Î” `, x) = (Î“ ,, Î”) `, x
Î“ ,, (Î” ğŸ”’)    = (Î“ ,, Î”) ğŸ”’

-- contexts free of locks
ğŸ”’-free : Ctx â†’ Set
ğŸ”’-free []       = âŠ¤
ğŸ”’-free (Î“ `, x) = ğŸ”’-free Î“
ğŸ”’-free (Î“ ğŸ”’)    = âŠ¥

-- context to left of the last lock
â†ğŸ”’ : Ctx â†’ Ctx
â†ğŸ”’ []       = []
â†ğŸ”’ (Î“ `, x) = â†ğŸ”’ Î“
â†ğŸ”’ (Î“ ğŸ”’)    = Î“

-- context to the right of the last lock
ğŸ”’â†’ : Ctx â†’ Ctx
ğŸ”’â†’ []       = []
ğŸ”’â†’ (Î“ `, x) = ğŸ”’â†’ Î“ `, x
ğŸ”’â†’ (Î“ ğŸ”’)    = []


-------------
-- Weakenings
-------------

-- weakening relation
data _â‰¤_  : Ctx â†’ Ctx â†’ Set where
  base   : [] â‰¤ []
  drop   : Î“ â‰¤ Î” â†’ (Î“ `, a) â‰¤ Î”
  keep   : Î“ â‰¤ Î” â†’ (Î“ `, a) â‰¤ (Î” `, a)
  keepğŸ”’  : Î“ â‰¤ Î” â†’ Î“ ğŸ”’ â‰¤ Î” ğŸ”’

{-
  Notes on _â‰¤_:

  In addition to the regular definition of weakening (`base`, `drop` and `keep`),
  we also allow weakening in the presence of locks:

  - `keepğŸ”’` allows weakening under locks

  Disallowing weakening with locks in general ensures that values
  that depend on "local" assumptions cannot be boxed by simply
  weakening with locks.

-}

-- weakening is reflexive
idWk : Î“ â‰¤ Î“
idWk {[]}     = base
idWk {Î“ `, x} = keep idWk
idWk {Î“ ğŸ”’}    = keepğŸ”’ idWk

-- weakening is transitive (or can be composed)
_âˆ™_ : {Î£ : Ctx} â†’ Î” â‰¤ Î£ â†’ Î“ â‰¤ Î” â†’ Î“ â‰¤ Î£
w       âˆ™ base     = w
w       âˆ™ drop w'  = drop (w âˆ™ w')
drop w  âˆ™ keep w'  = drop (w âˆ™ w')
keep w  âˆ™ keep w'  = keep (w âˆ™ w')
keepğŸ”’ w âˆ™ keepğŸ”’ w' = keepğŸ”’ (w âˆ™ w')

-- weakening that "generates a fresh variable"
fresh : (Î“ `, a) â‰¤ Î“
fresh = drop idWk

variable
  Î“L' Î“R' Î“'' Î“L'' Î“R'' : Ctx
  Î”L Î”R : Ctx

data Flag : Set where tt ff : Flag

variable
  Î¸ : Flag

-- with locks?
WL : Flag â†’ Set
WL tt = âŠ¤
WL ff = âŠ¥

------------
-- Variables
------------

data Var : Ctx â†’ Ty â†’ Set where
  ze : Var (Î“ `, a) a
  su : (v : Var Î“ a) â†’ Var (Î“ `, b) a

wkVar : Î“' â‰¤ Î“ â†’ Var Î“ a â†’ Var Î“' a
wkVar (drop e) ze     = su (wkVar e ze)
wkVar (keep e) ze     = ze
wkVar (drop e) (su v) = su (wkVar e (su v))
wkVar (keep e) (su v) = su (wkVar e v)

wkVarPresId : (x : Var Î“ a) â†’ wkVar idWk x â‰¡ x
wkVarPresId ze = refl
wkVarPresId (su x) = cong su (wkVarPresId x)

-- weakening a variable index increments
wkIncr : (x : Var Î“ a) â†’ wkVar (fresh {a = b}) x â‰¡ su x
wkIncr ze = refl
wkIncr (su x) = cong su (cong su (wkVarPresId x))

-- weakening of variables (a functor map) preserves weakening composition
wkVarPresâˆ™ : (w : Î“' â‰¤ Î“) (w' : Î” â‰¤ Î“') (x : Var Î“ a)
  â†’ wkVar w' (wkVar w x) â‰¡ wkVar (w âˆ™ w') x
wkVarPresâˆ™ (drop w) (drop w') ze     = cong su (wkVarPresâˆ™ (drop w) w' ze)
wkVarPresâˆ™ (drop w) (keep w') ze     = cong su (wkVarPresâˆ™ w w' ze)
wkVarPresâˆ™ (keep w) (drop w') ze     = cong su (wkVarPresâˆ™ (keep w) w' ze)
wkVarPresâˆ™ (keep w) (keep w') ze     = refl
wkVarPresâˆ™ (drop w) (drop w') (su x) = cong su (wkVarPresâˆ™ (drop w) w' (su x))
wkVarPresâˆ™ (drop w) (keep w') (su x) = cong su (wkVarPresâˆ™ w w' (su x))
wkVarPresâˆ™ (keep w) (drop w') (su x) = cong su (wkVarPresâˆ™ (keep w) w' (su x))
wkVarPresâˆ™ (keep w) (keep w') (su x) = cong su (wkVarPresâˆ™ w w' x)

-- weakening composition obeys the left identity law
leftIdWk : (w : Î“ â‰¤ Î“') â†’ idWk âˆ™ w â‰¡ w
leftIdWk base      = refl
leftIdWk (drop w)  = cong drop (leftIdWk w)
leftIdWk (keep w)  = cong keep (leftIdWk w)
leftIdWk (keepğŸ”’ w) = cong keepğŸ”’ (leftIdWk w)

-- weakening composition obeys the right identity law
rightIdWk : (w : Î“ â‰¤ Î“') â†’ w âˆ™ idWk â‰¡ w
rightIdWk base      = refl
rightIdWk (drop w)  = cong drop (rightIdWk w)
rightIdWk (keep w)  = cong keep (rightIdWk w)
rightIdWk (keepğŸ”’ w) = cong keepğŸ”’ (rightIdWk w)

-- weakening composition is associative
assocWk : {Î“1 Î“2 Î“3 Î“4 : Ctx} â†’ (w3 : Î“3 â‰¤ Î“4) (w2 : Î“2 â‰¤ Î“3) â†’ (w1 : Î“1 â‰¤ Î“2)
  â†’ (w3 âˆ™ w2) âˆ™ w1 â‰¡ w3 âˆ™ (w2 âˆ™ w1)
assocWk w3         w2         base       = refl
assocWk w3         w2         (drop w1)  = cong drop (assocWk w3 w2 w1)
assocWk w3         (drop w2)  (keep w1)  = cong drop (assocWk w3 w2 w1)
assocWk (drop w3)  (keep w2)  (keep w1)  = cong drop (assocWk w3 w2 w1)
assocWk (keep w3)  (keep w2)  (keep w1)  = cong keep (assocWk w3 w2 w1)
assocWk (keepğŸ”’ w3) (keepğŸ”’ w2) (keepğŸ”’ w1) = cong keepğŸ”’ (assocWk w3 w2 w1)

--------------------
-- Context extension
--------------------

data Ext (Î¸ : Flag) : Ctx â†’ Ctx â†’ Ctx â†’ Set where
  nil  : Ext Î¸ Î“ Î“ []
  ext  : Ext Î¸ Î“ Î“L Î“R â†’ Ext Î¸ (Î“ `, a) Î“L (Î“R `, a)
  extğŸ”’ : WL Î¸ â†’ Ext Î¸ Î“ Î“L Î“R â†’ Ext Î¸ (Î“ ğŸ”’) Î“L (Î“R ğŸ”’)

-- Lock free extension
LFExt : Ctx â†’ Ctx â†’ Ctx â†’ Set
LFExt = Ext ff

-- LFExt is indeed a lock-free extension
LFExtIsğŸ”’-free : LFExt Î“ Î“L Î“R â†’ ğŸ”’-free Î“R
LFExtIsğŸ”’-free nil = tt
LFExtIsğŸ”’-free (ext e) = LFExtIsğŸ”’-free e

-- embed lock-free extensions into ones that extend with locks
upExt : Ext ff Î“ Î“L Î“R â†’ Ext Î¸ Î“ Î“L Î“R
upExt nil     = nil
upExt (ext e) = ext (upExt e)

-- extension is appending
extIs,, : Ext Î¸ Î“ Î“L Î“R â†’ Î“ â‰¡ (Î“L ,, Î“R)
extIs,, nil        = refl
extIs,, (ext e)    = cong (_`, _) (extIs,, e)
extIs,, (extğŸ”’ f e) = cong _ğŸ”’ (extIs,, e)

-- appending (potentially) with locks is an extension
,,IsExt : Ext tt (Î“L ,, Î“R) Î“L Î“R
,,IsExt {Î“L = Î“L} {[]}      = nil
,,IsExt {Î“L = Î“L} {Î“R `, x} = ext ,,IsExt
,,IsExt {Î“L = Î“L} {Î“R ğŸ”’}    = extğŸ”’ tt ,,IsExt

-- appending without locks is an extension
,,IsExtLF : ğŸ”’-free Î“R â†’ Ext Î¸ (Î“L ,, Î“R) Î“L Î“R
,,IsExtLF {[]} p = nil
,,IsExtLF {Î“R `, x} p = ext (,,IsExtLF p)

-- NOTE: `extIs,,` + `,,IsExt` implies that `Ext` is the call-graph of `_,,_`

-- extensions are unique
-- i.e., an extension of Î“L with Î“R must yield a unique extension
extUniq : Ext Î¸ Î“' Î“L Î“R â†’ Ext Î¸ Î“ Î“L Î“R â†’ Î“' â‰¡ Î“
extUniq nil        nil         = refl
extUniq (ext e)    (ext e')    = cong (_`, _) (extUniq e e')
extUniq (extğŸ”’ f e) (extğŸ”’ _ e') = cong _ğŸ”’ (extUniq e e')

-- left identity of extension
extLId : Ext tt Î“ [] Î“
extLId {Î“ = []}     = nil
extLId {Î“ = Î“ `, x} = ext extLId
extLId {Î“ = Î“ ğŸ”’}    = extğŸ”’ tt extLId

-- right identity of extension
extRId : Ext Î¸ Î“ Î“ []
extRId = nil

-- lock-free extensions yield a "right" weakening (i.e., adding variables on the right)
wáµ£ : LFExt Î“ Î“L Î“R â†’ Î“ â‰¤ Î“L
wáµ£ nil     = idWk
wáµ£ (ext e) = drop (wáµ£ e)

private
 variable Î“LL Î“LR Î“RL Î“RR : Ctx

-- extension is assocative
extLAssoc : Ext Î¸ Î“ Î“L Î“R  â†’ Ext Î¸ Î“R Î“RL Î“RR â†’ Ext Î¸ Î“ (Î“L ,, Î“RL) Î“RR
extLAssoc el nil rewrite extIs,, el = nil
extLAssoc (ext el)    (ext er)      = ext (extLAssoc el er)
extLAssoc (extğŸ”’ x el) (extğŸ”’ _ er)   = extğŸ”’ x (extLAssoc el er)

-- extension is assocative
extRAssoc : Ext Î¸ Î“L Î“LL Î“LR â†’ Ext Î¸ Î“ Î“L Î“R â†’ Ext Î¸ Î“ Î“LL (Î“LR ,, Î“R)
extRAssoc el nil         = el
extRAssoc el (ext er)    = ext (extRAssoc el er)
extRAssoc el (extğŸ”’ x er) = extğŸ”’ x (extRAssoc el er)

-- "residual" extension
resExt : (e : LFExt Î“ (Î“L ğŸ”’) Î“R) â†’ Î“' â‰¤ Î“ â†’ LFExt Î“' ((â†ğŸ”’ Î“') ğŸ”’) (ğŸ”’â†’ Î“')
resExt e       (drop w)  = ext (resExt e w)
resExt nil     (keepğŸ”’ w) = nil
resExt (ext e) (keep w)  = ext (resExt e w)

-- "stashed" weakening
stashWk : (e : LFExt Î“ (Î“L ğŸ”’) Î“R) â†’ Î“' â‰¤ Î“ â†’ (â†ğŸ”’ Î“') â‰¤ Î“L
stashWk e       (drop w)  = stashWk e w
stashWk nil     (keepğŸ”’ w) = w
stashWk (ext e) (keep w)  = stashWk e w

-- the operation â†ğŸ”’ returns the context to the left of ğŸ”’
â†ğŸ”’IsPreğŸ”’ : LFExt Î“ (Î“L ğŸ”’) Î“R â†’ Î“L â‰¡ (â†ğŸ”’ Î“)
â†ğŸ”’IsPreğŸ”’ nil = refl
â†ğŸ”’IsPreğŸ”’ (ext e) = â†ğŸ”’IsPreğŸ”’ e

-- the operation ğŸ”’â†’ returns the context to the right of ğŸ”’
ğŸ”’â†’isPostğŸ”’ : LFExt Î“ (Î“L ğŸ”’) Î“R â†’ Î“R â‰¡ (ğŸ”’â†’ Î“)
ğŸ”’â†’isPostğŸ”’ nil     = refl
ğŸ”’â†’isPostğŸ”’ (ext e) = cong (_`, _) (ğŸ”’â†’isPostğŸ”’ e)

---------------
-- Slicing laws
---------------

resAccLem : (w' : Î” â‰¤ Î“') (w  : Î“' â‰¤ Î“) (e : LFExt Î“ (Î“L ğŸ”’) Î“R)
  â†’ resExt (resExt e w) w' â‰¡ resExt e (w âˆ™ w')
resAccLem (drop w') (drop w)   nil     = cong ext (resAccLem w' (drop w) nil)
resAccLem (drop w') (drop w)   (ext e) = cong ext (resAccLem w' (drop w) (ext e))
resAccLem (drop w') (keep w)   (ext e) = cong ext (resAccLem w' (keep w) (ext e))
resAccLem (drop w') (keepğŸ”’ w)  nil     = cong ext (resAccLem w' (keepğŸ”’ w) nil)
resAccLem (keep w') (drop w)   nil     = cong ext (resAccLem w' w nil)
resAccLem (keep w') (drop w)   (ext e) = cong ext (resAccLem w' w (ext e))
resAccLem (keep w') (keep w)   (ext e) = cong ext (resAccLem w' w e)
resAccLem (keepğŸ”’ w') (keepğŸ”’ w) nil     = refl

stashSquash : (w' : Î” â‰¤ Î“') (w  : Î“' â‰¤ Î“) (e : LFExt Î“ (Î“L ğŸ”’) Î“R)
  â†’ (stashWk e w âˆ™ stashWk (resExt e w) w') â‰¡ stashWk e (w âˆ™ w')
stashSquash (drop w')  (drop w)  nil     = stashSquash w' (drop w) nil
stashSquash (drop w')  (drop w)  (ext e) = stashSquash w' (drop w) (ext e)
stashSquash (keep w')  (drop w)  nil     = stashSquash w' w nil
stashSquash (keep w')  (drop w)  (ext e) = stashSquash w' w (ext e)
stashSquash (drop w')  (keep w)  (ext e) = stashSquash w' (keep w) (ext e)
stashSquash (keep w')  (keep w)  (ext e) = stashSquash w' w e
stashSquash (drop w')  (keepğŸ”’ w) nil     = stashSquash w' (keepğŸ”’ w) nil
stashSquash (keepğŸ”’ w') (keepğŸ”’ w) nil     = refl

-- a good slice is a slice whose composition doesn't change
goodSlice : (w : Î“' â‰¤ Î“) â†’ (e : LFExt Î“ (Î“L ğŸ”’) Î“R)
  â†’ wáµ£ e âˆ™ w â‰¡ (keepğŸ”’ (stashWk e w) âˆ™ wáµ£ (resExt e w))
goodSlice (drop w)  nil     = cong drop (goodSlice w nil)
goodSlice (drop w)  (ext e) = cong drop (goodSlice w (ext e))
goodSlice (keep w)  (ext e) = cong drop (goodSlice w e)
goodSlice (keepğŸ”’ w) nil     = cong keepğŸ”’ (trans (leftIdWk w) (sym (rightIdWk w)))

stashWkId : (e : LFExt Î“ (â†ğŸ”’ Î“ ğŸ”’) (ğŸ”’â†’ Î“)) â†’ stashWk e idWk â‰¡ idWk
stashWkId {Î“ `, x} (ext e) = stashWkId e
stashWkId {Î“ ğŸ”’}    nil     = refl

resExtId :  (e : LFExt Î“ (â†ğŸ”’ Î“ ğŸ”’) (ğŸ”’â†’ Î“)) â†’ resExt e idWk â‰¡ e
resExtId {Î“ `, x} (ext e) = cong ext (resExtId e)
resExtId {Î“ ğŸ”’}    nil     = refl
