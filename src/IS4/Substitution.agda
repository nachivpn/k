open import Context using ()
  renaming (Ctx to ICtx ; _â‰¤_ to Iâ‰¤ ; Var to IVar)

module IS4.Substitution (Ty : Set)
  (Tm    : ICtx Ty â†’ Ty â†’ Set)
  (var   : âˆ€ {Î“ a} â†’ IVar Ty Î“ a â†’ Tm Î“ a)
  (wkTm  : âˆ€ {Î“' Î“ a} â†’ Iâ‰¤ Ty Î“' Î“ â†’ Tm Î“ a â†’ Tm Î“' a)
  where

open import Data.Unit using (tt)
open import Data.Product
open import Relation.Binary.PropositionalEquality

open import Context Ty

private
  variable
    a b c d : Ty

----------------
-- Substitutions
----------------

data Sub : Ctx â†’ Ctx â†’ Set where
  []   : Sub Î” []
  _`,_ : Sub Î” Î“ â†’ Tm Î” a â†’ Sub Î” (Î“ `, a)
  lock : Sub Î”L Î“ â†’ Ext tt Î” Î”L Î”R â†’ Sub Î” (Î“ ğŸ”’)

-- composition operation for weakening after substituion
trimSub : Î“ â‰¤ Î” â†’ Sub Î“' Î“ â†’ Sub Î“' Î”
trimSub base      []         = []
trimSub (drop w)  (s `, x)   = trimSub w s
trimSub (keep w)  (s `, x)   = (trimSub w s) `, x
trimSub (keepğŸ”’ w) (lock s x) = lock (trimSub w s) x

-- apply substitution to a variable
substVar : Sub Î“ Î” â†’ Var Î” a â†’ Tm Î“ a
substVar (s `, t) ze     = t
substVar (s `, t) (su x) = substVar s x

-- weaken a substitution
wkSub : Î“' â‰¤ Î“ â†’ Sub Î“ Î” â†’ Sub Î“' Î”
wkSub w []          = []
wkSub w (s `, t)    = (wkSub w s) `, wkTm w t
wkSub w (lock s e)  = lock (wkSub (sliceLeftG e w) s) (wkExt e w)

-- identity substitution
idâ‚› : Sub Î“ Î“
idâ‚› {[]}     = []
idâ‚› {Î“ `, x} = wkSub fresh idâ‚› `, (var ze)
idâ‚› {Î“ ğŸ”’}    = lock (idâ‚› {Î“}) (extğŸ”’ tt nil)

-- NOTE: composition requires parallel substituion for terms

-- "drop" the last variable in the context
dropâ‚› : Sub Î“ Î” â†’ Sub (Î“ `, a) Î”
dropâ‚› s = wkSub fresh s

-- "keep" the last variable in the context
keepâ‚› : Sub Î“ Î” â†’ Sub (Î“ `, a) (Î” `, a)
keepâ‚› s = dropâ‚› s `, var ze

-- embed a weakening to substitution
embWk : Î“ â‰¤ Î” â†’ Sub Î“ Î”
embWk base      = []
embWk (drop w)  = dropâ‚› (embWk w)
embWk (keep w)  = keepâ‚› (embWk w)
embWk (keepğŸ”’ w) = lock (embWk w) (extğŸ”’ tt nil)

ExtToSub : Ext tt Î“ Î“L Î“R â†’ Sub Î“ (Î“L ğŸ”’)
ExtToSub e = lock idâ‚› e

--------------------
-- Substitution laws
--------------------

-- NOTE: these are only the laws that follow directly from the structure of substitutions
coh-trimSub-wkVar : (x : Var Î“ a) (s : Sub Î”' Î”) (w : Î” â‰¤ Î“)
  â†’ substVar (trimSub w s) x â‰¡ substVar s (wkVar w x)
coh-trimSub-wkVar ze (s `, x) (drop w)
  = coh-trimSub-wkVar ze s w
coh-trimSub-wkVar ze (s `, x) (keep w)
  = refl
coh-trimSub-wkVar (su x) (s `, xâ‚) (drop w)
  = coh-trimSub-wkVar (su x) s w
coh-trimSub-wkVar (su x) (s `, xâ‚) (keep w)
  = coh-trimSub-wkVar x s w

-- `trimSub` preserves the identity
trimSubPresId : (s : Sub Î” Î“) â†’ trimSub idWk s â‰¡ s
trimSubPresId []         = refl
trimSubPresId (s `, x)   = cong (_`, _) (trimSubPresId s)
trimSubPresId (lock s x) = congâ‚‚ lock (trimSubPresId s) refl

-- naturality of substVar
nat-substVar : (x : Var Î“ a) (s : Sub Î” Î“) (w : Î”' â‰¤ Î”)
  â†’ substVar (wkSub w s) x â‰¡ wkTm w (substVar s x)
nat-substVar ze     (s `, t) w = refl
nat-substVar (su x) (s `, t) w = nat-substVar x s w

-- naturality of trimSub
nat-trimSub : (s : Sub Î“ Î”) (w : Î” â‰¤ Î”') (w' : Î“' â‰¤ Î“)
  â†’ wkSub w' (trimSub w s) â‰¡ trimSub w (wkSub w' s)
nat-trimSub []         base      w' = refl
nat-trimSub (s `, t)   (drop w)  w' = nat-trimSub s w w'
nat-trimSub (s `, t)   (keep w)  w' = cong (_`, wkTm w' t) (nat-trimSub s w w')
nat-trimSub (lock s x) (keepğŸ”’ w) w' = congâ‚‚ lock (nat-trimSub s w _) refl

-- `trimSub` on the identity substituion embeds the weakening
trimSubId : (w : Î” â‰¤ Î“) â†’ trimSub w idâ‚› â‰¡ embWk w
trimSubId base = refl
trimSubId (drop w) = trans
  (sym (nat-trimSub idâ‚› w fresh))
  (cong (wkSub fresh) (trimSubId w))
trimSubId (keep w) = cong (_`, var ze) (trans
  (sym (nat-trimSub idâ‚› w fresh))
  (cong (wkSub fresh) (trimSubId w)))
trimSubId (keepğŸ”’ w) = congâ‚‚ lock (trimSubId w) refl
