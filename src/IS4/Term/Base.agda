{-# OPTIONS --safe --without-K #-}
module IS4.Term.Base where

--
-- Implementation of the IS4 (Intuitionistic S4) calculus from
-- "Fitch-Style Modal Lambda Calculi" by Ranald Clouston (2018)
--

open import Data.Product using (âˆƒ ; _Ã—_ ; _,_ ; -,_ ; projâ‚ ; projâ‚‚)

open import Relation.Binary.PropositionalEquality using (sym ; subst)

open import IK.Type as Type using (Ty ; Ty-Decidable)

import Context Ty Ty-Decidable as Context

open Context public hiding (extğŸ”’)
open Type    public

-------------------------------------
-- Variables, terms and substitutions
-------------------------------------

data Tm : Ctx â†’ Ty â†’ Set where

  var  : (v : Var Î“ a)
       ---------------
       â†’ Tm Î“ a

  lam  : (t : Tm (Î“ `, a) b)
         -------------------
       â†’ Tm Î“ (a â‡’ b)

  app  : (t : Tm Î“ (a â‡’ b))
       â†’ (u : Tm Î“ a)
         ------------------
       â†’ Tm Î“ b

  box   : (t : Tm (Î“ ğŸ”’) a)
        ------------------
        â†’ Tm Î“ (â–¡ a)

  unbox : (t : Tm Î“L (â–¡ a))
        â†’ (e : CExt Î“ Î“L Î“R)
        --------------------
        â†’ Tm Î“ a

variable
  t t' t'' : Tm Î“ a
  u u' u'' : Tm Î“ a

pattern var0 = var v0
pattern var1 = var v1
pattern var2 = var v2

wkTm : Î“ âŠ† Î“' â†’ Tm Î“ a â†’ Tm Î“' a
wkTm w (var x)     = var (wkVar w x)
wkTm w (lam t)     = lam (wkTm (keep w) t)
wkTm w (app t u)   = app (wkTm w t) (wkTm w u)
wkTm w (box t)     = box (wkTm (keepğŸ”’ w) t)
wkTm w (unbox t e) = unbox (wkTm (factorWk e w) t) (factorExt e w)

-- extension that "generates a new context frame"
new : CExt (Î“ ğŸ”’) Î“ ([] ğŸ”’) -- Î“ R Î“ ğŸ”’
new = extğŸ”’- nil

new[_] = Î» Î“ â†’ new {Î“}

open Substitution Tm var wkTm CExt new lCtx factorWk rCtx factorExt public
  renaming (module Composition to SubstitutionComposition)

-- "Left" context of factoring with a substitution (see factorSubâ‚› and factorExtâ‚›)
lCtxâ‚› : (e : CExt Î“ Î“L Î“R) (s : Sub Î” Î“) â†’ Ctx
lCtxâ‚› {Î” = Î”} nil       s           = Î”
lCtxâ‚›         (ext e)   (s `, t)    = lCtxâ‚› e s
lCtxâ‚›         (extğŸ”’- e) (lock s e') = lCtxâ‚› e s

factorSubâ‚› : (e : CExt Î“ Î“L Î“R) (s : Sub Î” Î“) â†’ Sub (lCtxâ‚› e s) Î“L
factorSubâ‚› nil       s           = s
factorSubâ‚› (ext e)   (s `, t)    = factorSubâ‚› e s
factorSubâ‚› (extğŸ”’- e) (lock s e') = factorSubâ‚› e s

-- "Right" context of factoring with a substitution (see factorExtâ‚›)
rCtxâ‚› : (e : CExt Î“ Î“L Î“R) (s : Sub Î” Î“) â†’ Ctx
rCtxâ‚› nil       s                     = []
rCtxâ‚› (ext e)   (s `, t)              = rCtxâ‚› e s
rCtxâ‚› (extğŸ”’- e) (lock {Î”R = Î”R} s e') = rCtxâ‚› e s ,, Î”R

factorExtâ‚› : (e : CExt Î“ Î“L Î“R) (s : Sub Î” Î“) â†’ CExt Î” (lCtxâ‚› e s) (rCtxâ‚› e s)
factorExtâ‚› nil       s           = nil
factorExtâ‚› (ext e)   (s `, _)    = factorExtâ‚› e s
factorExtâ‚› (extğŸ”’- e) (lock s e') = extRAssoc (factorExtâ‚› e s) e'

-- apply substitution to a term
substTm : Sub Î” Î“ â†’ Tm Î“ a â†’ Tm Î” a
substTm s (var x)     = substVar s x
substTm s (lam t)     = lam (substTm (keepâ‚› s) t)
substTm s (app t u)   = app (substTm s t) (substTm s u)
substTm s (box t)     = box (substTm (keepğŸ”’â‚› s) t)
substTm s (unbox t e) = unbox (substTm (factorSubâ‚› e s) t) (factorExtâ‚› e s)

open SubstitutionComposition substTm lCtxâ‚› factorSubâ‚› rCtxâ‚› factorExtâ‚› public
