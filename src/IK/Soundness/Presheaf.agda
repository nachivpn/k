{-# OPTIONS --allow-unsolved-metas #-}
module IK.Soundness.Presheaf where

open import Data.Unit  using (âŠ¤ ; tt)
open import Data.Product  using (Î£ ; _Ã—_ ; _,_ ; projâ‚ ; projâ‚‚)
open import Relation.Binary.PropositionalEquality

open import IK.Term
open import IK.Norm
open import IK.HellOfSyntacticLemmas

postulate
  funext  : âˆ€{i j}{A : Set i}{B : A â†’ Set j}{f g : (x : A) â†’ B x}
          â†’ ((x : A) â†’ f x â‰¡ g x) â†’ f â‰¡ g

  funexti : âˆ€{i j}{A : Set i}{B : A â†’ Set j}{f g : {x : A} â†’ B x}
          â†’ ((x : A) â†’ f {x} â‰¡ g {x}) â†’ _â‰¡_ {A = {x : A} â†’ B x} f g

-----------------------------
-- Presheaf refinement of Tm'
-----------------------------

-- Used to ensure that the domain of interpretation is indeed presheafs
Psh : Tm' Î“ a â†’ Set
Psh {Î“} {ğ•“}     n      = âŠ¤
Psh {Î“} {a â‡’ b} f      = {Î“' : Ctx} (w : Î“' â‰¤ Î“)
  â†’ (x : Tm' Î“' a) â†’ Psh x
  -- naturality of presheaf exponentials
  â†’ ({Î“â° : Ctx} â†’ (w' : Î“â° â‰¤ Î“') â†’ f (w âˆ™ w') (wkTm' w' x) â‰¡ wkTm' w' (f w x))
    Ã— Psh (f w x)
Psh {Î“} {â—» a} (box x) = Psh x

-- Psh extended to interpretation of contexts
Pshâ‚› : Sub' Î“ Î” â†’ Set
Pshâ‚› {Î“} {[]}     s          = âŠ¤
Pshâ‚› {Î“} {Î” `, a} (s , x)    = Pshâ‚› s Ã— Psh x
Pshâ‚› {Î“} {Î” ğŸ”’}    (lock s e) = Pshâ‚› s

-----------------------------------
-- Psh(â‚›) is preserved by weakening
-----------------------------------

-- wkTm' preserves Psh
wkTm'PresPsh : (w : Î“' â‰¤ Î“) (x : Tm' Î“ a) â†’ Psh x â†’ Psh (wkTm' w x)
wkTm'PresPsh {a = ğ•“}     w x       p = tt
wkTm'PresPsh {a = a â‡’ b} w f       p = Î» w' y q â†’
  -- nf gives us that f obeys naturality (ind. hyp enabled by PSh)
  -- pfx gives us that the codomain of f is a presheaf, i.e., `PSh (f _ x)`
  let (nf , pfx) = p (w âˆ™ w') y q
  in (Î» {Î“â°} w'' â†’
    subst (Î» z â†’ f z _ â‰¡ wkTm' _ _) (assocWk w w' w'') (nf w''))
    , pfx
wkTm'PresPsh {a = â—» a}  w (box x) p = wkTm'PresPsh (keepğŸ”’ w) x p

-- wkSub' preserves Pshâ‚›
wkSub'PresPsh : (w : Î“' â‰¤ Î“) (s : Sub' Î“ Î”) â†’ Pshâ‚› s â†’ Pshâ‚› (wkSub' w s)
wkSub'PresPsh {Î” = []}     w s          p         =
  tt
wkSub'PresPsh {Î” = Î” `, a} w (s , x)    (ps , px) =
  wkSub'PresPsh w s ps , wkTm'PresPsh w x px
wkSub'PresPsh {Î” = Î” ğŸ”’}    w (lock s e) p         =
  wkSub'PresPsh (sliceLeft e w) s p

-------------------------
-- `Tm'- a` is a presheaf
-------------------------

-- Given `a : Ty`,
-- (object map)   Tm'- a : Ctx â†’ Set
-- (morphism map) wkTm'  : Î“' â‰¤ Î“ â†’ Tm'- a Î“ â†’ Tm'- a Î“'

-- identity functor law of `Tm'- a`
wkTm'PresId : (x : Tm' Î“ a) â†’ wkTm' idWk x â‰¡ x
wkTm'PresId {a = ğ•“}     n
  = wkNfPresId n
wkTm'PresId {a = a â‡’ b} f
  = funexti (Î» _ â†’ funext (Î» _ â†’ cong f (leftIdWk _)))
wkTm'PresId {a = â—» a}  (box x)
  = cong box (wkTm'PresId x)

-- composition functor law of `Tm'- a`
wkTm'Presâˆ™ : (w : Î“' â‰¤ Î“) (w' : Î“'' â‰¤ Î“') (x : Tm' Î“ a)
  â†’ wkTm' w' (wkTm' w x) â‰¡ wkTm' (w âˆ™ w') x
wkTm'Presâˆ™ {a = ğ•“}     w w' n       =
  wkNfPresâˆ™ w w' n
wkTm'Presâˆ™ {a = a â‡’ b} w w' f       =
  funexti (Î» _ â†’ funext (Î» w'' â†’
    cong f (sym (assocWk w w' w''))))
wkTm'Presâˆ™ {a = â—» a}  w w' (box x) =
  cong box (wkTm'Presâˆ™ (keepğŸ”’ w) (keepğŸ”’ w') x)

--------------------------
-- `Sub'- Î”` is a presheaf
--------------------------

-- Given `Î” : Ctx`,
-- (object map)   Sub'- Î” : Ctx â†’ Set
-- (morphism map) wkSub'  : Î“' â‰¤ Î“ â†’ Sub'- Î” Î“ â†’ Sub'- Î” Î“'

-- identity functor law of `Sub'- Î“`
wkSub'PresId : (s : Sub' Î“ Î”) â†’ wkSub' idWk s â‰¡ s
wkSub'PresId {Î” = []}     tt         = refl
wkSub'PresId {Î” = Î” `, a} (s , x)    = congâ‚‚ _,_ (wkSub'PresId s) (wkTm'PresId x)
wkSub'PresId {Î” = Î” ğŸ”’}    (lock s e) with â†ğŸ”’IsPreğŸ”’ e | ğŸ”’â†’isPostğŸ”’ e
... | refl | refl = congâ‚‚ lock
  (trans (congâ‚‚ wkSub' (sliceLeftId e) refl) (wkSub'PresId s))
  (wkLFExtPresId e)

-- composition functor law of `Sub'- Î“`
wkSub'Presâˆ™ : (w : Î“' â‰¤ Î“) (w' : Î“'' â‰¤ Î“') (s : Sub' Î“ Î”)
  â†’ wkSub' w' (wkSub' w s) â‰¡ wkSub' (w âˆ™ w') s
wkSub'Presâˆ™ {Î” = []}     w w' tt         = refl
wkSub'Presâˆ™ {Î” = Î” `, a} w w' (s , x)    = congâ‚‚ _,_ (wkSub'Presâˆ™ w w' s) (wkTm'Presâˆ™ w w' x)
wkSub'Presâˆ™ {Î” = Î” ğŸ”’}    w w' (lock s e) = congâ‚‚ lock
  (trans  (wkSub'Presâˆ™ _ _ s) (congâ‚‚ wkSub' (sliceLeftPresâˆ™ w' w e) refl))
  (wkLFExtPresâˆ™ w' w e)

-------------------------------------------
-- `subsVar' x` is a natural transformation
-------------------------------------------

-- for `x : Var Î“ a`,
-- substVar x : Sub'- Î“ â†’Ì‡ Tm'- a

-- naturality of substVar'
nat-substVar' : (w : Î”' â‰¤ Î”) (x : Var Î“ a) (s : Sub' Î” Î“)
  â†’ substVar' x (wkSub' w s) â‰¡ wkTm' w (substVar' x s)
nat-substVar' w ze     s       = refl
nat-substVar' w (su x) (s , _) = nat-substVar' w x s

-- substVar' obeys Psh
psh-substVar' : (x : Var Î“ a) (s : Sub' Î” Î“) â†’ Pshâ‚› s â†’ Psh (substVar' x s)
psh-substVar' ze     (_ , x) (_ , px) = px
psh-substVar' (su x) (s , _) (ps , _) = psh-substVar' x s ps

---------------------------------------
-- `eval t` is a natural transformation
---------------------------------------

-- for `t : Tm Î“ a`,
-- eval t : Sub'- Î“ â†’Ì‡ Tm'- a

-- (mutually defined functions below)

-- result of evaluation is in Psh
psh-eval  : (t : Tm Î“ a) (s : Sub' Î” Î“)
  â†’ Pshâ‚› s â†’ Psh (eval t s)
-- naturality of `eval t`
nat-eval : (t : Tm Î“ a) (w : Î”' â‰¤ Î”) (s : Sub' Î” Î“)
  â†’ Pshâ‚› s â†’ eval t (wkSub' w s) â‰¡ wkTm' w (eval t s)

-- psh-eval
psh-eval (var x)           s         ps
  = psh-substVar' x s ps
psh-eval (lam t)           s         ps
  = Î» w x px
    â†’ (Î» w' â†’ trans
         -- rewrite using wkSub'Presâˆ™
         (cong (Î» z â†’ (eval t (z , _))) (sym (wkSub'Presâˆ™ w w' s)))
         -- follows directly from nat-eval
         (nat-eval t w' (wkSub' w s , x) (wkSub'PresPsh w s ps , px)))
      , (psh-eval t _ (wkSub'PresPsh w s ps , px))
psh-eval (app t u)         s         ps
  = let (_ , fxp) = psh-eval t s ps idWk _ (psh-eval u s ps) in fxp
psh-eval (box t)           s         ps
  = psh-eval t (lock s nil) ps
psh-eval (unbox t nil)     (lock s e') ps with eval t s | psh-eval t s ps
... | box x | px
  = wkTm'PresPsh (LFExtToâ‰¤ e') x px
psh-eval (unbox t (ext e)) (s , _)  (ps , _)
  = psh-eval (unbox t e) s ps

-- nat-eval
nat-eval (var x)           w s       ps
  = nat-substVar' w x s
nat-eval (lam t)           w s       ps
  = funexti (Î» _ â†’ funext Î» _ â†’ funext (Î» _
    â†’ cong (Î» z â†’  eval t (z , _)) (wkSub'Presâˆ™ _ _ _)))
nat-eval (app t u)         w s       ps with
  (psh-eval t s ps idWk (eval u s) (psh-eval u s ps))
... | (g , _)
  rewrite nat-eval t w s ps | nat-eval u w s ps
  = trans
    (cong
      (Î» z â†’ eval t s z (wkTm' w (eval u s)))
      (trans (rightIdWk w) (sym (leftIdWk w))))
    (g  w)
nat-eval (box t)           w s       ps
  = cong box (nat-eval t (keepğŸ”’ w) (lock s nil) ps)
nat-eval (unbox t nil)     w (lock s e) ps = trans
  (cong (Î» z â†’ unbox' z (wkLFExt e w)) (nat-eval t (sliceLeft e w) s ps))
  (gsLemma w e (eval t s))
  where
  gsLemma : (w : Î”' â‰¤ Î” ) (e : LFExt Î” (Î“L ğŸ”’) Î“R) (x : Tm' Î“L (â—» a))
    â†’ unbox' (wkTm' (sliceLeft e w) x) (wkLFExt e w) â‰¡ wkTm' w (unbox' x e)
  gsLemma w e (box x) = trans (wkTm'Presâˆ™ _ _ _)
    (sym (trans
      (wkTm'Presâˆ™ _ _ _)
      (cong (Î» z â†’ wkTm' z x) (slicingLemma w e))))
nat-eval (unbox t (ext e)) w (s , _) (ps , _)
  = nat-eval (unbox t e) w s ps

---------------------------------------
-- `evalâ‚› s` is a natural transformation
---------------------------------------

-- for `s : Sub Î“ Î”`,
-- evalâ‚› s : Sub'- Î“  â†’Ì‡ Sub'- Î”

psh-evalâ‚› : (s : Sub Î“ Î“') (s' : Sub' Î” Î“)
    â†’ Pshâ‚› s' â†’ Pshâ‚› (evalâ‚› s s')
psh-evalâ‚› []       s' ps'
  = tt
psh-evalâ‚› (s `, t) s' ps'
  = (psh-evalâ‚› s s' ps') , (psh-eval t s' ps')
psh-evalâ‚› (lock s nil) (lock s' e) ps'
  = psh-evalâ‚› s s' ps'
psh-evalâ‚› (lock s (ext e)) (s' , _) (ps' , _)
  = psh-evalâ‚› (lock s e) s' ps'

-- naturality of evalâ‚›
nat-evalâ‚› : (w : Î”' â‰¤ Î”)  (s : Sub Î“' Î“) (s' : Sub' Î” Î“') (ps' : Pshâ‚› s')
  â†’ evalâ‚› s (wkSub' w s') â‰¡ wkSub' w (evalâ‚› s s')
nat-evalâ‚› w []               s'        ps'
  = refl
nat-evalâ‚› w (s `, t)         s'        ps'
  = congâ‚‚ _,_ (nat-evalâ‚› w s s' ps') (nat-eval t w s' ps')
nat-evalâ‚› w (lock s (ext e)) (s' , _) (ps' , _)
  = nat-evalâ‚› w (lock s e) s' ps'
nat-evalâ‚› w (lock s nil)     (lock s' e) ps'
  = congâ‚‚ lock (nat-evalâ‚› (sliceLeft e w) s s' ps') refl


------------------------------------------------
-- reflect and reify are natural transformations
------------------------------------------------

-- reflect : Ne- a â†’Ì‡ Tm'- a
-- reify   : Tm'- a â†’Ì‡ Nf'- a

-- naturality of reflect
nat-reflect : (w : Î“' â‰¤ Î“) (n : Ne Î“ a) â†’ reflect (wkNe w n) â‰¡ wkTm' w (reflect n)
nat-reflect {a = ğ•“}     w n = refl
nat-reflect {a = a â‡’ b} w n = funexti (Î» _ â†’ funext (Î» _ â†’ funext (Î» _
  â†’ cong (Î» z â†’ reflect (app z (reify _))) (wkNePresâˆ™ w _ n))))
nat-reflect {a = â—» a}  w n = cong box (nat-reflect (keepğŸ”’ w) (unbox n nil))

-- image of reflect is in Psh
psh-reflect : (n : Ne Î“ a) â†’ Psh (reflect n)
-- naturality of reify
nat-reify : (w : Î“' â‰¤ Î“) (x : Tm' Î“ a) â†’ Psh x â†’ reify (wkTm' w x) â‰¡ wkNf w (reify x)

-- psh-reflect
psh-reflect {a = ğ•“}     n = tt
psh-reflect {a = a â‡’ b} n = Î» w x px
  â†’ (Î» w' â†’ trans
       (cong reflect
         (congâ‚‚ app (sym (wkNePresâˆ™ _ _ _)) (nat-reify _ _ px)))
       (nat-reflect w' (app (wkNe w n) (reify x))))
  , psh-reflect (app (wkNe w n) _)
psh-reflect {a = â—» a}  n = psh-reflect (unbox n nil)

-- nat-reify
nat-reify {a = ğ•“}         w x   px
  = refl
nat-reify {Î“} {a = a â‡’ b} w f   pf
  = let (nf , pfx) = pf fresh (reflect (var ze)) (psh-reflect {Î“ = _ `, a} (var ze))
  in cong lam
    (trans
      (cong reify
        (trans
          (congâ‚‚ f
            (cong drop (trans (rightIdWk _) (sym (leftIdWk _))))
            (nat-reflect (keep w) (var ze)))
          (nf (keep w))))
      (nat-reify (keep w) (f fresh (reflect (var ze))) pfx))
nat-reify {a = â—» a} w  (box x) px
  = cong box (nat-reify (keepğŸ”’ w) x px)

-- idâ‚›' is in Pshâ‚›
psh-idâ‚›' : Pshâ‚› (idâ‚›' {Î“})
psh-idâ‚›' {[]}     = tt
psh-idâ‚›' {Î“ `, a} = wkSub'PresPsh fresh (idâ‚›' {Î“}) (psh-idâ‚›' {Î“}) , psh-reflect {Î“ `, a} (var ze)
psh-idâ‚›' {Î“ ğŸ”’}    = psh-idâ‚›' {Î“}
