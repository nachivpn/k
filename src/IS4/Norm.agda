module IS4.Norm where

open import Data.Unit  using (âŠ¤ ; tt)
open import Data.Product  using (Î£ ; _Ã—_ ; _,_)

open import IS4.Term

---------------
-- Normal forms
---------------

data Ne : Ctx â†’ Ty â†’ Set
data Nf : Ctx â†’ Ty â†’ Set

data Ne where
  var   : Var Î“ a â†’ Ne Î“ a
  app   : Ne Î“ (a â‡’ b) â†’ Nf Î“ a â†’ Ne Î“ b
  unbox : Ne Î“L (â—» a) â†’ Ext tt Î“ Î“L Î“R â†’ Ne Î“ a

data Nf where
  upğ•“ : Ne Î“ ğ•“ â†’ Nf Î“ ğ•“
  lam : Nf (Î“ `, a) b â†’ Nf Î“ (a â‡’ b)
  box : Nf (Î“ ğŸ”’) a â†’ Nf Î“ (â—» a)

-- embedding into terms

embNe : Ne Î“ a â†’ Tm Î“ a
embNf : Nf Î“ a â†’ Tm Î“ a

embNe (var x)     = var x
embNe (app m n)   = app (embNe m) (embNf n)
embNe (unbox n x) = unbox (embNe n) x

embNf (upğ•“ x) = embNe x
embNf (lam n) = lam (embNf n)
embNf (box n) = box (embNf n)

-- weakening lemmas

wkNe : Î“' â‰¤ Î“ â†’ Ne Î“ a â†’ Ne Î“' a
wkNf : Î“' â‰¤ Î“ â†’ Nf Î“ a â†’ Nf Î“' a

wkNe w (var x)      = var (wkVar w x)
wkNe w (app m n)    = app (wkNe w m) (wkNf w n)
wkNe w (unbox n e)  = unbox (wkNe (factor2â‰¤ e w) n) (factor2Ext e w)

wkNf e (upğ•“ x) = upğ•“ (wkNe e x)
wkNf e (lam n) = lam (wkNf (keep e) n)
wkNf e (box n) = box (wkNf (keepğŸ”’ e) n)

------------
-- NbE Model
------------

-- family of maps between interpretations
_â†’Ì‡_ : (Ctx â†’ Set) â†’ (Ctx â†’ Set) â†’ Set
_â†’Ì‡_ A B = {Î” : Ctx} â†’ A Î” â†’ B Î”

-- semantic counterpart of `lock` from `Sub`
data Lock (A : Ctx â†’ Set) : Ctx â†’ Set where
  lock : A Î“L â†’ Ext tt Î“ Î“L Î“R  â†’ Lock A Î“

-- interpretation of types

Tm' : Ctx â†’ Ty â†’ Set
Tm' Î“  ğ•“       = Nf Î“ ğ•“
Tm' Î“  (a â‡’ b) = {Î“' : Ctx} â†’ Î“' â‰¤ Î“ â†’ (Tm' Î“' a â†’ Tm' Î“' b)
Tm' Î“L (â—» a)  = {Î“ Î“R : Ctx} â†’ Ext tt Î“ Î“L Î“R â†’ Tm' Î“ a

-- interpretation of contexts
Sub' : Ctx â†’ Ctx â†’ Set
Sub' Î” []       = âŠ¤
Sub' Î” (Î“ `, a) = Sub' Î” Î“ Ã— Tm' Î” a
Sub' Î” (Î“ ğŸ”’)    = Lock (Î» Î“' â†’ Sub' Î“' Î“) Î”

-- values in the model can be weakened
wkTm' : Î“' â‰¤ Î“ â†’ Tm' Î“ a â†’ Tm' Î“' a
wkTm' {a = ğ•“}     w n  = wkNf w n
wkTm' {a = a â‡’ b} w f  = Î» w' y â†’ f (w âˆ™ w') y
wkTm' {a = â—» a}  w bx = Î» e â†’ wkTm' (factor1â‰¤ e w) (bx (factor1Ext e w))

-- substitutions in the model can be weakened
wkSub' : Î“' â‰¤ Î“ â†’ Sub' Î“ Î” â†’ Sub' Î“' Î”
wkSub' {Î” = []}     w tt          = tt
wkSub' {Î” = Î” `, a} w (s , x)     = wkSub' w s , wkTm' w x
wkSub' {Î” = Î” ğŸ”’}    w (lock s e)  = lock (wkSub' (factor2â‰¤ e w) s) (factor2Ext e w)

-- semantic counterpart of `unbox` from `Tm`
unbox' : Tm' Î“L (â—» a) â†’ Ext tt Î“ Î“L Î“R â†’ Tm' Î“ a
unbox' bx e = bx e

-------------------------
-- Normalization function
-------------------------

Sub- : Ctx â†’ Ctx â†’ Set
Sub- Î” Î“ = Sub Î“ Î”

Sub'- : Ctx â†’ Ctx â†’ Set
Sub'- Î” Î“ = Sub' Î“ Î”

Tm'- : Ty â†’ Ctx â†’ Set
Tm'- a Î“ = Tm' Î“ a

reify   : Tm' Î“ a â†’ Nf Î“ a
reflect : Ne Î“ a  â†’ Tm' Î“ a

-- interpretation of neutrals
reflect {a = ğ•“} n     = upğ•“ n
reflect {a = a â‡’ b} n = Î» e x â†’ reflect (app (wkNe e n) (reify x))
reflect {a = â—» a} n  = Î» e â†’ reflect (unbox n e)

-- reify values to normal forms
reify {a = ğ•“}     x   = x
reify {a = a â‡’ b} x   = lam (reify (x (drop idWk) (reflect (var ze))))
reify {a = â—» a}  bx  = box (reify (bx (extğŸ”’ _ nil)))

-- identity substitution
idâ‚›' : Sub' Î“ Î“
idâ‚›' {[]}     = tt
idâ‚›' {Î“ `, x} = wkSub' (drop idWk) idâ‚›' , reflect (var ze)
idâ‚›' {Î“ ğŸ”’}    = lock (idâ‚›' {Î“}) (extğŸ”’ _ nil)

-- interpretation of variables
substVar' : Var Î“ a â†’ (Sub'- Î“ â†’Ì‡ Tm'- a)
substVar' ze     (_ , x) = x
substVar' (su x) (Î³ , _) = substVar' x Î³

import Context as C
import IS4.Substitution as S

-- interpretation of terms
eval : Tm Î“ a â†’ (Sub'- Î“ â†’Ì‡ Tm'- a)
eval (var x)              s
  = substVar' x s
eval (lam t)              s
  = Î» e x â†’ eval t (wkSub' e s , x)
eval (app t u)            s
  = (eval t s) idWk (eval u s)
eval (box t)              s
  = Î» e â†’ eval t (lock s e)
eval (unbox t nil)        s
  = unbox' (eval t s) nil
eval (unbox t (ext e))    (s , _)
  = eval (unbox t e) s
eval (unbox t (C.extğŸ”’ f e)) (lock s C.nil)
  = eval (unbox t e) s
eval (unbox t (C.extğŸ”’ f e)) (lock s (C.ext e'))
  = wkTm' fresh (eval (unbox t (extğŸ”’ _ e)) (lock s e'))
eval (unbox t (C.extğŸ”’ f C.nil)) (lock s (C.extğŸ”’ x e'))
  = unbox' (eval t s) (extğŸ”’ tt e')
eval (unbox t (C.extğŸ”’ f (C.ext e))) (lock (s , _) (C.extğŸ”’ x e'))
  = eval (unbox t (extğŸ”’ tt e)) (lock s (extğŸ”’ f e'))
eval (unbox t (extğŸ”’ f (C.extğŸ”’ _ e))) (lock (lock s e'') (C.extğŸ”’ _ e'))
  = eval (unbox t (extğŸ”’ _ e)) (lock s (extğŸ”’ _ (extRAssoc e'' e')))

-- retraction of interpretation
quot : (Sub'- Î“ â†’Ì‡ Tm'- a) â†’ Nf Î“ a
quot f = reify (f idâ‚›')

-- normalization function
norm : Tm Î“ a â†’ Nf Î“ a
norm t = quot (eval t)

----------------------------------
-- Normalization for substitutions
----------------------------------

-- (simply "do everything pointwise")

-- normal forms of substitutions
data Nfâ‚› : Ctx â†’ Ctx â†’ Set where
  []   : Nfâ‚› Î“ []
  _`,_ : Nfâ‚› Î“ Î” â†’ Nf Î“ a â†’ Nfâ‚› Î“ (Î” `, a)
  lock : Nfâ‚› Î”L Î“ â†’ Ext tt Î” Î”L Î”R â†’ Nfâ‚› Î” (Î“ ğŸ”’)

-- embeddding of substitution normal forms back into substitutions
embNfâ‚› : Nfâ‚› Î“ Î” â†’ Sub Î“ Î”
embNfâ‚› []         = []
embNfâ‚› (n `, s)   = embNfâ‚› n `, embNf s
embNfâ‚› (lock n s) = lock (embNfâ‚› n) s

Nfâ‚›- : Ctx â†’ Ctx â†’ Set
Nfâ‚›- Î” Î“ = Nfâ‚› Î“ Î”

-- interpretation of substitutions
evalâ‚› : Sub Î“ Î” â†’ Sub'- Î“  â†’Ì‡ Sub'- Î”
evalâ‚› []                                 s'
  = tt
evalâ‚› (s `, t)                           s'
  = (evalâ‚› s s') , eval t s'
evalâ‚› (lock s nil)                       s'
  = lock (evalâ‚› s s') nil
evalâ‚› (lock s (ext e))                   (s' , _)
  = evalâ‚› (lock s e) s'
evalâ‚› (S.lock s (C.extğŸ”’ f C.nil))        (lock s' e')
  = lock (evalâ‚› s s') e'
evalâ‚› (S.lock s (C.extğŸ”’ f (C.ext e)))    (lock (s' , _) e')
  = evalâ‚› (lock s (extğŸ”’ tt e)) (lock s' e')
evalâ‚› (S.lock s (C.extğŸ”’ f (C.extğŸ”’ x e))) (lock (lock s' e'') e')
  = evalâ‚› (lock s (extğŸ”’ tt e)) (lock s' (extRAssoc e'' e'))

-- retraction of evalâ‚›
quotâ‚› : Sub'- Î“ â†’Ì‡ Nfâ‚›- Î“
quotâ‚› {[]}     tt         = []
quotâ‚› {Î“ `, _} (s , x)    = (quotâ‚› s) `, (reify x)
quotâ‚› {Î“ ğŸ”’}    (lock s e) = lock (quotâ‚› s) e

-- normalization function, for substitutions
normâ‚› : Sub Î” Î“ â†’ Nfâ‚› Î” Î“
normâ‚› s = quotâ‚› (evalâ‚› s idâ‚›')
