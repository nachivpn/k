module IS4.Term.Base where

open import Data.Product using (âˆƒ; _Ã—_; _,_; -,_; projâ‚; projâ‚‚)

open import IK.Type public

open import Context Ty public using (Var)
open import Context Ty public hiding (Var; extğŸ”’)

--------
-- Terms
--------

data Tm : Ctx â†’ Ty â†’ Set where

  var  : (v : Var Î“ a)
       ---------------
       â†’ Tm Î“ a

  lam  : (t : Tm (Î“ `, a) b)
         -------------------
       â†’ Tm Î“ (a â‡’ b)

  app  : (t : Tm Î“ (a â‡’ b))
       â†’ (u : Tm Î“ a)
         ------------------
       â†’ Tm Î“ b

  box   : (t : Tm (Î“ ğŸ”’) a)
        ------------------
        â†’ Tm Î“ (â—» a)

  unbox : (t : Tm Î“L (â—» a))
        â†’ (e : CExt Î“ Î“L Î“R)
        --------------------
        â†’ Tm Î“ a

variable
  t t' t'' : Tm Î“ a
  u u' u'' : Tm Î“ a

-- weaken a term
wkTm : Î“ âŠ† Î“' â†’ Tm Î“ a â†’ Tm Î“' a
wkTm w (var x)     = var (wkVar w x)
wkTm w (lam t)     = lam (wkTm (keep w) t)
wkTm w (app t u)   = app (wkTm w t) (wkTm w u)
wkTm w (box t)     = box (wkTm (keepğŸ”’ w) t)
wkTm w (unbox t e) = unbox (wkTm (factor2â‰¤ e w) t) (factor2Ext e w)

-- "drop" the last variable in the context
dropâ‚œ : Tm Î“ b â†’ Tm (Î“ `, a) b
dropâ‚œ = wkTm fresh

-- extension that "generates a new context frame"
new : CExt (Î“ ğŸ”’) Î“ ([] ğŸ”’) -- Î“ R Î“ ğŸ”’
new = extğŸ”’- nil

new[_] = Î» Î“ â†’ new {Î“}

open Substitution Tm var wkTm CExt new f2LCtx factor2â‰¤ f2RCtx factor2Ext public
  renaming (module Composition to SubstitutionComposition)

factor2 : âˆ€ (e : CExt Î“ Î“L Î“R) (s : Sub Î” Î“) â†’ âˆƒ Î» Î”L â†’ âˆƒ Î» Î”R â†’ Sub Î”L Î“L Ã— CExt Î” Î”L Î”R
factor2 nil        s           = -, -, s , nil
factor2 (ext e)    (s `, t)    = factor2 e s
factor2 (extğŸ”’- e) (lock s e')  = let (Î”L , Î”R , s' , e'') = factor2 e s in -, -, s' , extRAssoc e'' e'

f2LCtxâ‚› : âˆ€ (e : CExt Î“ Î“L Î“R) (s : Sub Î” Î“) â†’ Ctx
f2LCtxâ‚› = Î» e s â†’ factor2 e s .projâ‚

f2RCtxâ‚› : âˆ€ (e : CExt Î“ Î“L Î“R) (s : Sub Î” Î“) â†’ Ctx
f2RCtxâ‚› = Î» e s â†’ factor2 e s .projâ‚‚ .projâ‚

factor2Sub : âˆ€ (e : CExt Î“ Î“L Î“R) (s : Sub Î” Î“) â†’ Sub (f2LCtxâ‚› e s) Î“L
factor2Sub = Î» e s â†’ factor2 e s .projâ‚‚ .projâ‚‚ .projâ‚

factor2Extâ‚› : âˆ€ (e : CExt Î“ Î“L Î“R) (s : Sub Î” Î“) â†’ CExt Î” (f2LCtxâ‚› e s) _
factor2Extâ‚› = Î» e s â†’ factor2 e s .projâ‚‚ .projâ‚‚ .projâ‚‚

-- apply substitution to a term
substTm : Sub Î” Î“ â†’ Tm Î“ a â†’ Tm Î” a
substTm s (var x)     = substVar s x
substTm s (lam t)     = lam (substTm (keepâ‚› s) t)
substTm s (app t u)   = app (substTm s t) (substTm s u)
substTm s (box t)     = box (substTm (keepğŸ”’â‚› s) t)
substTm s (unbox t e) = unbox (substTm (factor2Sub e s) t) (factor2Extâ‚› e s)

open SubstitutionComposition substTm f2LCtxâ‚› factor2Sub f2RCtxâ‚› factor2Extâ‚› public

----------------
-- Derived terms
----------------

ğŸ”’-Î· : Sub Î“ (Î“ ğŸ”’)
ğŸ”’-Î· = lock idâ‚› nil

ğŸ”’-Î¼ : Sub (Î“ ğŸ”’ ğŸ”’) (Î“ ğŸ”’)
ğŸ”’-Î¼ = lock idâ‚› (extğŸ”’- (extğŸ”’- nil))

Î»â€² : Tm Î“ (â—» a) â†’ Tm (Î“ ğŸ”’) a
Î»â€² t = unbox t new

â—»-Îµ : Tm Î“ (â—» a) â†’ Tm Î“ a
â—»-Îµ t = substTm ğŸ”’-Î· (Î»â€² t)

â—»-Î´ : Tm Î“ (â—» a) â†’ Tm Î“ (â—» â—» a)
â—»-Î´ t = box (box (substTm ğŸ”’-Î¼ (Î»â€² t)))
