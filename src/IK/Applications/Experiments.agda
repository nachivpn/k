{-# OPTIONS --without-K #-}
module IK.Applications.Experiments where

import Context

open import IK.Norm.Base
open import IK.Term

open import IK.Applications.Neutrality

open import Data.Empty
open import Relation.Nullary
open import Relation.Binary.PropositionalEquality

-- if `a` isn't a subformula (sf) of `Î“`,
-- then it isn't a sf of its prefix.
sfPrefix : Â¬ (a âŠ²á¶œ Î“) â†’ Ext Î¸ Î“ Î“L Î“R â†’ Â¬ (a âŠ²á¶œ Î“L)
sfPrefix noA nil        = noA
sfPrefix noA (ext e)    = sfPrefix (Î» z â†’ noA (there z)) e
sfPrefix noA (ext# x e) = sfPrefix (Î» z â†’ noA (there# z)) e

-- if ğ•“ is not a subformula of Î“, then any normal form
-- of the type `Nf Î“ (ğ•“ â‡’ ğ•“)` must be the identity function
uniqIdFun : Â¬ (ğ•“ âŠ²á¶œ Î“) â†’ (n : Nf Î“ (ğ•“ â‡’ ğ•“)) â†’ n â‰¡ lam (upğ•“ (var zero))
uniqIdFun noB (lam (upğ•“ (var zero)))        = refl
uniqIdFun noB (lam (upğ•“ (var (succ x))))    = âŠ¥-elim (noB (neutrVar x))
uniqIdFun noB (lam (upğ•“ (app m n)))    with neutrality m
... | there p                               =
  âŠ¥-elim (noB (âŠ²-lift (sbrâ‡’ âŠ²-refl) p))
uniqIdFun noB (lam (upğ•“ (unbox n (ext e)))) =
  âŠ¥-elim (sfPrefix noB e (âŠ²-lift (sbâ–¡ âŠ²-refl) (there# (neutrality n))))

-- if there are no boxed-formulas in `Î“`, then there are no neutrals in `Î“ #`
noLeftPeek : ({x : Ty} â†’ Â¬ (â–¡ x âŠ²á¶œ Î“)) â†’ Â¬ (Ne (Î“ #) a)
noLeftPeek f (app n x)     = noLeftPeek f n
noLeftPeek f (unbox n nil) = f (neutrality n)

-- strengthening relation
data _â‹—_  : Ctx â†’ Ctx â†’ Set where
  add#  : [] â‹— [#]
  keep  : Î“ â‹— Î” â†’ (Î“ `, a) â‹— (Î” `, a)
  keep# : Î“ â‹— Î” â†’ (Î“ #) â‹— (Î” #)

-- strengthening is the identity on variables
strenVar : Î“' â‹— Î“ â†’ Var Î“ a â†’ Var Î“' a
strenVar (keep w) zero     = zero
strenVar (keep w) (succ x) = succ (strenVar w x)

strenNe : Î“' â‹— Î“ â†’ Ne Î“ a â†’ Ne Î“' a
strenNf : Î“' â‹— Î“ â†’ Nf Î“ a â†’ Nf Î“' a

strenNe r          (var x)           = var (strenVar r x)
strenNe r          (app n x)         = app (strenNe r n) (strenNf r x)
strenNe add#       (unbox n nil)     = âŠ¥-elim (noClosedNe n)
strenNe (keep# r)  (unbox n nil)     = unbox (strenNe r n) nil
strenNe (keep r)   (unbox n (ext x)) = wkNe fresh (strenNe r (unbox n x))

strenNf r (upğ•“ x) = upğ•“ (strenNe r x)
strenNf r (lam n) = lam (strenNf (keep r) n)
strenNf r (box n) = box (strenNf (keep# r) n)

-- NOTE:
-- direct induction to show strengthing for terms fails;
-- consider a `t : Tm [] (â–¡ a)` and defining
-- `strenTm : Î“' â‹— Î“ â†’ Tm Î“ a â†’ Tm Î“' a`,
-- what should `strenTm add# (unbox t nil) : Tm [] a` be?

strenTm : Î“' â‹— Î“ â†’ Tm Î“ a â†’ Tm Î“' a
strenTm r t = embNf (strenNf r (norm t))

module _ where

  -- Show that `a` is a theorem iff `â–¡ a` is a theorem,
  -- i.e., [] âŠ¢ a iff [] âŠ¢ â–¡ a.

  -- forth : Tm [] a â†’ Tm Î“ (â–¡ a)
  -- forth t = {!!}

  back : Tm [] (â–¡ a) â†’ Tm [] a
  back t = embNf (strenNf add# (norm (unbox t nil)))

noFreeUnbox : Â¬ (Nf [] (â–¡ ğ•“ â‡’ ğ•“))
noFreeUnbox (lam (upğ•“ (var (succ ()))))
noFreeUnbox (lam (upğ•“ (app n _))) with neutrality n
... | here (sbâ–¡ ())
noFreeUnbox (lam (upğ•“ (unbox x (ext ()))))

noFreeBox : Â¬ (Nf [] (ğ•“ â‡’ â–¡ ğ•“))
noFreeBox (lam (box (upğ•“ (app n _)))) with neutrality n
... | there# (here ())
... | there# (there ())
noFreeBox (lam (box (upğ•“ (unbox (var (succ ())) nil))))
noFreeBox (lam (box (upğ•“ (unbox (app n _) nil)))) with neutrality n
... | here ()
... | there ()
noFreeBox (lam (box (upğ•“ (unbox (unbox _ (ext ())) nil))))
