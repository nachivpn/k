{-# OPTIONS --safe --with-K #-}
module IS4.Term.Properties where

open import Relation.Binary.PropositionalEquality
  using    (_â‰¡_ ; cong ; congâ‚‚ ; subst ; substâ‚‚ ; subst-subst ; subst-sym-subst ; module â‰¡-Reasoning)
  renaming (refl to â‰¡-refl ; sym to â‰¡-sym ; trans to â‰¡-trans)

import Relation.Binary.Reasoning.Setoid as SetoidReasoning
module RelReasoning {a} {A : Set a} {r} (R : A â†’ A â†’ Set r) where
  â‰¡-step-â‰¡ : âˆ€ {x'} {x} {y} {y'} â†’ x' â‰¡ x â†’ R x y â†’ y â‰¡ y' â†’ R x' y'
  â‰¡-step-â‰¡ â‰¡-refl r â‰¡-refl = r

  step-â‰¡ : âˆ€ {x} {y} {y'} â†’ R x y â†’ y â‰¡ y' â†’ R x y'
  step-â‰¡ = â‰¡-step-â‰¡ â‰¡-refl

open import HEUtil
open import PEUtil renaming (Ë˜trans to â‰¡-Ë˜trans)

open import IS4.Term.Base
open import IS4.Term.Conversion
open import IS4.Term.NormalForm
open import IS4.Term.Reduction

----------------------
-- Substitution lemmas
----------------------

-- Left context of weakening and applying a substituion
-- is the same as the
-- Left context of applying and then weakening it
lCtxâ‚›-wkSub-comm : (e  : CExt Î“ Î“L Î“R) (w  : Î” âŠ† Î”') (s  : Sub Î” Î“)
  â†’ lCtxâ‚› e (wkSub w s) â‰¡ lCtx (factorExtâ‚› e s) w
lCtxâ‚›-wkSub-comm nil       w s           = â‰¡-refl
lCtxâ‚›-wkSub-comm (ext e)   w (s `, _)    = lCtxâ‚›-wkSub-comm e w s
lCtxâ‚›-wkSub-comm (extğŸ”’- e) w (lock s e') = â‰¡-trans
  (lCtxâ‚›-wkSub-comm e (factorWk e' w) s)
  (â‰¡-sym (lCtxPresTrans (factorExtâ‚› e _) e' _))

-- Right context of weakening and applying a substituion
-- is the same as the
-- Right context of applying and then weakening it
rCtxâ‚›-wkSub-comm : (e  : CExt Î“ Î“L Î“R) (w  : Î” âŠ† Î”') (s  : Sub Î” Î“)
  â†’ rCtxâ‚› e (wkSub w s) â‰¡ rCtx (factorExtâ‚› e s) w
rCtxâ‚›-wkSub-comm nil w s                 = â‰¡-refl
rCtxâ‚›-wkSub-comm (ext e) w (s `, _)      = rCtxâ‚›-wkSub-comm e w s
rCtxâ‚›-wkSub-comm (extğŸ”’- e) w (lock s e') = â‰¡-trans
  (congâ‚‚ _,,_ (rCtxâ‚›-wkSub-comm e (factorWk e' w) s) â‰¡-refl)
  (â‰¡-sym (rCtxPresTrans (factorExtâ‚› e _) e' _))

-- Weakening and factoring a subtitution can be achieved by factoring and then weakening it
factorSubâ‚›-wkSub-comm : (e :  CExt Î“ Î“L Î“R) (s  : Sub Î” Î“) (w : Î” âŠ† Î”')
  â†’ subst (Î» Î”L â†’ Sub Î”L Î“L) (lCtxâ‚›-wkSub-comm e w s) (factorSubâ‚› e (wkSub w s)) â‰¡ wkSub (factorWk (factorExtâ‚› e s) w) (factorSubâ‚› e s)
factorSubâ‚›-wkSub-comm nil       s           w = â‰¡-refl
factorSubâ‚›-wkSub-comm (ext e)   (s `, t)    w = factorSubâ‚›-wkSub-comm e s w
factorSubâ‚›-wkSub-comm (extğŸ”’- e) (lock s e') w = let open â‰¡-Reasoning in begin
  subst (Î» Î”L â†’ Sub Î”L _)
    (â‰¡-trans (lCtxâ‚›-wkSub-comm e _ _) (â‰¡-sym (lCtxPresTrans _ e' _)))
    (factorSubâ‚› e (wkSub (factorWk e' w) s))
    -- split `subst _ (trans p q) ...` to `subst _ q (subst _ p ...)`
    â‰¡âŸ¨ â‰¡-sym (subst-subst (lCtxâ‚›-wkSub-comm e _ _)) âŸ©
  subst (Î» Î”L â†’ Sub Î”L _)
    (â‰¡-sym (lCtxPresTrans _ e' _))
    (subst (Î» Î”L â†’ Sub Î”L _) (lCtxâ‚›-wkSub-comm e _ _)
      (factorSubâ‚› e (wkSub (factorWk e' w) s)))
    -- rewrite inner subst
    â‰¡âŸ¨ cong (subst (Î» Î”L â†’ Sub Î”L _) _) (factorSubâ‚›-wkSub-comm e s (factorWk e' w)) âŸ©
  subst (Î» Î”L â†’ Sub Î”L _)
    (â‰¡-sym (lCtxPresTrans _ e' _))
    (wkSub (factorWk (factorExtâ‚› e s) (factorWk e' w)) (factorSubâ‚› e s))
    -- remove subst and apply factorWkPresTrans
    â‰…âŸ¨ â‰…-trans (â‰¡-subst-removable _ _ _) factorWkPresTrans-under-wkSub âŸ©
 wkSub (factorWk (extRAssoc (factorExtâ‚› e s) e') w) (factorSubâ‚› e s) âˆ
 where
   factorWkPresTrans-under-wkSub : wkSub (factorWk (factorExtâ‚› e s) (factorWk e' w)) _ â‰… wkSub (factorWk (extRAssoc (factorExtâ‚› e s) e') w) _
   factorWkPresTrans-under-wkSub = â‰…-cong (_ âŠ†_) (â‰¡-sym (lCtxPresTrans _ e' _)) (Î» s' â†’ wkSub s' _)
     (â‰…-sym (â‰…-trans (â‰¡-subst-addable _ _ _) (â‰¡-to-â‰… (factorWkPresTrans _ e' _))))

-- factorExtâ‚› counterpart of factorSubâ‚›-wkSub-comm
factorExtâ‚›-wkSub-comm : (e :  CExt Î“ Î“L Î“R) (s  : Sub Î” Î“) (w : Î” âŠ† Î”')
  â†’ substâ‚‚ (CExt Î”') (lCtxâ‚›-wkSub-comm e w s) (rCtxâ‚›-wkSub-comm e w s) (factorExtâ‚› e (wkSub w s)) â‰¡ factorExt (factorExtâ‚› e s) w
factorExtâ‚›-wkSub-comm _ _ _ = ExtIsProp _ _

lCtxâ‚›-factorExt-trimSub-assoc : (e : CExt Î“ Î“L Î“R) (s : Sub Î”' Î”) (w : Î“ âŠ† Î”)
   â†’ lCtxâ‚› e (trimSub w s) â‰¡ lCtxâ‚› (factorExt e w) s
lCtxâ‚›-factorExt-trimSub-assoc nil       s          w
  = â‰¡-refl
lCtxâ‚›-factorExt-trimSub-assoc (ext e)   (s `, _)   (drop w)
  = lCtxâ‚›-factorExt-trimSub-assoc (ext e) s w
lCtxâ‚›-factorExt-trimSub-assoc (ext e)   (s `, _)   (keep w)
  = lCtxâ‚›-factorExt-trimSub-assoc e s w
lCtxâ‚›-factorExt-trimSub-assoc (extğŸ”’- e) (s `, t)   (drop w)
  = lCtxâ‚›-factorExt-trimSub-assoc (extğŸ”’- e) s w
lCtxâ‚›-factorExt-trimSub-assoc (extğŸ”’- e) (lock s _) (keepğŸ”’ w)
  = lCtxâ‚›-factorExt-trimSub-assoc e s w

rCtxâ‚›-factorExt-trimSub-assoc : (e : CExt Î“ Î“L Î“R) (s : Sub Î”' Î”) (w : Î“ âŠ† Î”)
   â†’ rCtxâ‚› e (trimSub w s) â‰¡ rCtxâ‚› (factorExt e w) s
rCtxâ‚›-factorExt-trimSub-assoc nil       s          w
  = â‰¡-refl
rCtxâ‚›-factorExt-trimSub-assoc (ext e)   (s `, t)   (drop w)
  = rCtxâ‚›-factorExt-trimSub-assoc (ext e) s w
rCtxâ‚›-factorExt-trimSub-assoc (ext e)   (s `, t)   (keep w)
  = rCtxâ‚›-factorExt-trimSub-assoc e s w
rCtxâ‚›-factorExt-trimSub-assoc (extğŸ”’- e) (s `, t)   (drop w)
  = rCtxâ‚›-factorExt-trimSub-assoc (extğŸ”’- e) s w
rCtxâ‚›-factorExt-trimSub-assoc (extğŸ”’- e) (lock s _) (keepğŸ”’ w)
  = cong (_,, _) (rCtxâ‚›-factorExt-trimSub-assoc e s w)

factorSubâ‚›-trimSub-comm : (e : CExt Î“ Î“L Î“R) (s : Sub Î”' Î”) (w : Î“ âŠ† Î”)
  â†’ subst (Î» Î”L â†’ Sub Î”L _) (lCtxâ‚›-factorExt-trimSub-assoc e s w) (factorSubâ‚› e (trimSub w s)) â‰¡ trimSub (factorWk e w) (factorSubâ‚› (factorExt e w) s)
factorSubâ‚›-trimSub-comm nil       s          w
  = â‰¡-refl
factorSubâ‚›-trimSub-comm (ext e)   (s `, _)   (drop w)
  = factorSubâ‚›-trimSub-comm (ext e) s w
factorSubâ‚›-trimSub-comm (ext e)   (s `, _)   (keep w)
  = factorSubâ‚›-trimSub-comm e s w
factorSubâ‚›-trimSub-comm (extğŸ”’- e) (s `, t)   (drop w)
  = factorSubâ‚›-trimSub-comm (extğŸ”’- e) s w
factorSubâ‚›-trimSub-comm (extğŸ”’- e) (lock s _) (keepğŸ”’ w)
  = factorSubâ‚›-trimSub-comm e s w

factorExtâ‚›-trimSub-comm : (e : CExt Î“ Î“L Î“R) (s : Sub Î”' Î”) (w : Î“ âŠ† Î”)
  â†’ substâ‚‚ (CExt Î”') (lCtxâ‚›-factorExt-trimSub-assoc e s w) (rCtxâ‚›-factorExt-trimSub-assoc e s w) (factorExtâ‚› e (trimSub w s)) â‰¡ factorExtâ‚› (factorExt e w) s
factorExtâ‚›-trimSub-comm _ _ _ = ExtIsProp _ _

---------------------------------------------
-- Factorisation of the identity substitution
---------------------------------------------

â†ğŸ”’â‚rCtx : (e : CExt Î“ Î“L Î“R) â†’ Ctx
â†ğŸ”’â‚rCtx nil             = []
â†ğŸ”’â‚rCtx (ext {a = a} e) = â†ğŸ”’â‚rCtx e ,, rCtxâ€² (factorExtâ‚› e idâ‚›) freshExt[ a ]
â†ğŸ”’â‚rCtx (extğŸ”’- e)       = â†ğŸ”’â‚rCtx e

private

  ex : {a b c : Ty} â†’ CExt (Î“L `, a `, b ğŸ”’ `, c ğŸ”’) Î“L ([] `, a `, b ğŸ”’ `, c ğŸ”’)
  ex {Î“} {a} {b} {c} = extğŸ”’- (ext[ c ] (extğŸ”’- (ext[ b ] (ext[ a ] nil))))

  _ : â†ğŸ”’â‚rCtx (ex {Î“L} {c = c}) â‰¡ [] `, a `, b
  _ = â‰¡-refl

-- Given `e` that Î“L extends Î“, Î“L is a lock-free extension of `lCtxâ‚› e idâ‚›`.
-- This means that Î“L âŠ† (lCtxâ‚› e idâ‚›), and thus applying `factorSubâ‚› e idâ‚›` weakens
-- a term with variables in `â†ğŸ”’â‚rCtx e`
factorSubâ‚›IdWk : (e : CExt Î“ Î“L Î“R) â†’ LFExt (lCtxâ‚› e idâ‚›) Î“L (â†ğŸ”’â‚rCtx e)
factorSubâ‚›IdWk nil             = nil
factorSubâ‚›IdWk {Î“R = Î“R `, a} (ext {a = .a} e) = subst
  (Î» Î“ â†’ LFExt Î“ _ (â†ğŸ”’â‚rCtx (ext e))) (â‰¡-sym ((lCtxâ‚›-wkSub-comm e fresh idâ‚›)))
  (extRAssoc (factorSubâ‚›IdWk e) (factorDropsWk (factorExtâ‚› e idâ‚›) freshExt))
factorSubâ‚›IdWk (extğŸ”’- e)       = factorSubâ‚›IdWk e

-- Obs: Deliberately named _Wk instead of _LFExt

--------------------
-- Substitution laws
--------------------

-- NOTE: these are only the laws that follow directly from the structure of substitutions
coh-trimSub-wkVar : (x : Var Î“ a) (s : Sub Î”' Î”) (w : Î“ âŠ† Î”)
  â†’ substVar (trimSub w s) x â‰¡ substVar s (wkVar w x)
coh-trimSub-wkVar ze (s `, x) (drop w)
  = coh-trimSub-wkVar ze s w
coh-trimSub-wkVar ze (s `, x) (keep w)
  = â‰¡-refl
coh-trimSub-wkVar (su x) (s `, xâ‚) (drop w)
  = coh-trimSub-wkVar (su x) s w
coh-trimSub-wkVar (su x) (s `, xâ‚) (keep w)
  = coh-trimSub-wkVar x s w

-- `trimSub` preserves the identity
trimSubPresId : (s : Sub Î” Î“) â†’ trimSub idWk s â‰¡ s
trimSubPresId []         = â‰¡-refl
trimSubPresId (s `, x)   = cong (_`, _) (trimSubPresId s)
trimSubPresId (lock s x) = congâ‚‚ lock (trimSubPresId s) â‰¡-refl

-- naturality of substVar
nat-substVar : (x : Var Î“ a) (s : Sub Î” Î“) (w : Î” âŠ† Î”')
  â†’ substVar (wkSub w s) x â‰¡ wkTm w (substVar s x)
nat-substVar ze     (s `, t) w = â‰¡-refl
nat-substVar (su x) (s `, t) w = nat-substVar x s w

-- naturality of trimSub
nat-trimSub : (s : Sub Î“ Î”) (w : Î”' âŠ† Î”) (w' : Î“ âŠ† Î“')
  â†’ wkSub w' (trimSub w s) â‰¡ trimSub w (wkSub w' s)
nat-trimSub []         base      w' = â‰¡-refl
nat-trimSub (s `, t)   (drop w)  w' = nat-trimSub s w w'
nat-trimSub (s `, t)   (keep w)  w' = cong (_`, wkTm w' t) (nat-trimSub s w w')
nat-trimSub (lock s x) (keepğŸ”’ w) w' = congâ‚‚ lock (nat-trimSub s w _) â‰¡-refl

-- `trimSub` on the identity substitution embeds the weakening
trimSubId : (w : Î“ âŠ† Î”) â†’ trimSub w idâ‚› â‰¡ embWk w
trimSubId base = â‰¡-refl
trimSubId (drop w) = â‰¡-trans
  (â‰¡-sym (nat-trimSub idâ‚› w fresh))
  (cong (wkSub fresh) (trimSubId w))
trimSubId (keep w) = cong (_`, var ze) (â‰¡-trans
  (â‰¡-sym (nat-trimSub idâ‚› w fresh))
  (cong (wkSub fresh) (trimSubId w)))
trimSubId (keepğŸ”’ w) = congâ‚‚ lock (trimSubId w) â‰¡-refl

---------------------------
-- Hell Of Syntactic Lemmas
---------------------------

-- Welcome to the hell of mind-numbing syntactic lemmas.
-- No good ever comes from proving these lemmas, but no
-- good can happen without proving them.

cong-unboxâ‰… : {Î“L1 Î“L2 Î“R1 Î“R2 : Ctx} â†’
           Î“L1 â‰¡ Î“L2 â†’ Î“R1 â‰¡ Î“R2 â†’
          {t1 : Tm Î“L1 (â—» a)} {t2 : Tm Î“L2 (â—» a)} {e1 : CExt Î“ Î“L1 Î“R1} {e2 : CExt Î“ Î“L2 Î“R2} â†’
          t1 â‰… t2 â†’
          e1 â‰… e2 â†’
          unbox t1 e1 â‰… unbox t2 e2
cong-unboxâ‰… {Î“ = Î“} Î“L1â‰¡Î“L2 Î“R1â‰¡Î“R2 t1â‰…t2 e1â‰…e2
  = xcong (Î» Î“L â†’ Tm Î“L _) (CExt Î“) {R = Î» _ _ â†’ Tm Î“ _} Î“L1â‰¡Î“L2 Î“R1â‰¡Î“R2 unbox t1â‰…t2 e1â‰…e2

cong-lockâ‰… : {Î“L1 Î“L2 Î“R1 Î“R2 : Ctx} â†’
           Î“L1 â‰¡ Î“L2 â†’ Î“R1 â‰¡ Î“R2 â†’
          {s1 : Sub Î“L1 Î”} {s2 : Sub Î“L2 Î”} {e1 : CExt Î“ Î“L1 Î“R1} {e2 : CExt Î“ Î“L2 Î“R2} â†’
          s1 â‰… s2 â†’
          e1 â‰… e2 â†’
          lock s1 e1 â‰… lock s2 e2
cong-lockâ‰… {Î“ = Î“} Î“L1â‰¡Î“L2 Î“R1â‰¡Î“R2 s1â‰…s2 e1â‰…e2
  = xcong (Î» Î“L â†’ Sub Î“L _) (CExt Î“) {R = Î» _ _ â†’ Sub Î“ _} Î“L1â‰¡Î“L2 Î“R1â‰¡Î“R2 lock s1â‰…s2 e1â‰…e2

subst-sym : âˆ€ {A : Set} {x y : A} {P : A â†’ Set} {p : P x} {q : P y} (xâ‰¡y : x â‰¡ y)
  â†’ subst P xâ‰¡y p â‰¡ q
  â†’ p â‰¡ subst P (â‰¡-sym xâ‰¡y) q
subst-sym {P = P} {p = p} xâ‰¡y q =
  â‰¡-sym (subst (Î» z â†’ subst P (â‰¡-sym xâ‰¡y) z â‰¡ p) q (subst-sym-subst xâ‰¡y))

wkTmPresId : (t : Tm Î“ a) â†’ wkTm idWk t â‰¡ t
wkTmPresId (var x)     = cong var (wkVarPresId x)
wkTmPresId (lam t)     = cong lam (wkTmPresId t)
wkTmPresId (app t u)   = congâ‚‚ app (wkTmPresId t) (wkTmPresId u)
wkTmPresId (box t)     = cong box (wkTmPresId t)
wkTmPresId {Î“ = Î“} {a = a} (unbox {Î“L = Î“L} {Î“R = Î“R} t e) = let open â‰¡-Reasoning in begin
  wkTm idWk (unbox t e)
    â‰¡âŸ¨âŸ©
  unbox {Î“L = lCtx e idWk} {Î“R = rCtx e idWk} (wkTm (factorWk e idWk[ Î“ ]) t) (factorExt e idWk[ Î“ ])
    â‰…âŸ¨ xcong
      (Î» Î“L â†’ Tm Î“L (â—» a)) (CExt Î“)
      (lCtxPresId e) (rCtxPresId e)
      unbox
      factorWkPresId-under-wkTm
      (â‰¡-substâ‚‚-addable (CExt Î“) _ _ (factorExt _ _)) âŸ©
  unbox {Î“L = Î“L} {Î“R = Î“R} (wkTm idWk[ Î“L ] t) (substâ‚‚ (CExt Î“) (lCtxPresId e) (rCtxPresId e) (factorExt e idWk))
    â‰¡âŸ¨ congâ‚‚ unbox (wkTmPresId t) (factorExtPresId e) âŸ©
  unbox t e âˆ
    where
      factorWkPresId-under-wkTm : wkTm (factorWk e idWk) t â‰… wkTm idWk t
      factorWkPresId-under-wkTm = â‰…-cong (Î“L âŠ†_) (lCtxPresId e) (Î» w â†’ wkTm w t)
        (â‰…-trans (â‰¡-subst-addable _ _ _) (â‰¡-to-â‰… (factorWkPresId e)))

wkSubPresId : (s : Sub Î” Î“) â†’ wkSub idWk s â‰¡ s
wkSubPresId []         = â‰¡-refl
wkSubPresId (s `, t)   = congâ‚‚ _`,_ (wkSubPresId s) (wkTmPresId t)
wkSubPresId {Î” = Î”} (lock {Î”L = Î”L} {Î“ = Î“} s e) = let open â‰¡-Reasoning in begin
  wkSub idWk (lock s e)
    â‰¡âŸ¨âŸ©
  lock (wkSub (factorWk e idWk) s) (factorExt e idWk)
    â‰…âŸ¨ xcong
      (Î» Î”L â†’ Sub Î”L Î“) (CExt Î”)
      (lCtxPresId e) (rCtxPresId e)
      lock
      factorWkPresId-under-wkSub
      (â‰¡-substâ‚‚-addable (CExt Î”) _ _ (factorExt _ _)) âŸ©
  lock (wkSub idWk s) (substâ‚‚ (CExt Î”) (lCtxPresId e) (rCtxPresId e) (factorExt e idWk))
    â‰¡âŸ¨ congâ‚‚ lock (wkSubPresId s) (factorExtPresId e) âŸ©
  lock s e âˆ
    where
      factorWkPresId-under-wkSub : wkSub (factorWk e idWk) s â‰… wkSub idWk s
      factorWkPresId-under-wkSub = â‰…-cong (Î”L âŠ†_) (lCtxPresId e) (Î» w â†’ wkSub w s)
        (â‰…-trans (â‰¡-subst-addable _ _ _) (â‰¡-to-â‰… (factorWkPresId e)))

wkTmPresâˆ™ : (w : Î“ âŠ† Î“') (w' : Î“' âŠ† Î“'') (t : Tm Î“ a)
  â†’ wkTm w' (wkTm w t) â‰¡ wkTm (w âˆ™ w') t
wkTmPresâˆ™ w w' (var x)     = cong var (wkVarPresâˆ™ w w' x)
wkTmPresâˆ™ w w' (lam t)     = cong lam (wkTmPresâˆ™ (keep w) (keep w') t)
wkTmPresâˆ™ w w' (app t u)   = congâ‚‚ app (wkTmPresâˆ™ w w' t) (wkTmPresâˆ™ w w' u)
wkTmPresâˆ™ w w' (box t)     = cong box (wkTmPresâˆ™ (keepğŸ”’ w) (keepğŸ”’ w') t)
wkTmPresâˆ™ {Î“ = Î“} {Î“' = Î“'} {Î“'' = Î“''} w w' (unbox {Î“L = Î“L} {a = a} {Î“R = Î“R} t e) = let open â‰¡-Reasoning in begin
  wkTm w' (wkTm w (unbox t e))
    â‰¡âŸ¨âŸ©
  unbox {Î“L = lCtx (factorExt e w) w'} {Î“R = rCtx (factorExt e w) w'}
    (wkTm (factorWk (factorExt e w) w') (wkTm (factorWk e w) t))
    (factorExt (factorExt e w) w')
    â‰¡âŸ¨ congâ‚‚ unbox (wkTmPresâˆ™ _ _ t) (â‰¡-sym (factorExtPresâˆ™ _ _ _)) âŸ©
  unbox {Î“L = lCtx (factorExt e w) w'} {Î“R = rCtx (factorExt e w) w'}
    (wkTm (factorWk e w âˆ™ factorWk (factorExt e w) w') t)
    (substâ‚‚ (CExt Î“'') (lCtxPresâˆ™ e w w') (rCtxPresâˆ™ e w w') (factorExt e (w âˆ™ w')))
    â‰…âŸ¨ xcong
      (Î» Î“L â†’ Tm Î“L (â—» a)) (CExt Î“'')
      (â‰¡-sym (lCtxPresâˆ™ e w w')) (â‰¡-sym (rCtxPresâˆ™ e w w'))
      unbox
      factorWkPresâˆ™-under-wkTm
      (â‰¡-substâ‚‚-removable (CExt Î“'') (lCtxPresâˆ™ e w w') (rCtxPresâˆ™ e w w') (factorExt e (w âˆ™ w'))) âŸ©
  unbox {Î“L = lCtx e (w âˆ™ w')} {Î“R = rCtx e (w âˆ™ w')} (wkTm (factorWk e (w âˆ™ w')) t) (factorExt e (w âˆ™ w'))
    â‰¡âŸ¨âŸ©
  wkTm (w âˆ™ w') (unbox t e) âˆ
    where
      factorWkPresâˆ™-under-wkTm :  wkTm (factorWk e w âˆ™ factorWk (factorExt e w) w') t â‰… wkTm (factorWk e (w âˆ™ w')) t
      factorWkPresâˆ™-under-wkTm = â‰…-cong (Î“L âŠ†_) (â‰¡-sym (lCtxPresâˆ™ e w w')) (Î» w â†’ wkTm w t)
        (â‰…-trans (â‰¡-to-â‰… (â‰¡-sym (factorWkPresâˆ™ e w w'))) (â‰¡-subst-removable _ _ _))

wkSubPresâˆ™ : (w : Î” âŠ† Î”') (w' : Î”' âŠ† Î”'') (s : Sub Î” Î“)
  â†’ wkSub w' (wkSub w s) â‰¡ wkSub (w âˆ™ w') s
wkSubPresâˆ™ w w' []         = â‰¡-refl
wkSubPresâˆ™ w w' (s `, t)   = congâ‚‚ _`,_ (wkSubPresâˆ™ w w' s) (wkTmPresâˆ™ w w' t)
wkSubPresâˆ™ {Î”'' = Î”''} w w' (lock {Î”L = Î”L} {Î“ = Î“} s e) = let open â‰¡-Reasoning in begin
  wkSub w' (wkSub w (lock s e))
    â‰¡âŸ¨âŸ©
  lock (wkSub (factorWk (factorExt e w) w') (wkSub (factorWk e w) s)) (factorExt (factorExt e w) w')
    â‰¡âŸ¨ congâ‚‚ lock (wkSubPresâˆ™ _ _ _ ) (â‰¡-sym (factorExtPresâˆ™ _ _ _)) âŸ©
  lock
    (wkSub (factorWk e w âˆ™ factorWk (factorExt e w) w') s)
    (substâ‚‚ (CExt Î”'') (lCtxPresâˆ™ e w w') (rCtxPresâˆ™ e w w') (factorExt e (w âˆ™ w')))
    â‰…âŸ¨ xcong
      (Î» Î”L â†’ Sub Î”L Î“) (CExt Î”'')
      (â‰¡-sym (lCtxPresâˆ™ e w w')) (â‰¡-sym (rCtxPresâˆ™ e w w'))
      lock
      factorWkPresâˆ™-under-wkSub
      (â‰¡-substâ‚‚-removable (CExt Î”'') (lCtxPresâˆ™ e w w') (rCtxPresâˆ™ e w w') (factorExt e (w âˆ™ w'))) âŸ©
  lock (wkSub (factorWk e (w âˆ™ w')) s) (factorExt e (w âˆ™ w'))
    â‰¡âŸ¨âŸ©
  wkSub (w âˆ™ w') (lock s e) âˆ
    where
      factorWkPresâˆ™-under-wkSub :  wkSub (factorWk e w âˆ™ factorWk (factorExt e w) w') s â‰… wkSub (factorWk e (w âˆ™ w')) s
      factorWkPresâˆ™-under-wkSub = â‰…-cong (Î”L âŠ†_) (â‰¡-sym (lCtxPresâˆ™ e w w')) (Î» w â†’ wkSub w s)
        (â‰…-trans (â‰¡-to-â‰… (â‰¡-sym (factorWkPresâˆ™ e w w'))) (â‰¡-subst-removable _ _ _))

private
  wkSubFreshLemma : {s : Sub Î” Î“} {w : Î” âŠ† Î”'}
    â†’ wkSub (fresh {a = a}) (wkSub w s) â‰¡ wkSub (keep w) (dropâ‚› s)
  wkSubFreshLemma {s = s} {w} = â‰¡-trans (wkSubPresâˆ™ w fresh s) (â‰¡-trans
    (congâ‚‚ wkSub (cong drop (rightIdWk _)) â‰¡-refl )
    (â‰¡-sym (â‰¡-trans
      (wkSubPresâˆ™ _ _ _)
      (congâ‚‚ wkSub (cong drop (leftIdWk _)) â‰¡-refl))))

nat-substTm : (t : Tm Î“ a) (s : Sub Î” Î“) (w : Î” âŠ† Î”')
  â†’ substTm (wkSub w s) t â‰¡ wkTm w (substTm s t)
nat-substTm (var x)           s          w
  = nat-substVar x s w
nat-substTm (lam {Î“} {a} t)   s          w
  = cong lam
    (â‰¡-trans (cong (Î» s â†’ substTm (s `, var ze) t) wkSubFreshLemma)
    (nat-substTm t (keepâ‚› s) (keep w)))
nat-substTm (app t u)         s          w
  = congâ‚‚ app (nat-substTm t s w) (nat-substTm u s w)
nat-substTm (box t)           s          w
  = cong box (nat-substTm t (lock s (extğŸ”’- nil)) (keepğŸ”’ w))
nat-substTm {Î“ = Î“} {Î”' = Î”'} (unbox {Î“L = Î“L} {a = a} t e) s w
  = let open â‰¡-Reasoning in begin
      substTm (wkSub w s) (unbox t e)
        â‰¡âŸ¨âŸ©
      unbox {Î“L = lCtxâ‚› e (wkSub w s)} {Î“R = rCtxâ‚› e (wkSub w s)}
        (substTm (factorSubâ‚› e (wkSub w s)) t)
        (factorExtâ‚› e (wkSub w s))
        â‰…âŸ¨ xcong
          (Î» Î“L â†’  Tm Î“L (â—» a)) (CExt Î”')
          (lCtxâ‚›-wkSub-comm e w s) (rCtxâ‚›-wkSub-comm e w s)
          unbox
          factorSubâ‚›-wkSub-comm-under-substTm
          (â‰¡-substâ‚‚-addable (CExt Î”') (lCtxâ‚›-wkSub-comm e w s) _ _) âŸ©
     unbox {Î“L = lCtx (factorExtâ‚› e s) w} {Î“R = rCtx (factorExtâ‚› e s) w}
        (substTm (wkSub (factorWk (factorExtâ‚› e s) w) (factorSubâ‚› e s)) t)
        (substâ‚‚ (CExt Î”') (lCtxâ‚›-wkSub-comm e w s) (rCtxâ‚›-wkSub-comm e w s) (factorExtâ‚› e (wkSub w s)))
        â‰¡âŸ¨ congâ‚‚ unbox (nat-substTm t _ _) (factorExtâ‚›-wkSub-comm e s _) âŸ©
      unbox {Î“L = lCtx (factorExtâ‚› e s) w} {Î“R = rCtx (factorExtâ‚› e s) w}
        (wkTm (factorWk (factorExtâ‚› e s) w) (substTm (factorSubâ‚› e s) t))
        (factorExt (factorExtâ‚› e s) w)
        â‰¡âŸ¨âŸ©
      wkTm w (substTm s (unbox t e)) âˆ
      where
        factorSubâ‚›-wkSub-comm-under-substTm : substTm (factorSubâ‚› e (wkSub w s)) t â‰… substTm (wkSub (factorWk (factorExtâ‚› e s) w) (factorSubâ‚› e s)) t
        factorSubâ‚›-wkSub-comm-under-substTm = â‰…-cong (Î» x â†’ Sub x Î“L) (lCtxâ‚›-wkSub-comm e w s) (Î» z â†’ substTm z t)
          (â‰…-trans (â‰¡-subst-addable _ _ _) (â‰¡-to-â‰… (factorSubâ‚›-wkSub-comm e s w)))

coh-wkSub-âˆ™â‚›  : {Î”'' : Ctx} (s : Sub Î” Î“) (s' : Sub Î”' Î”) (w : Î”' âŠ† Î”'')
         â†’ wkSub w (s âˆ™â‚› s') â‰¡ s âˆ™â‚› (wkSub w s')
coh-wkSub-âˆ™â‚› []         s' w = â‰¡-refl
coh-wkSub-âˆ™â‚› (s `, x)   s' w = congâ‚‚ _`,_  (coh-wkSub-âˆ™â‚› s s' w) (â‰¡-sym (nat-substTm x s' w))
coh-wkSub-âˆ™â‚› (lock s e) s' w = let open â‰¡-Reasoning in begin
  wkSub w (lock s e âˆ™â‚› s')
    â‰¡âŸ¨âŸ©
  lock
    (wkSub (factorWk (factorExtâ‚› e s') w) (s âˆ™â‚› factorSubâ‚› e s'))
    (factorExt (factorExtâ‚› e s') w)
    -- apply IH
    â‰¡âŸ¨ congâ‚‚ lock (coh-wkSub-âˆ™â‚› _ _ _) â‰¡-refl âŸ©
 lock
   (s âˆ™â‚› wkSub (factorWk (factorExtâ‚› e s') w) (factorSubâ‚› e s'))
   (factorExt (factorExtâ‚› e s') w)
   -- applying factoring equalities
   â‰¡âŸ¨ congâ‚‚ lock (cong (_ âˆ™â‚›_) (â‰¡-sym (factorSubâ‚›-wkSub-comm e s' w))) (â‰¡-sym (factorExtâ‚›-wkSub-comm e _ _)) âŸ©
 lock
   (s âˆ™â‚› subst (Î» Î”L â†’ Sub Î”L _) (lCtxâ‚›-wkSub-comm e w s') (factorSubâ‚› e (wkSub w s')))
   (substâ‚‚ (CExt _) (lCtxâ‚›-wkSub-comm e w s') (rCtxâ‚›-wkSub-comm e w s') (factorExtâ‚› e (wkSub w s')))
   -- remove substs
   â‰…âŸ¨ xcong
     (Î» Î“L â†’ Sub Î“L _) (CExt _)
     (â‰¡-sym (lCtxâ‚›-wkSub-comm e w s')) (â‰¡-sym (rCtxâ‚›-wkSub-comm e w s'))
     {t2 = s âˆ™â‚› factorSubâ‚› e (wkSub w s')}
     {e2 = factorExtâ‚› e (wkSub w s')}
     lock
     (â‰…-cong  (Î» Î”L â†’ Sub Î”L _) (â‰¡-sym (lCtxâ‚›-wkSub-comm e w s')) (s âˆ™â‚›_) (â‰¡-subst-removable _ _ _))
     (â‰¡-substâ‚‚-removable _ _ _ _) âŸ©
 lock
   (s âˆ™â‚› factorSubâ‚› e (wkSub w s'))
   (factorExtâ‚› e (wkSub w s'))
   â‰¡âŸ¨âŸ©
 lock s e âˆ™â‚› wkSub w s' âˆ

coh-trimSub-wkTm : (t : Tm Î“ a) (s : Sub Î”' Î”) (w : Î“ âŠ† Î”)
  â†’ substTm (trimSub w s) t â‰¡ substTm s (wkTm w t)
coh-trimSub-wkTm (var x) s w
  = coh-trimSub-wkVar x s w
coh-trimSub-wkTm (lam t) s w
  = cong lam (â‰¡-trans
    (cong (Î» p â†’ substTm (p `, var ze) t) (nat-trimSub s w fresh))
    (coh-trimSub-wkTm t (keepâ‚› s) (keep w)))
coh-trimSub-wkTm (app t u) s w
  = congâ‚‚ app (coh-trimSub-wkTm t s w) (coh-trimSub-wkTm u s w)
coh-trimSub-wkTm (box t) s w
  = cong box (coh-trimSub-wkTm t (lock s (extğŸ”’- nil)) (keepğŸ”’ w))
coh-trimSub-wkTm (unbox t e) s w
  = let open â‰¡-Reasoning in begin
    substTm (trimSub w s) (unbox t e)
      â‰¡âŸ¨âŸ©
    unbox
      (substTm (factorSubâ‚› e (trimSub w s)) t)
      (factorExtâ‚› e (trimSub w s))
      -- add substs
      â‰…âŸ¨ xcong (Î» Î”L â†’ Tm Î”L _) (CExt _)
           (lCtxâ‚›-factorExt-trimSub-assoc e s w)
           (rCtxâ‚›-factorExt-trimSub-assoc e s w)
           {t2 = substTm (subst (Î» Î”L â†’ Sub Î”L _) (lCtxâ‚›-factorExt-trimSub-assoc e s w) (factorSubâ‚› e (trimSub w s))) t}
           {e2 = substâ‚‚ (CExt _) (lCtxâ‚›-factorExt-trimSub-assoc e s w) (rCtxâ‚›-factorExt-trimSub-assoc e s w) (factorExtâ‚› e (trimSub w s))}
           unbox
           (â‰…-cong (Î» Î”L â†’ Sub Î”L _) (lCtxâ‚›-factorExt-trimSub-assoc e s w) (Î» s' â†’ substTm s' t) (â‰¡-subst-addable _ _ _))
           (â‰¡-substâ‚‚-addable _ _ _ _) âŸ©
    unbox
      (substTm (subst (Î» Î”L â†’ Sub Î”L _) (lCtxâ‚›-factorExt-trimSub-assoc e s w) (factorSubâ‚› e (trimSub w s))) t)
      (substâ‚‚ (CExt _) (lCtxâ‚›-factorExt-trimSub-assoc e s w) (rCtxâ‚›-factorExt-trimSub-assoc e s w) (factorExtâ‚› e (trimSub w s)))
      -- apply factoring equalities
      â‰¡âŸ¨ congâ‚‚ unbox (congâ‚‚ substTm {u = t} (factorSubâ‚›-trimSub-comm e s w) â‰¡-refl) (factorExtâ‚›-trimSub-comm e s w) âŸ©
    unbox
      (substTm (trimSub (factorWk e w) (factorSubâ‚› (factorExt e w) s)) t)
      (factorExtâ‚› (factorExt e w) s)
      -- aplpy IH
      â‰¡âŸ¨ congâ‚‚ unbox (coh-trimSub-wkTm t _ _) â‰¡-refl âŸ©
    unbox
      (substTm (factorSubâ‚› (factorExt e w) s) (wkTm (factorWk e w) t))
      (factorExtâ‚› (factorExt e w) s)
      â‰¡âŸ¨âŸ©
    (substTm s (wkTm w (unbox t e))) âˆ

coh-trimSub-wkSub  : {Î”â‚ : Ctx} (s : Sub Î” Î“) (s' : Sub Î”â‚ Î”') (w : Î” âŠ† Î”')
         â†’ s âˆ™â‚› (trimSub w s') â‰¡ (wkSub w s) âˆ™â‚› s'
coh-trimSub-wkSub []         s' w
  = â‰¡-refl
coh-trimSub-wkSub (s `, x)   s' w
  = congâ‚‚ _`,_ (coh-trimSub-wkSub s s' w) (coh-trimSub-wkTm x s' w)
coh-trimSub-wkSub (lock s e) s' w
  = let open â‰¡-Reasoning in begin
    lock s e âˆ™â‚› trimSub w s'
      â‰¡âŸ¨âŸ©
    lock
      (s âˆ™â‚› factorSubâ‚› e (trimSub w s'))
      (factorExtâ‚› e (trimSub w s'))
      -- add substs
      â‰…âŸ¨ xcong
           (Î» Î“L â†’ Sub Î“L _) (CExt _)
           (lCtxâ‚›-factorExt-trimSub-assoc e s' w) (rCtxâ‚›-factorExt-trimSub-assoc e s' w)
           lock
           (â‰…-cong  (Î» Î”L â†’ Sub Î”L _) (lCtxâ‚›-factorExt-trimSub-assoc e s' w) (s âˆ™â‚›_) (â‰¡-subst-addable _ _ _))
           (â‰¡-substâ‚‚-addable (CExt _) _ _ (factorExtâ‚› e (trimSub w s'))) âŸ©
    lock
      (s âˆ™â‚› (subst (Î» Î”L â†’ Sub Î”L _) (lCtxâ‚›-factorExt-trimSub-assoc e s' w) (factorSubâ‚› e (trimSub w s'))))
      (substâ‚‚ (CExt _) (lCtxâ‚›-factorExt-trimSub-assoc e s' w) (rCtxâ‚›-factorExt-trimSub-assoc e s' w) (factorExtâ‚› e (trimSub w s')))
      -- apply factoring equalities
      â‰¡âŸ¨ congâ‚‚ lock (cong (s âˆ™â‚›_) (factorSubâ‚›-trimSub-comm e s' w)) (factorExtâ‚›-trimSub-comm e s' w) âŸ©
    lock
       (s âˆ™â‚› (trimSub (factorWk e w) (factorSubâ‚› (factorExt e w) s')))
       (factorExtâ‚› (factorExt e w) s')
      -- apply IH
      â‰¡âŸ¨ congâ‚‚ lock (coh-trimSub-wkSub _ _ _) â‰¡-refl âŸ©
    lock
      (wkSub (factorWk e w) s âˆ™â‚› factorSubâ‚› (factorExt e w) s')
      (factorExtâ‚› (factorExt e w) s')
      â‰¡âŸ¨âŸ©
    (wkSub w (lock s e) âˆ™â‚› s') âˆ

lCtxâ‚›PresTrans : âˆ€ {Î“LL Î“LR : Ctx} (e : CExt Î“L Î“LL Î“LR) (e' : CExt Î“ Î“L Î“R) (s : Sub Î” Î“)
  â†’ lCtxâ‚› e (factorSubâ‚› e' s) â‰¡ lCtxâ‚› (extRAssoc e e') s
lCtxâ‚›PresTrans e nil        s          = â‰¡-refl
lCtxâ‚›PresTrans e (ext e')   (s `, _)   = lCtxâ‚›PresTrans e e' s
lCtxâ‚›PresTrans e (extğŸ”’- e') (lock s _) = lCtxâ‚›PresTrans e e' s

rCtxâ‚›PresTrans : âˆ€ {Î“LL Î“LR : Ctx} (e : CExt Î“L Î“LL Î“LR) (e' : CExt Î“ Î“L Î“R) (s : Sub Î” Î“)
  â†’ rCtxâ‚› e (factorSubâ‚› e' s) ,, rCtxâ‚› e' s â‰¡ rCtxâ‚› (extRAssoc e e') s
rCtxâ‚›PresTrans e nil        s                    = â‰¡-refl
rCtxâ‚›PresTrans e (ext e')   (s `, t)             = rCtxâ‚›PresTrans e e' s
rCtxâ‚›PresTrans e (extğŸ”’- e') (lock {Î”R = Î”R} s _) = â‰¡-trans (â‰¡-sym (,,-assoc {Î“R = Î”R})) (cong (_,, Î”R) (rCtxâ‚›PresTrans e e' s))

lCtxâ‚›Presâˆ™â‚› : (e : CExt Î“ Î“L Î“R) (s : Sub Î“' Î“) (s' : Sub Î” Î“')
  â†’ lCtxâ‚› e (s âˆ™â‚› s') â‰¡ lCtxâ‚› (factorExtâ‚› e s) s'
lCtxâ‚›Presâˆ™â‚› nil       s s'           = â‰¡-refl
lCtxâ‚›Presâˆ™â‚› (ext e)   (s `, t) s'    = lCtxâ‚›Presâˆ™â‚› e s s'
lCtxâ‚›Presâˆ™â‚› (extğŸ”’- e) (lock s e1) s' = â‰¡-trans (lCtxâ‚›Presâˆ™â‚› e _ _) (lCtxâ‚›PresTrans _ e1 _)

rCtxâ‚›Presâˆ™â‚› : (e : CExt Î“ Î“L Î“R) (s : Sub Î“' Î“) (s' : Sub Î” Î“')
  â†’ rCtxâ‚› e (s âˆ™â‚› s') â‰¡ rCtxâ‚› (factorExtâ‚› e s) s'
rCtxâ‚›Presâˆ™â‚› nil       s s'           = â‰¡-refl
rCtxâ‚›Presâˆ™â‚› (ext e)   (s `, t) s'    = rCtxâ‚›Presâˆ™â‚› e s s'
rCtxâ‚›Presâˆ™â‚› (extğŸ”’- e) (lock s e1) s' = â‰¡-trans (cong (_,, _) (rCtxâ‚›Presâˆ™â‚› e _ _)) (rCtxâ‚›PresTrans _ e1 _)

factorSubPresTrans : âˆ€ {Î“LL Î“LR : Ctx} (e : CExt Î“L Î“LL Î“LR) (e' : CExt Î“ Î“L Î“R) (s : Sub Î” Î“)
  â†’ subst (Î» Î”L â†’ Sub Î”L Î“LL) (lCtxâ‚›PresTrans e e' s) (factorSubâ‚› e (factorSubâ‚› e' s)) â‰¡ factorSubâ‚› (extRAssoc e e') s
factorSubPresTrans e nil        s = â‰¡-refl
factorSubPresTrans e (ext e')   (s `, _) = factorSubPresTrans e e' s
factorSubPresTrans e (extğŸ”’- e') (lock s _) = factorSubPresTrans e e' s

factorSubPresâˆ™â‚› : (e : CExt Î“ Î“L Î“R) (s : Sub Î“' Î“) (s' : Sub Î” Î“')
  â†’ subst (Î» Î”L â†’ Sub Î”L Î“L) (lCtxâ‚›Presâˆ™â‚› e s s') (factorSubâ‚› e (s âˆ™â‚› s'))  â‰¡ factorSubâ‚› e s âˆ™â‚› factorSubâ‚› (factorExtâ‚› e s) s'
factorSubPresâˆ™â‚› nil       s           s' = â‰¡-refl
factorSubPresâˆ™â‚› (ext e)   (s `, t)    s' = factorSubPresâˆ™â‚› e s s'
factorSubPresâˆ™â‚› (extğŸ”’- e) (lock s e1) s' = let open â‰¡-Reasoning in begin
  subst (Î» Î”L â†’ Sub Î”L _)
    (lCtxâ‚›Presâˆ™â‚› (extğŸ”’- e) (lock s e1) s')
    (factorSubâ‚› (extğŸ”’- e) (lock s e1 âˆ™â‚› s'))
    â‰¡âŸ¨âŸ©
  subst (Î» Î”L â†’ Sub Î”L _)
    (â‰¡-trans (lCtxâ‚›Presâˆ™â‚› e s (factorSubâ‚› e1 s')) (lCtxâ‚›PresTrans (factorExtâ‚› e s) e1 s'))
    (factorSubâ‚› e (s âˆ™â‚› factorSubâ‚› e1 s'))
    -- split `subst _ (â‰¡-trans p q) ...` to `subst _ q (subst _ p ...)`
    â‰¡âŸ¨ â‰¡-sym (subst-subst (lCtxâ‚›Presâˆ™â‚› e s (factorSubâ‚› e1 s'))) âŸ©
  subst (Î» Î”L â†’ Sub Î”L _)
    (lCtxâ‚›PresTrans (factorExtâ‚› e s) e1 s')
    (subst (Î» Î”L â†’ Sub Î”L _)
      (lCtxâ‚›Presâˆ™â‚› e s (factorSubâ‚› e1 s'))
      (factorSubâ‚› e (s âˆ™â‚› factorSubâ‚› e1 s')))
    -- rewrite (remove) inner subst with IH
    â‰¡âŸ¨ cong (subst (Î» Î”L â†’ Sub Î”L _) _) (factorSubPresâˆ™â‚› e s (factorSubâ‚› e1 s')) âŸ©
  subst (Î» Î”L â†’ Sub Î”L _)
    (lCtxâ‚›PresTrans (factorExtâ‚› e s) e1 s')
    (factorSubâ‚› e s âˆ™â‚› factorSubâ‚› (factorExtâ‚› e s) (factorSubâ‚› e1 s'))
    -- push subst inside application of (_ âˆ™â‚›_)
    â‰¡âŸ¨ subst-applicationâ€²  (Î» Î”L â†’ Sub Î”L _) (factorSubâ‚› e s âˆ™â‚›_) (lCtxâ‚›PresTrans (factorExtâ‚› e s) e1 s') âŸ©
  factorSubâ‚› e s âˆ™â‚› subst (Î» Î”L â†’ Sub Î”L _) (lCtxâ‚›PresTrans (factorExtâ‚› e s) e1 s') (factorSubâ‚› (factorExtâ‚› e s) (factorSubâ‚› e1 s'))
    -- apply factorSubPresTrans
    â‰¡âŸ¨ cong (_ âˆ™â‚›_) (factorSubPresTrans (factorExtâ‚› e s) e1 s') âŸ©
  factorSubâ‚› e s âˆ™â‚› factorSubâ‚› (extRAssoc (factorExtâ‚› e s) e1) s'
    â‰¡âŸ¨âŸ©
  factorSubâ‚› (extğŸ”’- e) (lock s e1) âˆ™â‚› factorSubâ‚› (factorExtâ‚› (extğŸ”’- e) (lock s e1)) s' âˆ

factorExtPresâˆ™â‚› : (e : CExt Î“ Î“L Î“R) (s : Sub Î“' Î“) (s' : Sub Î” Î“')
  â†’ substâ‚‚ (CExt _) (lCtxâ‚›Presâˆ™â‚› e s s') (rCtxâ‚›Presâˆ™â‚› e s s') (factorExtâ‚› e (s âˆ™â‚› s')) â‰¡ factorExtâ‚› (factorExtâ‚› e s) s'
factorExtPresâˆ™â‚› _ _ _ = ExtIsProp _ _

substVarPresId : (x : Var Î“ a) â†’ substVar idâ‚› x â‰¡ var x
substVarPresId ze = â‰¡-refl
substVarPresId (su x) = â‰¡-trans (nat-substVar x idâ‚› fresh) (â‰¡-trans
  (cong (wkTm fresh) (substVarPresId x))
  (cong var (wkIncr x)))

-- parallel substitution (substVar) preserves substitution composition
substVarPresâˆ™ : (s : Sub Î“' Î“) (s' : Sub Î” Î“') (x : Var Î“ a)
  â†’ substTm s' (substVar s x) â‰¡ substVar (s âˆ™â‚› s') x
substVarPresâˆ™ (s `, x) s' ze      = â‰¡-refl
substVarPresâˆ™ (s `, x) s' (su xâ‚) = substVarPresâˆ™ s s' xâ‚

private
  dropKeepLemma : (s' : Sub Î”' Î”) (s : Sub Î“ Î”')
    â†’  dropâ‚› (s' âˆ™â‚› s) â‰¡ dropâ‚› {a = a} s' âˆ™â‚› keepâ‚› s
  dropKeepLemma s' s = â‰¡-trans (coh-wkSub-âˆ™â‚› s' s fresh)
    (â‰¡-trans
      ((cong (s' âˆ™â‚›_) (â‰¡-sym (trimSubPresId (dropâ‚› s)))))
      (coh-trimSub-wkSub s' (keepâ‚› s) fresh))

substTmPresâˆ™ : (s : Sub Î“' Î“) (s' : Sub Î” Î“') (t : Tm Î“ a)
  â†’ substTm s' (substTm s t) â‰¡ substTm (s âˆ™â‚› s') t
substTmPresâˆ™ s s' (var v) = substVarPresâˆ™ s s' v
substTmPresâˆ™ s s' (lam t) = cong lam
    (â‰¡-trans (substTmPresâˆ™ _ _ t)
    (cong ((Î» s â†’ substTm (s `, var ze) t)) (â‰¡-sym (dropKeepLemma s s'))))
substTmPresâˆ™ s s' (app t tâ‚) = congâ‚‚ app (substTmPresâˆ™ s s' t) (substTmPresâˆ™ s s' tâ‚)
substTmPresâˆ™ s s' (box t) = cong box (substTmPresâˆ™ _ _ t)
substTmPresâˆ™ {Î” = Î”} {a = a} s s' (unbox t e) = let open â‰¡-Reasoning in begin
  substTm s' (substTm s (unbox t e))
    â‰¡âŸ¨âŸ©
  unbox
    (substTm (factorSubâ‚› (factorExtâ‚› e s) s') (substTm (factorSubâ‚› e s) t))
    (factorExtâ‚› (factorExtâ‚› e s) s')
    -- apply IH
    â‰¡âŸ¨ congâ‚‚ unbox (substTmPresâˆ™ _ _ t) â‰¡-refl âŸ©
  unbox
    (substTm (factorSubâ‚› e s âˆ™â‚› factorSubâ‚› (factorExtâ‚› e s) s') t)
    (factorExtâ‚› (factorExtâ‚› e s) s')
    -- apply factoring equalities
    â‰¡âŸ¨ congâ‚‚ unbox (cong (Î» x â†’ substTm x t) (â‰¡-sym (factorSubPresâˆ™â‚› e _ _))) (â‰¡-sym (factorExtPresâˆ™â‚› e _ _)) âŸ©
  unbox
    (substTm (subst (Î» Î”L â†’ Sub Î”L _) (lCtxâ‚›Presâˆ™â‚› e s s') (factorSubâ‚› e (s âˆ™â‚› s'))) t)
    (substâ‚‚ (CExt _) (lCtxâ‚›Presâˆ™â‚› e s s') (rCtxâ‚›Presâˆ™â‚› e s s') (factorExtâ‚› e (s âˆ™â‚› s')))
    -- remove substs
    â‰…âŸ¨ xcong
      (Î» Î“L â†’ Tm Î“L (â—» a)) (CExt Î”)
      (â‰¡-sym (lCtxâ‚›Presâˆ™â‚› e s s')) (â‰¡-sym (rCtxâ‚›Presâˆ™â‚› e s s'))
      {t2 = substTm (factorSubâ‚› e (s âˆ™â‚› s')) t}
      {e2 = factorExtâ‚› e (s âˆ™â‚› s')}
      unbox
      (â‰…-cong (Î» Î”L â†’ Sub Î”L _) (â‰¡-sym (lCtxâ‚›Presâˆ™â‚› e s s')) (Î» x â†’ substTm x t) (â‰¡-subst-removable _ _ _))
      (â‰¡-substâ‚‚-removable _ _ _ _) âŸ©
  unbox (substTm (factorSubâ‚› e (s âˆ™â‚› s')) t) (factorExtâ‚› e (s âˆ™â‚› s'))
    â‰¡âŸ¨âŸ©
  substTm (s âˆ™â‚› s') (unbox t e) âˆ

assocSub : {Î“1 Î“2 Î“3 Î“4 : Ctx} â†’ (s3 : Sub Î“3 Î“4) (s2 : Sub Î“2 Î“3) â†’ (s1 : Sub Î“1 Î“2)
  â†’ (s3 âˆ™â‚› s2) âˆ™â‚› s1 â‰¡ s3 âˆ™â‚› (s2 âˆ™â‚› s1)
assocSub []           s2 s1 = â‰¡-refl
assocSub (s3 `, t)    s2 s1 = congâ‚‚ _`,_ (assocSub s3 s2 s1) (substTmPresâˆ™ s2 s1 t)
assocSub {Î“1 = Î“1} (lock s3 e3) s2 s1 = let open â‰¡-Reasoning in begin
  (lock s3 e3 âˆ™â‚› s2) âˆ™â‚› s1
    â‰¡âŸ¨âŸ©
  lock
    ((s3 âˆ™â‚› factorSubâ‚› e3 s2) âˆ™â‚› factorSubâ‚› (factorExtâ‚› e3 s2) s1)
    (factorExtâ‚› (factorExtâ‚› e3 s2) s1)
    -- apply IH
    â‰¡âŸ¨ congâ‚‚ lock (assocSub _ _ _) â‰¡-refl âŸ©
  lock
    (s3 âˆ™â‚› (factorSubâ‚› e3 s2 âˆ™â‚› factorSubâ‚› (factorExtâ‚› e3 s2) s1))
    (factorExtâ‚› (factorExtâ‚› e3 s2) s1)
    -- apply factoring equalities
    â‰¡âŸ¨ congâ‚‚ lock (cong (s3 âˆ™â‚›_) (â‰¡-sym (factorSubPresâˆ™â‚› e3 _ _ ))) (â‰¡-sym (factorExtPresâˆ™â‚› e3 _ _)) âŸ©
  lock
    (s3 âˆ™â‚› subst (Î» Î”L â†’ Sub Î”L _) (lCtxâ‚›Presâˆ™â‚› e3 s2 s1) (factorSubâ‚› e3 (s2 âˆ™â‚› s1)))
    (substâ‚‚ (CExt _) (lCtxâ‚›Presâˆ™â‚› e3 s2 s1) (rCtxâ‚›Presâˆ™â‚› e3 s2 s1) (factorExtâ‚› e3 (s2 âˆ™â‚› s1)))
    -- remove substs
    â‰…âŸ¨ xcong (Î» Î”L â†’ Sub Î”L _)
      (CExt Î“1)
      (â‰¡-sym (lCtxâ‚›Presâˆ™â‚› e3 s2 s1)) (â‰¡-sym (rCtxâ‚›Presâˆ™â‚› e3 s2 s1))
      {t2 = s3 âˆ™â‚› factorSubâ‚› e3 (s2 âˆ™â‚› s1)}
      {e2 = factorExtâ‚› e3 (s2 âˆ™â‚› s1)}
      lock
      (â‰…-cong (Î» Î”L â†’ Sub Î”L _) (â‰¡-sym (lCtxâ‚›Presâˆ™â‚› e3 s2 s1)) (s3 âˆ™â‚›_) (â‰¡-subst-removable _ _ _))
      (â‰¡-substâ‚‚-removable _ _ _ _) âŸ©
  lock (s3 âˆ™â‚› factorSubâ‚› e3 (s2 âˆ™â‚› s1)) (factorExtâ‚› e3 (s2 âˆ™â‚› s1))
    â‰¡âŸ¨âŸ©
  lock s3 e3 âˆ™â‚› (s2 âˆ™â‚› s1) âˆ

leftIdSub : (s : Sub Î“ Î“') â†’ (idâ‚› âˆ™â‚› s) â‰¡ s
leftIdSub []         = â‰¡-refl
leftIdSub (s `, t)   = let open â‰¡-Reasoning in begin
  idâ‚› âˆ™â‚› (s `, t)
    â‰¡âŸ¨âŸ©
  (wkSub fresh idâ‚› âˆ™â‚› (s `, t)) `, t
    â‰¡âŸ¨ cong (_`, _) (â‰¡-sym (coh-trimSub-wkSub idâ‚› (s `, t) fresh)) âŸ©
  idâ‚› âˆ™â‚› trimSub fresh (s `, t) `, t
    â‰¡âŸ¨ cong (_`, _) (â‰¡-trans (leftIdSub _) (trimSubPresId _)) âŸ©
  (s `, t) âˆ
leftIdSub {Î“ = Î“} (lock {Î”L = Î”L} {Î”R = Î”R} s e) = let open â‰¡-Reasoning in begin
  lock (idâ‚› âˆ™â‚› s) (extRAssoc nil e)
    â‰¡âŸ¨ congâ‚‚ lock (leftIdSub s) extLeftUnit âŸ©
  lock s (subst (CExt Î“ Î”L) _ e)
    â‰…âŸ¨ â‰…-cong (CExt Î“ Î”L) ,,-leftUnit (lock s) (â‰¡-subst-removable (CExt Î“ Î”L) _ e) âŸ©
  lock s e âˆ

private
  -- just a helper to reduce redundancy, nothing too interesting
  auxLemma : (w : Î“ âŠ† Î”) â†’ wkSub (drop[ a ] (w âˆ™ idWk)) idâ‚› â‰¡ dropâ‚› (embWk w)

wkSubId : (w : Î“ âŠ† Î”) â†’ wkSub w idâ‚› â‰¡ embWk w

auxLemma w = (â‰¡-trans
    (â‰¡-sym (wkSubPresâˆ™ w fresh idâ‚›))
    (cong (wkSub fresh) (wkSubId w)))

wkSubId base = â‰¡-refl
wkSubId (drop w) = â‰¡-trans
  (cong (Î» w' â†’ wkSub (drop w') idâ‚›) (â‰¡-sym (rightIdWk w)))
  (auxLemma w)
wkSubId (keep w)  = cong (_`, var ze) (â‰¡-trans
  (wkSubPresâˆ™ fresh (keep w) idâ‚›)
  (â‰¡-trans
    (congâ‚‚ wkSub (cong drop (â‰¡-trans (leftIdWk _) (â‰¡-sym (rightIdWk _)))) â‰¡-refl)
    (auxLemma w)))
wkSubId (keepğŸ”’ w) = congâ‚‚ lock (wkSubId w) â‰¡-refl

-- Outcast lemmas

keepFreshLemma : {w : Î“ âŠ† Î“'} {t : Tm Î“ a}
  â†’ wkTm (fresh {a = b}) (wkTm w t) â‰¡ wkTm (keep w) (wkTm fresh t)
keepFreshLemma = â‰¡-trans (wkTmPresâˆ™ _ _ _) (â‰¡-sym (â‰¡-trans
    (wkTmPresâˆ™ _ _ _)
    (congâ‚‚ wkTm (cong drop (â‰¡-trans (leftIdWk _) (â‰¡-sym (rightIdWk _)))) â‰¡-refl)))

sliceCompLemma : (w : Î“ âŠ† Î”) (e : LFExt Î“ (Î“L ğŸ”’) Î“R) (t : Tm (Î“L ğŸ”’) a)
  â†’ wkTm (LFExtToâŠ† (wkLFExt e w)) (wkTm (keepğŸ”’ (sliceLeft e w)) t) â‰¡      wkTm w (wkTm (LFExtToâŠ† e) t)
sliceCompLemma w e t = (â‰¡-trans (wkTmPresâˆ™ _ _ _) (â‰¡-sym (â‰¡-trans
  (wkTmPresâˆ™ _ _ _)
  (congâ‚‚ wkTm (slicingLemma w e) â‰¡-refl))))

beta-wk-lemma : (w  : Î“ âŠ† Î”) (u : Tm Î“ a) (t : Tm (Î“ `, a) b)
  â†’ substTm (idâ‚› `, wkTm w u) (wkTm (keep w) t) â‰¡ wkTm w (substTm (idâ‚› `, u) t)
beta-wk-lemma w u t = â‰¡-trans
  (â‰¡-sym (coh-trimSub-wkTm t _ (keep w)))
  (â‰¡-sym (â‰¡-trans
    (â‰¡-sym (nat-substTm t _ _))
    (cong
      (Î» p â†’ substTm (p `, wkTm w u) t)
      (â‰¡-sym (â‰¡-trans (trimSubId w) (â‰¡-sym (wkSubId w)))))))

-- factorising the identity substituion yields a weakening that only drops
factorSubâ‚›IdWkIsFactorSubâ‚›Id : (e : CExt Î“ Î“L Î“R) â†’ factorSubâ‚› e idâ‚› â‰¡ embWk (LFExtToâŠ† (factorSubâ‚›IdWk e))
factorSubâ‚›IdWkIsFactorSubâ‚›Id nil             = â‰¡-refl
factorSubâ‚›IdWkIsFactorSubâ‚›Id (extğŸ”’- e)       = factorSubâ‚›IdWkIsFactorSubâ‚›Id e
factorSubâ‚›IdWkIsFactorSubâ‚›Id (ext {a = a} e) = let open â‰¡-Reasoning in begin
  factorSubâ‚› e (wkSub fresh idâ‚›)
    -- apply `factorSubâ‚›-wkSub-comm`
    â‰¡âŸ¨ subst-sym (lCtxâ‚›-wkSub-comm e fresh idâ‚›) (factorSubâ‚›-wkSub-comm e idâ‚› fresh)  âŸ©
  subst (Î» Î”L â†’ Sub Î”L _) (â‰¡-sym (lCtxâ‚›-wkSub-comm e fresh idâ‚›))
    (wkSub (factorWk (factorExtâ‚› e idâ‚›) fresh) (factorSubâ‚› e idâ‚›))
    -- apply IH
    â‰¡âŸ¨ cong
        (Î» z â†’ subst (Î» Î”L â†’ Sub Î”L _) (â‰¡-sym (lCtxâ‚›-wkSub-comm e fresh idâ‚›)) (wkSub (factorWk (factorExtâ‚› e idâ‚›) fresh) z))
        (factorSubâ‚›IdWkIsFactorSubâ‚›Id e) âŸ©
  subst (Î» Î”L â†’ Sub Î”L _) (â‰¡-sym (lCtxâ‚›-wkSub-comm e fresh idâ‚›))
    (wkSub (factorWk (factorExtâ‚› e idâ‚›) fresh) (embWk (LFExtToâŠ† (factorSubâ‚›IdWk e))))
    -- apply `substCrunch` which crunches substitution with substitution and weakening equalities
    â‰¡âŸ¨ cong
        (Î» z â†’ subst (Î» Î”L â†’ Sub Î”L _)
        (â‰¡-sym (lCtxâ‚›-wkSub-comm e fresh idâ‚›)) z) substCrunch âŸ©
  subst (Î» Î”L â†’ Sub Î”L _) (â‰¡-sym (lCtxâ‚›-wkSub-comm e fresh idâ‚›))
    (embWk (LFExtToâŠ† (extRAssoc (factorSubâ‚›IdWk e) (factorDropsWk (factorExtâ‚› e idâ‚›) freshExt))))
    -- pull out subst
    â‰¡âŸ¨ subst-applicationâ€² (Î» Î“ â†’ LFExt Î“ _ _)
         (Î» z â†’ embWk (LFExtToâŠ† z))
         (â‰¡-sym (lCtxâ‚›-wkSub-comm e fresh idâ‚›)) âŸ©
  embWk (LFExtToâŠ†
    (subst (Î» Î“ â†’ LFExt Î“ _ (â†ğŸ”’â‚rCtx e ,, rCtxâ€² (factorExtâ‚› e idâ‚›) freshExt)) (â‰¡-sym (lCtxâ‚›-wkSub-comm e fresh idâ‚›))
      (extRAssoc (factorSubâ‚›IdWk e) (factorDropsWk (factorExtâ‚› e idâ‚›) freshExt))))
    â‰¡âŸ¨âŸ©
  embWk (LFExtToâŠ† (factorSubâ‚›IdWk (ext e))) âˆ
  where
  --
  coh-wkSub-embwk : (w : Î“' âŠ† Î“'') (w' : Î“ âŠ† Î“') â†’ wkSub w (embWk w') â‰¡ embWk (w' âˆ™ w)
  coh-wkSub-embwk w w' = let open â‰¡-Reasoning in begin
    wkSub w (embWk w')
      â‰¡âŸ¨ cong (wkSub w) (â‰¡-sym (wkSubId _)) âŸ©
    wkSub w (wkSub w' idâ‚›)
      â‰¡âŸ¨ wkSubPresâˆ™ _ _ _ âŸ©
    wkSub (w' âˆ™ w) idâ‚›
      â‰¡âŸ¨ wkSubId _ âŸ©
    embWk (w' âˆ™ w) âˆ
  --
  substCrunch : wkSub (factorWk (factorExtâ‚› e idâ‚›) (fresh {a = a})) (embWk (LFExtToâŠ† (factorSubâ‚›IdWk e)))
    â‰¡ embWk (LFExtToâŠ† (extRAssoc (factorSubâ‚›IdWk e) (factorDropsWk (factorExtâ‚› e idâ‚›) freshExt)))
  substCrunch = let open â‰¡-Reasoning in begin
    wkSub (factorWk (factorExtâ‚› e idâ‚›) (fresh {a = a})) (embWk (LFExtToâŠ† (factorSubâ‚›IdWk e)))
      â‰¡âŸ¨ coh-wkSub-embwk (factorWk (factorExtâ‚› e idâ‚›) (fresh {a = a})) (LFExtToâŠ† (factorSubâ‚›IdWk e)) âŸ©
    embWk (LFExtToâŠ† (factorSubâ‚›IdWk e) âˆ™ factorWk (factorExtâ‚› e idâ‚›) fresh)
      â‰¡âŸ¨ cong (Î» x â†’ embWk (LFExtToâŠ† (factorSubâ‚›IdWk e) âˆ™ x)) (â‰¡-sym (factorDropsWkIsfactorWk (factorExtâ‚› e idâ‚›) freshExt)) âŸ©
    embWk (LFExtToâŠ† (factorSubâ‚›IdWk e) âˆ™ LFExtToâŠ† (factorDropsWk (factorExtâ‚› e idâ‚›) freshExt))
      â‰¡âŸ¨ cong embWk (â‰¡-sym (LFExtToâŠ†PresTrans (factorSubâ‚›IdWk e) (factorDropsWk (factorExtâ‚› e idâ‚›) freshExt))) âŸ©
    embWk
      (LFExtToâŠ† (extRAssoc (factorSubâ‚›IdWk e) (factorDropsWk (factorExtâ‚› e idâ‚›) freshExt))) âˆ

-----------------------------------
--- Reduction and conversion lemmas
-----------------------------------

module _ where
  module _ {Î“} {a} where
    open RelReasoning (_âŸ¶_ {Î“} {a}) public

  wkTmPresâŸ¶ : (w : Î“ âŠ† Î“') â†’ t âŸ¶ t' â†’ wkTm w t âŸ¶ wkTm w t'
  wkTmPresâŸ¶ w (red-fun t u)
    = step-â‰¡ (red-fun _ _) (beta-wk-lemma w u t)
  wkTmPresâŸ¶ w (exp-fun _)
    = step-â‰¡ (exp-fun _) (cong lam (congâ‚‚ app keepFreshLemma â‰¡-refl))
  wkTmPresâŸ¶ w (red-box t e)
    = step-â‰¡
      (red-box _ _)
      (â‰¡-trans
        (â‰¡-Ë˜trans
          (coh-trimSub-wkTm t _ _)
          (cong
            (Î» s â†’ substTm (lock s (factorExt e w)) t)
            (â‰¡-trans
              (trimSubId (factorWk e w))
              (â‰¡-sym (wkSubId _)))))
        (nat-substTm t _ _))
  wkTmPresâŸ¶ w (exp-box _)
    = exp-box _
  wkTmPresâŸ¶ w (cong-lam r)
    = cong-lam (wkTmPresâŸ¶ (keep w) r)
  wkTmPresâŸ¶ w (cong-box r)
    = cong-box (wkTmPresâŸ¶ (keepğŸ”’ w) r)
  wkTmPresâŸ¶ w (cong-unbox {e = e} r)
    = cong-unbox (wkTmPresâŸ¶ (factorWk e w ) r)
  wkTmPresâŸ¶ w (cong-app1 r)
    = cong-app1 (wkTmPresâŸ¶ w r)
  wkTmPresâŸ¶ w (cong-app2 r)
    = cong-app2 (wkTmPresâŸ¶ w r)
  wkTmPresâŸ¶ w (shift-unbox t e e')
    = â‰¡-step-â‰¡
      (let open â‰¡-Reasoning in begin
      wkTm w (unbox t (extRAssoc (upLFExt e) e'))
        â‰¡âŸ¨âŸ©
      unbox
        (wkTm (factorWk (extRAssoc (upLFExt e) e') w) t)
        (factorExt (extRAssoc (upLFExt e) e') w)
        -- add substs
        â‰¡âŸ¨ â‰…-to-â‰¡ (cong-unboxâ‰…
          (lCtxPresTrans (upLFExt e) e' w) (rCtxPresTrans (upLFExt e) e' w)
          (â‰¡-subst-addable _ _ _) (â‰¡-substâ‚‚-addable _ _ _ _)) âŸ©
      unbox
        (subst (Î» Î“L â†’ Tm Î“L _) (lCtxPresTrans (upLFExt e) e' w) (wkTm (factorWk (extRAssoc (upLFExt e) e') w) t))
        (substâ‚‚ (CExt _) (lCtxPresTrans (upLFExt e) e' w) (rCtxPresTrans (upLFExt e) e' w) (factorExt (extRAssoc (upLFExt e) e') w))
        -- push subst on subterm inside
        â‰¡âŸ¨ congâ‚‚ unbox (subst-applicationâ€² (_ âŠ†_) (Î» w â†’ wkTm w t) (lCtxPresTrans (upLFExt e) e' w)) â‰¡-refl âŸ©
     unbox
        (wkTm (subst (_ âŠ†_) (lCtxPresTrans (upLFExt e) e' w) (factorWk (extRAssoc (upLFExt e) e') w)) t)
        (substâ‚‚ (CExt _) (lCtxPresTrans (upLFExt e) e' w) (rCtxPresTrans (upLFExt e) e' w) (factorExt (extRAssoc (upLFExt e) e') w))
        -- factorisation preserves transitivity
        â‰¡âŸ¨ congâ‚‚ unbox (congâ‚‚ wkTm (factorWkPresTrans (upLFExt e) e' w) â‰¡-refl) (factorExtPresTrans (upLFExt e) _ _) âŸ©
      unbox
        (wkTm (factorWk (upLFExt e) (factorWk e' w)) t)
        (extRAssoc (factorExt (upLFExt e) (factorWk e' w)) (factorExt e' w))
        -- apply equalities for absorption of upLFExt
        â‰¡âŸ¨ congâ‚‚ unbox (congâ‚‚ wkTm (â‰¡-sym (factorWkAbsorbsUpLFExt e (factorWk e' w))) â‰¡-refl) (congâ‚‚ extRAssoc (â‰¡-sym (factorExtAbsorbsUpLFExt e (factorWk e' w))) â‰¡-refl) âŸ©
      unbox
        (wkTm (subst (_ âŠ†_) (lCtxAbsorbsUpLFExt e (factorWk e' w)) (factorWk e (factorWk e' w))) t)
        (extRAssoc (substâ‚‚ (CExt _) (lCtxAbsorbsUpLFExt e (factorWk e' w)) (rCtxAbsorbsUpLFExt e (factorWk e' w)) (upLFExt (factorExt e (factorWk e' w)))) (factorExt e' w))
        -- pull out substs
        â‰¡âŸ¨ congâ‚‚ unbox (â‰¡-sym (subst-applicationâ€² (_ âŠ†_) (Î» x â†’ wkTm x t) (lCtxAbsorbsUpLFExt e (factorWk e' w)))) (ExtIsProp _ _) âŸ©
      unbox
        (subst (Î» Î“L â†’ Tm Î“L _) (lCtxAbsorbsUpLFExt e (factorWk e' w)) (wkTm (factorWk e (factorWk e' w)) t))
        (substâ‚‚ (Î» Î“L Î“R â†’ CExt _ Î“L (Î“R ,, _)) (lCtxAbsorbsUpLFExt e (factorWk e' w)) (rCtxAbsorbsUpLFExt e (factorWk e' w)) (extRAssoc (upLFExt (factorExt e (factorWk e' w))) (factorExt e' w)))
        -- remove substs
        â‰¡âŸ¨ â‰…-to-â‰¡ (cong-unboxâ‰…
          (â‰¡-sym (lCtxAbsorbsUpLFExt e (factorWk e' w))) (cong (_,, _) (â‰¡-sym (rCtxAbsorbsUpLFExt e (factorWk e' w))))
          (â‰¡-subst-removable _ _ _) (â‰¡-substâ‚‚-removable _ _ _ _)) âŸ©
      unbox
        (wkTm (factorWk e (factorWk e' w)) t)
        (extRAssoc (upLFExt (factorExt e (factorWk e' w))) (factorExt e' w)) âˆ)
      (shift-unbox _ _ _)
      (let open â‰¡-Reasoning in begin
      unbox
        (wkTm (LFExtToâŠ† (factorExt e (factorWk e' w))) (wkTm (factorWk e (factorWk e' w)) t))
        (factorExt e' w)
        -- wkTm preserves composition
        â‰¡âŸ¨ congâ‚‚ unbox (wkTmPresâˆ™ _ _ _) â‰¡-refl âŸ©
      unbox
        (wkTm (factorWk e (factorWk e' w) âˆ™ LFExtToâŠ† (factorExt e (factorWk e' w))) t)
        (factorExt e' w)
        -- apply factorisationLemma
        â‰¡âŸ¨ congâ‚‚ unbox (congâ‚‚ wkTm (â‰¡-sym (factorisationLemma e _)) â‰¡-refl) â‰¡-refl âŸ©
      unbox
        (wkTm (LFExtToâŠ† e âˆ™ factorWk e' w) t)
        (factorExt e' w)
        -- wkTm preserves composition
        â‰¡âŸ¨ congâ‚‚ unbox (â‰¡-sym (wkTmPresâˆ™ _ _ _)) â‰¡-refl âŸ©
      unbox
        (wkTm (factorWk e' w) (wkTm (LFExtToâŠ† e) t))
        (factorExt e' w)
        â‰¡âŸ¨âŸ©
      wkTm w (unbox (wkTm (LFExtToâŠ† e) t) e') âˆ)

wkTmPresâ‰ˆ : (w : Î“ âŠ† Î“') â†’ t â‰ˆ t' â†’ wkTm w t â‰ˆ wkTm w t'
wkTmPresâ‰ˆ w = cong-âŸ¶-to-cong-â‰ˆ (wkTmPresâŸ¶ w)

wkSubPresâŸ¶ : (w : Î” âŠ† Î”') â†’ Ïƒ âŸ¶â‚› Ïƒ' â†’ wkSub w Ïƒ âŸ¶â‚› wkSub w Ïƒ'
wkSubPresâŸ¶ w (cong-`,âŸ¶â‚›1 r) = cong-`,âŸ¶â‚›1 (wkSubPresâŸ¶ w r)
wkSubPresâŸ¶ w (cong-`,âŸ¶â‚›2 r) = cong-`,âŸ¶â‚›2 (wkTmPresâ‰ˆ w r)
wkSubPresâŸ¶ w (cong-lockâŸ¶â‚› r) = cong-lockâŸ¶â‚› (wkSubPresâŸ¶ _ r)
wkSubPresâŸ¶ w (shift-lockâŸ¶â‚› {s = s} w' {e}) = RelReasoning.â‰¡-step-â‰¡ _âŸ¶â‚›_
  (let open â‰¡-Reasoning in begin
  wkSub w (lock s (extRAssoc (upLFExt w') e))
     â‰¡âŸ¨âŸ©
  lock
    (wkSub (factorWk (extRAssoc (upLFExt w') e) w) s)
    (factorExt (extRAssoc (upLFExt w') e) w)
    -- add substs
    â‰¡âŸ¨ â‰…-to-â‰¡ (cong-lockâ‰… (lCtxPresTrans (upLFExt w') e w) (rCtxPresTrans (upLFExt w') e w) (â‰¡-subst-addable _ _ _) (â‰¡-substâ‚‚-addable _ _ _ _)) âŸ©
  lock
    (subst (Î» Î“L â†’ Sub Î“L _) (lCtxPresTrans (upLFExt w') e w) (wkSub (factorWk (extRAssoc (upLFExt w') e) w) s))
    (substâ‚‚ (CExt _) (lCtxPresTrans (upLFExt w') e w) (rCtxPresTrans (upLFExt w') e w) (factorExt (extRAssoc (upLFExt w') e) w))
    -- push subst on subterm inside
    â‰¡âŸ¨ congâ‚‚ lock (subst-applicationâ€² (_ âŠ†_) (Î» w â†’ wkSub w s) (lCtxPresTrans (upLFExt w') e w)) â‰¡-refl âŸ©
  lock
    (wkSub (subst (_ âŠ†_) (lCtxPresTrans (upLFExt w') e w) (factorWk (extRAssoc (upLFExt w') e) w)) s)
    (substâ‚‚ (CExt _) (lCtxPresTrans (upLFExt w') e w) (rCtxPresTrans (upLFExt w') e w) (factorExt (extRAssoc (upLFExt w') e) w))
    -- factorisation preserves transitivity
    â‰¡âŸ¨ congâ‚‚ lock (congâ‚‚ wkSub (factorWkPresTrans (upLFExt w') e w) â‰¡-refl) (factorExtPresTrans (upLFExt w') _ _) âŸ©
  lock
    (wkSub (factorWk (upLFExt w') (factorWk e w)) s)
    (extRAssoc (factorExt (upLFExt w') (factorWk e w)) (factorExt e w))
    -- apply equalities for absorption of upLFExt
    â‰¡âŸ¨ congâ‚‚ lock (congâ‚‚ wkSub (â‰¡-sym (factorWkAbsorbsUpLFExt w' (factorWk e w))) â‰¡-refl) (congâ‚‚ extRAssoc (â‰¡-sym (factorExtAbsorbsUpLFExt w' (factorWk e w))) â‰¡-refl) âŸ©
  lock
    (wkSub (subst (_ âŠ†_) (lCtxAbsorbsUpLFExt w' (factorWk e w)) (factorWk w' (factorWk e w))) s)
    (extRAssoc (substâ‚‚ (CExt _) (lCtxAbsorbsUpLFExt w' (factorWk e w)) (rCtxAbsorbsUpLFExt w' (factorWk e w)) (upLFExt (factorExt w' (factorWk e w)))) (factorExt e w))
    -- pull out substs
    â‰¡âŸ¨ congâ‚‚ lock (â‰¡-sym (subst-applicationâ€² (_ âŠ†_) (Î» x â†’ wkSub x s) (lCtxAbsorbsUpLFExt w' (factorWk e w)))) (ExtIsProp _ _) âŸ©
  lock
    (subst (Î» Î“L â†’ Sub Î“L _) (lCtxAbsorbsUpLFExt w' (factorWk e w)) (wkSub (factorWk w' (factorWk e w)) s))
    (substâ‚‚ (Î» Î“L Î“R â†’ CExt _ Î“L (Î“R ,, _)) (lCtxAbsorbsUpLFExt w' (factorWk e w)) (rCtxAbsorbsUpLFExt w' (factorWk e w)) (extRAssoc (upLFExt (factorExt w' (factorWk e w))) (factorExt e w)))
    -- remove substs
    â‰¡âŸ¨ â‰…-to-â‰¡ (cong-lockâ‰… (â‰¡-sym (lCtxAbsorbsUpLFExt w' (factorWk e w))) (â‰¡-sym (cong (_,, _) (rCtxAbsorbsUpLFExt w' (factorWk e w)))) (â‰¡-subst-removable _ _ _) (â‰¡-substâ‚‚-removable _ _ _ _)) âŸ©
  lock
   (wkSub (factorWk w' (factorWk e w)) s)
   (extRAssoc (upLFExt (factorExt w' (factorWk e w))) (factorExt e w)) âˆ)
  (shift-lockâŸ¶â‚› _)
  (let open â‰¡-Reasoning in begin
  lock
   (wkSub (LFExtToâŠ† (factorExt w' (factorWk e w))) (wkSub (factorWk w' (factorWk e w)) s))
   (factorExt e w)
   -- wkSub preserves composition
   â‰¡âŸ¨ congâ‚‚ lock (wkSubPresâˆ™ _ _ _) â‰¡-refl âŸ©
  lock
   (wkSub (factorWk w' (factorWk e w) âˆ™ LFExtToâŠ† (factorExt w' (factorWk e w))) s)
   (factorExt e w)
   -- apply factorisation lemma
   â‰¡âŸ¨ congâ‚‚ lock (congâ‚‚ wkSub (â‰¡-sym (factorisationLemma w' _)) â‰¡-refl) â‰¡-refl âŸ©
  lock
   (wkSub (LFExtToâŠ† w' âˆ™ factorWk e w) s)
   (factorExt e w)
   -- wkSub preserves composition
   â‰¡âŸ¨ congâ‚‚ lock (â‰¡-sym (wkSubPresâˆ™ _ _ _)) â‰¡-refl âŸ©
  lock
   (wkSub (factorWk e w) (wkSub (LFExtToâŠ† w') s))
   (factorExt e w)
   â‰¡âŸ¨âŸ©
  wkSub w (lock (wkSub (LFExtToâŠ† w') s) e) âˆ)

wkSubPresâ‰ˆ : (w : Î” âŠ† Î”') â†’ Ïƒ â‰ˆâ‚› Ïƒ' â†’ wkSub w Ïƒ â‰ˆâ‚› wkSub w Ïƒ'
wkSubPresâ‰ˆ w = cong-âŸ¶â‚›-to-cong-â‰ˆâ‚› (wkSubPresâŸ¶ w)

fact-extâ‰… : (e : CExt Î“ Î“L Î“R)
  â†’ e â‰… extRAssoc (upLFExt (factorSubâ‚›IdWk e)) (factorExtâ‚› e idâ‚›)
fact-extâ‰… e = â‰…-trans
  (â‰¡-subst-addable _ _ _)
  (â‰¡-to-â‰… (ExtIsPropâ€² e (extRAssoc (upLFExt (factorSubâ‚›IdWk e)) (factorExtâ‚› e idâ‚›))))

substTmPresId : (t : Tm Î“ a) â†’ t â‰ˆ substTm idâ‚› t
substTmPresId (var x)     = â‰ˆ-reflexiveË˜ (substVarPresId x)
substTmPresId (lam t)     = cong-lamâ‰ˆ (substTmPresId t)
substTmPresId (app t u)   = cong-appâ‰ˆ (substTmPresId t) (substTmPresId u)
substTmPresId (box t)     = cong-boxâ‰ˆ (substTmPresId t)
substTmPresId (unbox t e) = fact-unboxâ‰ˆ t e
  where
  --
  coh-wkTm-substTm : (t : Tm Î“ a) (w : Î“ âŠ† Î“') â†’ wkTm w t â‰ˆ substTm (embWk w) t
  coh-wkTm-substTm {a = a} {Î“' = Î“'} t w = begin
    wkTm w t
      -- apply IH
      â‰ˆâŸ¨ wkTmPresâ‰ˆ w (substTmPresId t) âŸ©
    wkTm w (substTm idâ‚› t)
      -- apply naturality of substTm
      â‰¡âŸ¨ â‰¡-sym (nat-substTm t idâ‚› w) âŸ©
    substTm (wkSub w idâ‚›) t
      -- weakening id subst is same as embedding the weakening into a subst
      â‰¡âŸ¨ congâ‚‚ substTm {u = t} (wkSubId w) â‰¡-refl âŸ©
    substTm (embWk w) t âˆ
    where
    open SetoidReasoning (Tm-setoid Î“' a)
  --
  fact-unboxâ‰ˆ : (t : Tm Î“L (â—» a)) (e : CExt Î“ Î“L Î“R)
    â†’ unbox t e â‰ˆ unbox (substTm (factorSubâ‚› e idâ‚›) t) (factorExtâ‚› e idâ‚›)
  fact-unboxâ‰ˆ {a = a} {Î“ = Î“} t e = begin
    unbox t e
      -- expand extension e
      â‰¡âŸ¨ â‰…-to-â‰¡ (cong-unboxâ‰… â‰¡-refl (extRUniq e (extRAssoc (upLFExt (factorSubâ‚›IdWk e)) (factorExtâ‚› e idâ‚›))) â‰…-refl (fact-extâ‰… e)) âŸ©
    unbox t (extRAssoc (upLFExt (factorSubâ‚›IdWk e)) (factorExtâ‚› e idâ‚›))
      -- apply shift-unbox
      â‰ˆâŸ¨ âŸ¶-to-â‰ˆ (shift-unbox _ _ _) âŸ©
    unbox (wkTm (LFExtToâŠ† (factorSubâ‚›IdWk e)) t) (factorExtâ‚› e idâ‚›)
      -- rewrite wkTm to substTm
      â‰ˆâŸ¨ cong-unbox1â‰ˆ (coh-wkTm-substTm t _) âŸ©
    unbox (substTm (embWk (LFExtToâŠ† (factorSubâ‚›IdWk e))) t) (factorExtâ‚› e idâ‚›)
      -- show that the subst is the factorisation of the id subst
      â‰¡âŸ¨ congâ‚‚ unbox (congâ‚‚ substTm {u = t} (â‰¡-sym (factorSubâ‚›IdWkIsFactorSubâ‚›Id e)) â‰¡-refl) â‰¡-refl âŸ©
    unbox (substTm (factorSubâ‚› e idâ‚›) t) (factorExtâ‚› e idâ‚›) âˆ
    where
    open SetoidReasoning (Tm-setoid Î“ a)

rightIdSub : (s : Sub Î“ Î“') â†’ s â‰ˆâ‚› (s âˆ™â‚› idâ‚›)
rightIdSub []         = â‰ˆâ‚›-refl
rightIdSub (s `, t)   = cong-`,â‰ˆâ‚› (rightIdSub s) (substTmPresId t)
rightIdSub (lock s e) = fact-lockâ‰ˆ s e
  where
  --
  fact-lockâ‰ˆ : (s : Sub Î“L Î”) (e : CExt Î“ Î“L Î“R)
    â†’ lock s e â‰ˆâ‚› lock (s âˆ™â‚› factorSubâ‚› e idâ‚›) (factorExtâ‚› e idâ‚›)
  fact-lockâ‰ˆ {Î” = Î”} {Î“ = Î“} s e = begin
    lock s e
      -- expand extension e
      â‰¡âŸ¨ â‰…-to-â‰¡ (cong-lockâ‰… â‰¡-refl (extRUniq e (extRAssoc (upLFExt (factorSubâ‚›IdWk e)) (factorExtâ‚› e idâ‚›))) â‰…-refl (fact-extâ‰… e)) âŸ©
    lock s (extRAssoc (upLFExt (factorSubâ‚›IdWk e)) (factorExtâ‚› e idâ‚›))
      -- apply shift-lockâ‰ˆâ‚›
      â‰ˆâŸ¨ shift-lockâ‰ˆâ‚› _ âŸ©
    lock (wkSub (LFExtToâŠ† (factorSubâ‚›IdWk e)) s) (factorExtâ‚› e idâ‚›)
      -- apply IH
      â‰ˆâŸ¨ cong-lockâ‰ˆâ‚› (wkSubPresâ‰ˆ _ (rightIdSub s)) âŸ©
    lock (wkSub (LFExtToâŠ† (factorSubâ‚›IdWk e)) (s âˆ™â‚› idâ‚›)) (factorExtâ‚› e idâ‚›)
      -- rewrite using coherence between weakening and composing substs (associativity, really)
      â‰¡âŸ¨ congâ‚‚ lock (coh-wkSub-âˆ™â‚› s idâ‚› (LFExtToâŠ† (factorSubâ‚›IdWk e))) â‰¡-refl âŸ©
    lock (s âˆ™â‚› wkSub (LFExtToâŠ† (factorSubâ‚›IdWk e)) idâ‚›) (factorExtâ‚› e idâ‚›)
      --  weakening of id subst is itself a weakening
      â‰¡âŸ¨ congâ‚‚ lock (cong (s âˆ™â‚›_) (wkSubId _)) â‰¡-refl âŸ©
    lock (s âˆ™â‚› (embWk (LFExtToâŠ† (factorSubâ‚›IdWk e)))) (factorExtâ‚› e idâ‚›)
      -- show that the weakening subst is the factorisation of the id subst
      â‰¡âŸ¨ congâ‚‚ lock (cong (s âˆ™â‚›_) (â‰¡-sym (factorSubâ‚›IdWkIsFactorSubâ‚›Id e))) â‰¡-refl âŸ©
    lock (s âˆ™â‚› factorSubâ‚› e idâ‚›) (factorExtâ‚› e idâ‚›) âˆ
    where
    open SetoidReasoning (Sub-setoid Î“ (Î” ğŸ”’))

substVarPresâŸ¶ : (v : Var Î“ a) â†’ Ïƒ âŸ¶â‚› Ïƒ' â†’ substVar Ïƒ v â‰ˆ substVar Ïƒ' v
substVarPresâŸ¶ ze     (cong-`,âŸ¶â‚›1 sâŸ¶s') = â‰ˆ-refl
substVarPresâŸ¶ ze     (cong-`,âŸ¶â‚›2 tâ‰ˆt') = tâ‰ˆt'
substVarPresâŸ¶ (su v) (cong-`,âŸ¶â‚›1 sâŸ¶s') = substVarPresâŸ¶ v sâŸ¶s'
substVarPresâŸ¶ (su v) (cong-`,âŸ¶â‚›2 tâ‰ˆt') = â‰ˆ-refl

-- XXX: fold
substVarPresâ‰ˆ : (v : Var Î“ a) â†’ Ïƒ â‰ˆâ‚› Ïƒ' â†’ substVar Ïƒ v â‰ˆ substVar Ïƒ' v
substVarPresâ‰ˆ v Îµ                    = â‰ˆ-refl
substVarPresâ‰ˆ v (injâ‚ ÏƒâŸ¶Ïƒ' â—… Ïƒ'â‰ˆÏƒ'') = â‰ˆ-trans (substVarPresâŸ¶ v ÏƒâŸ¶Ïƒ') (substVarPresâ‰ˆ v Ïƒ'â‰ˆÏƒ'')
substVarPresâ‰ˆ v (injâ‚‚ Ïƒ'âŸ¶Ïƒ â—… Ïƒâ‰ˆÏƒ'')  = â‰ˆ-Ë˜trans (substVarPresâŸ¶ v Ïƒ'âŸ¶Ïƒ) (substVarPresâ‰ˆ v Ïƒâ‰ˆÏƒ'')

substTmPresâŸ¶ : (t : Tm Î“ a) â†’ Ïƒ âŸ¶â‚› Ïƒ' â†’ substTm Ïƒ t â‰ˆ substTm Ïƒ' t
substTmPresâŸ¶ (var v)     r = substVarPresâŸ¶ v r
substTmPresâŸ¶ (lam t)     r = cong-lamâ‰ˆ (substTmPresâŸ¶ t (cong-`,âŸ¶â‚›1 (wkSubPresâŸ¶ fresh r)))
substTmPresâŸ¶ (app t u)   r = cong-appâ‰ˆ (substTmPresâŸ¶ t r) (substTmPresâŸ¶ u r)
substTmPresâŸ¶ (box t)     r = cong-boxâ‰ˆ (substTmPresâŸ¶ t (cong-lockâŸ¶â‚› r))
substTmPresâŸ¶ (unbox t e) r = h e r t
  where
    h : âˆ€ (e    : CExt Î“ Î“L Î“R)
          (ÏƒâŸ¶Ïƒ' : Ïƒ âŸ¶â‚› Ïƒ')
          (t    : Tm Î“L (â—» a))
          {e'   : CExt Î˜ _ Î”R}
          {e''  : CExt Î˜ _ Î”R'}
        â†’ unbox (substTm (factorSubâ‚› e Ïƒ)  t) e'
        â‰ˆ unbox (substTm (factorSubâ‚› e Ïƒ') t) e''
    h nil        ÏƒâŸ¶â‚›Ïƒ'                    t = cong-unboxâ‰ˆ (substTmPresâŸ¶ t ÏƒâŸ¶â‚›Ïƒ')
    h (ext e)    (cong-`,âŸ¶â‚›1 ÏƒâŸ¶Ïƒ')        t = h e ÏƒâŸ¶Ïƒ' t
    h (ext e)    (cong-`,âŸ¶â‚›2 tâ‰ˆt')        t = cong-unbox2â‰ˆ
    h (extğŸ”’- e) (cong-lockâŸ¶â‚› ÏƒâŸ¶Ïƒ')       t = h e ÏƒâŸ¶Ïƒ' t
    h (extğŸ”’- e) (shift-lockâŸ¶â‚› {s = Ïƒ} w) t {e'} {e''} = let open SetoidReasoning (Tm-setoid _ _) in
        begin
          unbox (substTm (factorSubâ‚› e Ïƒ) t) e'
        â‰ˆâŸ¨ shift-unboxâ‰ˆ (substTm (factorSubâ‚› e Ïƒ) t) (factorDropsWk (factorExtâ‚› e Ïƒ) w) âŸ©
          unbox (wkTm (LFExtToâŠ† (factorDropsWk (factorExtâ‚› e Ïƒ) w)) (substTm (factorSubâ‚› e Ïƒ) t)) (subst (Î» Î” â†’ CExt _ Î” _) (lCtxâ‚›-wkSub-comm e (LFExtToâŠ† w) Ïƒ) e'')
        â‰¡âŸ¨ cong (Î» w' â†’ unbox (wkTm w' _) (subst (Î» Î” â†’ CExt _ Î” _) (lCtxâ‚›-wkSub-comm e (LFExtToâŠ† w) Ïƒ) e'')) (factorDropsWkIsfactorWk (factorExtâ‚› e Ïƒ) w) âŸ©
          unbox (wkTm (factorWk (factorExtâ‚› e Ïƒ) (LFExtToâŠ† w)) (substTm (factorSubâ‚› e Ïƒ) t)) (subst (Î» Î” â†’ CExt _ Î” _) (lCtxâ‚›-wkSub-comm e (LFExtToâŠ† w) Ïƒ) e'')
        â‰¡Ë˜âŸ¨ congâ‚‚ unbox (nat-substTm t (factorSubâ‚› e Ïƒ) (factorWk (factorExtâ‚› e Ïƒ) (LFExtToâŠ† w))) â‰¡-refl âŸ©
          unbox (substTm (wkSub (factorWk (factorExtâ‚› e Ïƒ) (LFExtToâŠ† w)) (factorSubâ‚› e Ïƒ)) t) (subst (Î» Î” â†’ CExt _ Î” _) (lCtxâ‚›-wkSub-comm e (LFExtToâŠ† w) Ïƒ) e'')
        â‰¡Ë˜âŸ¨ dcongâ‚ƒ (Î» _Î” s e â†’ unbox (substTm s t) e) (lCtxâ‚›-wkSub-comm e (LFExtToâŠ† w) Ïƒ) (factorSubâ‚›-wkSub-comm e Ïƒ (LFExtToâŠ† w)) â‰¡-refl âŸ©
          unbox (substTm (factorSubâ‚› e (wkSub (LFExtToâŠ† w) Ïƒ)) t) e''
        âˆ

-- XXX: fold
substTmPresâ‰ˆ : (t : Tm Î“ a) â†’ (Ïƒâ‰ˆÏƒ' : Ïƒ â‰ˆâ‚› Ïƒ') â†’ substTm Ïƒ t â‰ˆ substTm Ïƒ' t
substTmPresâ‰ˆ t Îµ                    = â‰ˆ-refl
substTmPresâ‰ˆ t (injâ‚ ÏƒâŸ¶Ïƒ' â—… Ïƒ'â‰ˆÏƒ'') = â‰ˆ-trans (substTmPresâŸ¶ t ÏƒâŸ¶Ïƒ') (substTmPresâ‰ˆ t Ïƒ'â‰ˆÏƒ'')
substTmPresâ‰ˆ t (injâ‚‚ Ïƒ'âŸ¶Ïƒ â—… Ïƒâ‰ˆÏƒ'')  = â‰ˆ-Ë˜trans (substTmPresâŸ¶ t Ïƒ'âŸ¶Ïƒ) (substTmPresâ‰ˆ t Ïƒâ‰ˆÏƒ'')
