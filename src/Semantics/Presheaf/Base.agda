{-# OPTIONS --safe --without-K #-}
module Semantics.Presheaf.Base
  (C       : Set)
  (_âŠ†_     : (Î“ Î“' : C) â†’ Set)
  (âŠ†-refl  : âˆ€ {Î“ : C} â†’ Î“ âŠ† Î“)
  (âŠ†-trans : âˆ€ {Î“ Î“' Î“'' : C} â†’ (_w : Î“ âŠ† Î“') â†’ (_w' : Î“' âŠ† Î“'') â†’ Î“ âŠ† Î“'')
  where

open import Level using (0â„“)

open import Relation.Binary using (Reflexive; Symmetric; Transitive; IsEquivalence; Setoid)
import Relation.Binary.Reasoning.Setoid as EqReasoning
open import Relation.Binary.PropositionalEquality using (_â‰¡_; refl; sym; trans; subst; cong)

infixr 19 _âˆ˜_
infix  18 _â†’Ì‡_ _â‰ˆÌ‡_

private
  variable
    Î“ Î“' Î“'' : C
    Î” Î”' Î”'' : C
    Î˜ Î˜' Î˜'' : C
    w w' w'' : Î“ âŠ† Î”

record Psh : Setâ‚ where
  no-eta-equality
  field
    -- setoids
    Fam       : (Î“ : C) â†’ Set
    _â‰‹_       : (x y : Fam Î“) â†’ Set -- type \~~~
    â‰‹-equiv   : âˆ€ (Î“ : C) â†’ IsEquivalence {A = Fam Î“} _â‰‹_

    -- setoid morphisms
    wk        : (w : Î“ âŠ† Î”) â†’ (x : Fam Î“) â†’ Fam Î”
    wk-pres-â‰‹ : âˆ€ (w : Î“ âŠ† Î”) {x y : Fam Î“} (xâ‰‹y : x â‰‹ y) â†’ wk w x â‰‹ wk w y

    -- functoriality
    wk-pres-refl  : âˆ€ (x : Fam Î“) â†’ wk âŠ†-refl x â‰‹ x
    wk-pres-trans : âˆ€ (w : Î“ âŠ† Î“') (w' : Î“' âŠ† Î“'') (x : Fam Î“) â†’ wk (âŠ†-trans w w') x â‰‹ wk w' (wk w x)

  infix 19 Fam

  Fam-setoid : {Î“ : C} â†’ Setoid _ _
  Fam-setoid {Î“} = record
    { Carrier       = Fam Î“
    ; _â‰ˆ_           = _â‰‹_
    ; isEquivalence = â‰‹-equiv Î“
    }

  wk-pres-â‰¡-â‰‹ : âˆ€ {w w' : Î“ âŠ† Î”} (wâ‰¡w' : w â‰¡ w') {x y : Fam Î“} (xâ‰‹y : x â‰‹ y) â†’ wk w x â‰‹ wk w' y
  wk-pres-â‰¡-â‰‹ {w = w} {.w} refl = wk-pres-â‰‹ w

  module _ {Î“ : C} where
    open IsEquivalence (â‰‹-equiv Î“) public
      using ()
      renaming
        ( refl      to â‰‹-refl
        ; sym       to â‰‹-sym
        ; trans     to â‰‹-trans
        ; reflexive to â‰‹-reflexive
        )

  â‰‹-reflexiveË˜ : âˆ€ {x y : Fam Î“} â†’ y â‰¡ x â†’ x â‰‹ y
  â‰‹-reflexiveË˜ refl = â‰‹-refl

-- open Psh {{...}} using (_â‰‹_; â‰‹-refl; â‰‹-sym; â‰‹-trans; wk) public
-- â‰‹-refl  = Î» {ğ’«} {Î“} {p}         â†’ ğ’« .Psh.â‰‹-refl {Î“} {p}
-- â‰‹-sym   = Î» {ğ’«} {Î“} {p} {q}     â†’ ğ’« .Psh.â‰‹-sym {Î“} {p} {q}
-- â‰‹-trans = Î» {ğ’«} {Î“} {p} {q} {r} â†’ ğ’« .Psh.â‰‹-trans {Î“} {p} {q} {r}
open Psh public
  using ()
  renaming
    ( Fam           to _â‚€_
    ; Fam-setoid    to â‰‹[_]-setoid
    ; â‰‹-refl        to â‰‹[_]-refl
    ; â‰‹-sym         to â‰‹[_]-sym
    ; â‰‹-trans       to â‰‹[_]-trans
    ; â‰‹-reflexive   to â‰‹[_]-reflexive
    ; â‰‹-reflexiveË˜   to â‰‹[_]-reflexiveË˜
    ; wk            to wk[_]
    ; wk-pres-â‰‹     to wk[_]-pres-â‰‹
    ; wk-pres-refl  to wk[_]-pres-refl
    ; wk-pres-trans to wk[_]-pres-trans
    )

private
  variable
    ğ’« ğ’«' : Psh -- type \McP
    ğ’¬ ğ’¬' : Psh -- type \McQ
    â„› â„›' : Psh -- type \McR
    ğ’® ğ’®' : Psh -- type \McS

â‰‹[]-syntax : (ğ’« : Psh) â†’ (x y : ğ’« â‚€ Î“) â†’ Set
â‰‹[]-syntax ğ’« = ğ’« .Psh._â‰‹_

syntax â‰‹[]-syntax ğ’« x y = x â‰‹[ ğ’« ] y

record _â†’Ì‡_ (ğ’« ğ’¬ : Psh) : Set where -- type \-> \^.
  no-eta-equality
  field
    fun     : (p : ğ’« â‚€ Î“) â†’ ğ’¬ â‚€ Î“
    pres-â‰‹  : âˆ€ {p p' : ğ’« â‚€ Î“} (pâ‰‹p' : p â‰‹[ ğ’« ] p') â†’ fun p â‰‹[ ğ’¬ ] fun p'
    natural : âˆ€ (w : Î“ âŠ† Î”) (p : ğ’« â‚€ Î“) â†’ wk[ ğ’¬ ] w (fun p) â‰‹[ ğ’¬ ] fun (wk[ ğ’« ] w p)

open _â†’Ì‡_ using (natural) renaming (fun to apply; pres-â‰‹ to apply-â‰‹) public

record _â‰ˆÌ‡_ (Ï† Ïˆ : ğ’« â†’Ì‡ ğ’¬) : Set where -- type \~~ \^.
  no-eta-equality
  field
    proof : âˆ€ (p : ğ’« â‚€ Î“) â†’ Ï† .apply p â‰‹[ ğ’¬ ] Ïˆ .apply p

  apply-sq : âˆ€ {p p' : ğ’« â‚€ Î“} â†’ p â‰‹[ ğ’« ] p' â†’ Ï† .apply p â‰‹[ ğ’¬ ] Ïˆ .apply p' -- XXX: rename
  apply-sq {p = p} {p'} pâ‰‹p' = let open EqReasoning â‰‹[ ğ’¬ ]-setoid in begin
    Ï† .apply p   â‰ˆâŸ¨ Ï† .apply-â‰‹ pâ‰‹p' âŸ©
    Ï† .apply p'  â‰ˆâŸ¨ proof p' âŸ©
    Ïˆ .apply p'  âˆ

open _â‰ˆÌ‡_ using (apply-sq) renaming (proof to apply-â‰‹) public

private
  variable
    Ï† Ï†' : ğ’« â†’Ì‡ ğ’¬
    Ïˆ Ïˆ' : ğ’« â†’Ì‡ ğ’¬
    Ï‰ Ï‰' : ğ’« â†’Ì‡ ğ’¬

module _ {ğ’« ğ’¬ : Psh} where
  abstract
    â‰ˆÌ‡-refl : Reflexive {A = ğ’« â†’Ì‡ ğ’¬} _â‰ˆÌ‡_
    â‰ˆÌ‡-refl = record { proof = Î» {_} _ â†’ â‰‹[ ğ’¬ ]-refl }

    â‰ˆÌ‡-sym : Symmetric {A = ğ’« â†’Ì‡ ğ’¬} _â‰ˆÌ‡_
    â‰ˆÌ‡-sym Ï†â‰‹Ï†' = record { proof = Î» {_} p â†’ â‰‹[ ğ’¬ ]-sym (Ï†â‰‹Ï†' ._â‰ˆÌ‡_.proof p) }

    â‰ˆÌ‡-trans : Transitive {A = ğ’« â†’Ì‡ ğ’¬} _â‰ˆÌ‡_
    â‰ˆÌ‡-trans Ï†â‰‹Ïˆ Ïˆâ‰‹Ï‰ = record { proof = Î» {_} p â†’ â‰‹[ ğ’¬ ]-trans (Ï†â‰‹Ïˆ ._â‰ˆÌ‡_.proof p) (Ïˆâ‰‹Ï‰ ._â‰ˆÌ‡_.proof p) }

    â‰ˆÌ‡-equiv : IsEquivalence {A = ğ’« â†’Ì‡ ğ’¬} _â‰ˆÌ‡_
    â‰ˆÌ‡-equiv = record
      { refl  = â‰ˆÌ‡-refl
      ; sym   = â‰ˆÌ‡-sym
      ; trans = â‰ˆÌ‡-trans
      }

module _ (ğ’« ğ’¬ : Psh) where
  â†’Ì‡-setoid : Setoid 0â„“ 0â„“
  â†’Ì‡-setoid = record
    { Carrier       = ğ’« â†’Ì‡ ğ’¬
    ; _â‰ˆ_           = _â‰ˆÌ‡_
    ; isEquivalence = â‰ˆÌ‡-equiv
    }

_âˆ˜_ : (Ïˆ : ğ’¬ â†’Ì‡ â„›) â†’ (Ï† : ğ’« â†’Ì‡ ğ’¬) â†’ ğ’« â†’Ì‡ â„›
_âˆ˜_ {ğ’¬} {â„›} {ğ’«} Ïˆ Ï† = record
  { fun     = Î» p â†’ Ïˆ .apply (Ï† .apply p)
  ; pres-â‰‹  = Î» pâ‰‹p' â†’ Ïˆ .apply-â‰‹ (Ï† .apply-â‰‹ pâ‰‹p')
  ; natural = Î» w p â†’ let open EqReasoning â‰‹[ â„› ]-setoid in begin
      wk[ â„› ] w (Ïˆ .apply (Ï† .apply p))  â‰ˆâŸ¨ Ïˆ .natural _ _ âŸ©
      Ïˆ .apply (wk[ ğ’¬ ] w (Ï† .apply p))  â‰ˆâŸ¨ Ïˆ .apply-â‰‹ (Ï† .natural _ _) âŸ©
      Ïˆ .apply (Ï† .apply (wk[ ğ’« ] w p))  âˆ
  }

_[_]' = _âˆ˜_

abstract
  âˆ˜-pres-â‰ˆÌ‡ : Ïˆ â‰ˆÌ‡ Ïˆ' â†’ Ï† â‰ˆÌ‡ Ï†' â†’ Ïˆ âˆ˜ Ï† â‰ˆÌ‡ Ïˆ' âˆ˜ Ï†'
  âˆ˜-pres-â‰ˆÌ‡ Ïˆâ‰ˆÌ‡Ïˆ' Ï†â‰ˆÌ‡Ï†' = record { proof = Î» p â†’ apply-sq Ïˆâ‰ˆÌ‡Ïˆ' (Ï†â‰ˆÌ‡Ï†' .apply-â‰‹ p) }

  âˆ˜-pres-â‰ˆÌ‡-left : âˆ€ (_ : Ïˆ â‰ˆÌ‡ Ïˆ') (Ï† : ğ’« â†’Ì‡ ğ’¬) â†’ Ïˆ âˆ˜ Ï† â‰ˆÌ‡ Ïˆ' âˆ˜ Ï†
  âˆ˜-pres-â‰ˆÌ‡-left Ïˆâ‰ˆÌ‡Ïˆ' Ï† = âˆ˜-pres-â‰ˆÌ‡ Ïˆâ‰ˆÌ‡Ïˆ' (â‰ˆÌ‡-refl {x = Ï†})

  âˆ˜-pres-â‰ˆÌ‡-right : âˆ€ (Ïˆ : ğ’¬ â†’Ì‡ â„›) (_ : Ï† â‰ˆÌ‡ Ï†') â†’ Ïˆ âˆ˜ Ï† â‰ˆÌ‡ Ïˆ âˆ˜ Ï†'
  âˆ˜-pres-â‰ˆÌ‡-right Ïˆ Ï†â‰ˆÌ‡Ï†' = âˆ˜-pres-â‰ˆÌ‡ (â‰ˆÌ‡-refl {x = Ïˆ}) Ï†â‰ˆÌ‡Ï†'

  âˆ˜-assoc : âˆ€ (Ï‰ : â„› â†’Ì‡ ğ’®) (Ïˆ : ğ’¬ â†’Ì‡ â„›) (Ï† : ğ’« â†’Ì‡ ğ’¬) â†’ (Ï‰ âˆ˜ Ïˆ) âˆ˜ Ï† â‰ˆÌ‡ Ï‰ âˆ˜ Ïˆ âˆ˜ Ï†
  âˆ˜-assoc {_} {â„›} Ï‰ Ïˆ Ï† = record { proof = Î» p â†’ â‰‹[ â„› ]-refl }

id'[_] : (ğ’« : Psh) â†’ ğ’« â†’Ì‡ ğ’«
id'[_] ğ’« = record
  { fun     = Î» p â†’ p
  ; pres-â‰‹  = Î» pâ‰‹p' â†’ pâ‰‹p'
  ; natural = Î» _ _ â†’ â‰‹[ ğ’« ]-refl
  }

id' = Î» {ğ’«} â†’ id'[ ğ’« ]

abstract
  id'-unit-left : âˆ€ {ğ’« : Psh} (ğ’¬ : Psh) (Ï† : ğ’« â†’Ì‡ ğ’¬) â†’ id'[ ğ’¬ ] âˆ˜ Ï† â‰ˆÌ‡ Ï†
  id'-unit-left ğ’¬ _ = record { proof = Î» p â†’ â‰‹[ ğ’¬ ]-refl }

  id'-unit-right : âˆ€ (ğ’« : Psh) {ğ’¬ : Psh} (Ï† : ğ’« â†’Ì‡ ğ’¬) â†’ Ï† âˆ˜ id'[ ğ’« ] â‰ˆÌ‡ Ï†
  id'-unit-right _ {ğ’¬} _ = record { proof = Î» p â†’ â‰‹[ ğ’¬ ]-refl }
