module IS4.Conversion where

open import HEUtil
open import IS4.Term
open import IS4.Reduction
  as Reduction
open import IS4.HellOfSyntacticLemmas

import Data.Sum as Sum

open import Relation.Nullary
  using (Â¬_)

open import Relation.Binary
  using (Setoid)

open import Relation.Binary.Construct.Closure.Equivalence
  using (setoid)
import Relation.Binary.Construct.Closure.Equivalence.Properties
  as EquivalenceProperties

open import Relation.Binary.Construct.Closure.ReflexiveTransitive
  as ReflexiveTransitive
  using (Star)

open import Relation.Binary.PropositionalEquality
  using    (_â‰¡_ ; cong ; congâ‚‚ ; subst ; substâ‚‚)
  renaming (refl to â‰¡-refl ; sym to â‰¡-sym ; trans to â‰¡-trans)

open import Relation.Binary.HeterogeneousEquality as HE
  using (_â‰…_)
  renaming (refl to â‰…-refl ; sym to â‰…-sym ; trans to â‰…-trans)

open Sum public
  using (injâ‚ ; injâ‚‚)
open ReflexiveTransitive public
  using (Îµ ; _â—…_)
open EquivalenceProperties public
  using    ()
  renaming (aâ€”â† bâ‡’aâ†”b to âŸ¶*-to-â‰ˆ)

-- Convertibility is defined taking the
-- equivalence closure of the reduction relation.
Tm-setoid : (Î“ : Ctx) â†’ (a : Ty) â†’ Setoid _ _
Tm-setoid Î“ a = setoid (_âŸ¶_ {Î“} {a})

module _ {Î“ : Ctx} {a : Ty} where
  open Setoid (Tm-setoid Î“ a) public
    using    (_â‰ˆ_)
    renaming (refl to â‰ˆ-refl ; reflexive to â‰¡-to-â‰ˆ ; sym to â‰ˆ-sym ; trans to â‰ˆ-trans ; isEquivalence to â‰ˆ-equiv)

â‰¡Ë˜-to-â‰ˆ : t' â‰¡ t â†’ t â‰ˆ t'
â‰¡Ë˜-to-â‰ˆ t'â‰¡t = â‰¡-to-â‰ˆ (â‰¡-sym t'â‰¡t)

âŸ¶-to-â‰ˆ : t âŸ¶ t' â†’ t â‰ˆ t'
âŸ¶-to-â‰ˆ p = injâ‚ p â—… Îµ

âŸµ-to-â‰ˆ : t' âŸ¶ t â†’ t â‰ˆ t'
âŸµ-to-â‰ˆ p = injâ‚‚ p â—… Îµ

module _ {t : Tm Î“ a â†’ Tm Î” b} (cong-t : âˆ€ {u u' : Tm Î“ a} â†’ (uâŸ¶u' : u âŸ¶ u') â†’ t u âŸ¶ t u') where
  -- XXX: fold
  cong-âŸ¶-to-cong-â‰ˆ : âˆ€ (uâ‰ˆu' : u â‰ˆ u') â†’ t u â‰ˆ t u'
  cong-âŸ¶-to-cong-â‰ˆ Îµ                     = Îµ
  cong-âŸ¶-to-cong-â‰ˆ (injâ‚ uâŸ¶u'' â—… u''â‰ˆu') = injâ‚ (cong-t uâŸ¶u'') â—… cong-âŸ¶-to-cong-â‰ˆ u''â‰ˆu'
  cong-âŸ¶-to-cong-â‰ˆ (injâ‚‚ uâŸµu'' â—… u''â‰ˆu') = injâ‚‚ (cong-t uâŸµu'') â—… cong-âŸ¶-to-cong-â‰ˆ u''â‰ˆu'

red-funâ‰ˆ : (t : Tm (Î“ `, a) b) (u : Tm Î“ a) â†’ (app (lam t) u) â‰ˆ substTm (idâ‚› `, u) t
red-funâ‰ˆ t u = âŸ¶-to-â‰ˆ (Reduction.red-fun t u)

exp-funâ‰ˆ : (t : Tm Î“ (a â‡’ b)) â†’ t â‰ˆ lam (app (wkTm fresh t) (var ze))
exp-funâ‰ˆ t = âŸ¶-to-â‰ˆ (Reduction.exp-fun t)

red-boxâ‰ˆ : (t : Tm (Î“L ğŸ”’) a) (e : CExt Î“ Î“L Î“R) â†’ unbox (box t) e â‰ˆ substTm (lock idâ‚› e) t
red-boxâ‰ˆ t e = âŸ¶-to-â‰ˆ (Reduction.red-box t e)

exp-boxâ‰ˆ : (t : Tm Î“ (â—» a)) â†’ t â‰ˆ box (unbox t new)
exp-boxâ‰ˆ t = âŸ¶-to-â‰ˆ (Reduction.exp-box t)

cong-lamâ‰ˆ : âˆ€ (tâ‰ˆt' : t â‰ˆ t') â†’ lam t â‰ˆ lam t'
cong-lamâ‰ˆ = cong-âŸ¶-to-cong-â‰ˆ Reduction.cong-lam

cong-app1â‰ˆ : âˆ€ (tâ‰ˆt' : t â‰ˆ t') â†’ app t u â‰ˆ app t' u
cong-app1â‰ˆ = cong-âŸ¶-to-cong-â‰ˆ Reduction.cong-app1

cong-app2â‰ˆ : âˆ€ (uâ‰ˆu' : u â‰ˆ u') â†’ app t u â‰ˆ app t u'
cong-app2â‰ˆ = cong-âŸ¶-to-cong-â‰ˆ Reduction.cong-app2

cong-appâ‰ˆ : âˆ€ (tâ‰ˆt' : t â‰ˆ t') (uâ‰ˆu' : u â‰ˆ u') â†’ app t u â‰ˆ app t' u'
cong-appâ‰ˆ tâ‰ˆt' uâ‰ˆu' = â‰ˆ-trans (cong-app1â‰ˆ tâ‰ˆt') (cong-app2â‰ˆ uâ‰ˆu')

cong-boxâ‰ˆ : âˆ€ (tâ‰ˆt' : t â‰ˆ t') â†’ box t â‰ˆ box t'
cong-boxâ‰ˆ = cong-âŸ¶-to-cong-â‰ˆ Reduction.cong-box

cong-unbox1â‰ˆ : âˆ€ (tâ‰ˆt' : t â‰ˆ t') â†’ unbox t e â‰ˆ unbox t' e
cong-unbox1â‰ˆ = cong-âŸ¶-to-cong-â‰ˆ Reduction.cong-unbox

cong-unbox2â‰ˆ : âˆ€ (eâ‰¡e' : e â‰¡ e') â†’ unbox t e â‰ˆ unbox t e'
cong-unbox2â‰ˆ eâ‰¡e' = â‰¡-to-â‰ˆ (congâ‚‚ unbox â‰¡-refl eâ‰¡e')

cong-unboxâ‰ˆ : âˆ€ (tâ‰ˆt' : t â‰ˆ t') (eâ‰¡e' : e â‰¡ e') â†’ unbox t e â‰ˆ unbox t' e'
cong-unboxâ‰ˆ tâ‰ˆt' eâ‰¡e' = â‰ˆ-trans (cong-unbox1â‰ˆ tâ‰ˆt') (cong-unbox2â‰ˆ eâ‰¡e')

data _â‰ˆâ‚›_ : Sub Î” Î“ â†’ Sub Î” Î“ â†’ Set where
  â‰ˆâ‚›-refl    : {s : Sub Î” Î“}
    â†’ s â‰ˆâ‚› s
  â‰ˆâ‚›-trans   : {s s' s'' : Sub Î” Î“}
    â†’ s â‰ˆâ‚› s' â†’ s' â‰ˆâ‚› s'' â†’ s â‰ˆâ‚› s''
  â‰ˆâ‚›-sym     : {s s' : Sub Î” Î“}
    â†’ s â‰ˆâ‚› s' â†’ s' â‰ˆâ‚› s
  cong-`,â‰ˆâ‚›   : {s s' : Sub Î” Î“} {t t' : Tm Î” a}
    â†’ s â‰ˆâ‚› s' â†’ t â‰ˆ t' â†’ (s `, t) â‰ˆâ‚› (s' `, t')
  cong-lockâ‰ˆâ‚›  : {s s' : Sub Î”L Î“L} {e : CExt Î” Î”L Î”R}
    â†’ s â‰ˆâ‚› s' â†’ lock s e â‰ˆâ‚› lock s' e
  shift-lockâ‰ˆâ‚› : {Î”LL Î”LR : Ctx} {s : Sub Î”LL Î“} {e : LFExt Î”L Î”LL Î”LR} {e' : CExt Î” Î”L Î”R}
    â†’ lock s (extRAssoc (upLFExt e) e') â‰ˆâ‚› lock (wkSub (LFExtToâŠ† e) s) e'

â‰¡-to-â‰ˆâ‚› : {s s' : Sub Î” Î“} â†’ s â‰¡ s' â†’ s â‰ˆâ‚› s'
â‰¡-to-â‰ˆâ‚› â‰¡-refl = â‰ˆâ‚›-refl

Sub-setoid : (Î“ Î” : Ctx) â†’ Setoid _ _
Sub-setoid Î“ Î” = record {
  Carrier         = Sub Î“ Î”
  ; _â‰ˆ_           = _â‰ˆâ‚›_
  ; isEquivalence = record {
    refl    = â‰ˆâ‚›-refl
    ; sym   = â‰ˆâ‚›-sym
    ; trans = â‰ˆâ‚›-trans } }

---------
-- Lemmas
---------

invRed :  {t t' : Tm Î“ a}
  â†’ (w : Î“ âŠ† Î“')
  â†’ t âŸ¶ t'
  â†’ wkTm w t â‰ˆ wkTm w t'
invRed w (red-fun t u)
  = â‰ˆ-trans (âŸ¶-to-â‰ˆ (red-fun _ _)) (â‰¡-to-â‰ˆ (beta-wk-lemma w u t))
invRed w (exp-fun _)
  = â‰ˆ-trans (âŸ¶-to-â‰ˆ (exp-fun _)) (â‰¡-to-â‰ˆ (cong lam (congâ‚‚ app keepFreshLemma â‰¡-refl)))
invRed w (red-box t e)
  = â‰ˆ-trans
    (âŸ¶-to-â‰ˆ (red-box _ _))
    (â‰ˆ-trans
      (â‰ˆ-trans
        (â‰¡-to-â‰ˆ (â‰¡-sym (coh-trimSub-wkTm t _ _)))
        (â‰¡-to-â‰ˆ
          (cong
            (Î» s â†’ substTm (lock s (factorExt e w)) t)
            (â‰¡-trans
              (trimSubId (factorWk e w))
              (â‰¡-sym (wkSubId _))))))
      (â‰¡-to-â‰ˆ (nat-substTm t _ _)))
invRed w (exp-box _)
  = âŸ¶-to-â‰ˆ (exp-box _)
invRed w (cong-lam r)
  = cong-lamâ‰ˆ (invRed (keep w) r)
invRed w (cong-box r)
  = cong-boxâ‰ˆ (invRed (keepğŸ”’ w) r)
invRed w (cong-unbox {e = e} r)
  = cong-unboxâ‰ˆ (invRed (factorWk e w ) r) â‰¡-refl
invRed w (cong-app1 r)
  = cong-appâ‰ˆ (invRed w r) Îµ
invRed w (cong-app2 r)
  = cong-appâ‰ˆ Îµ (invRed w r)
invRed {a = a} {Î“' = Î“'} w (shift-unbox t e e')
  = begin
    wkTm w (unbox t (extRAssoc (upLFExt e) e'))
      â‰¡âŸ¨âŸ©
    unbox
      (wkTm (factorWk (extRAssoc (upLFExt e) e') w) t)
      (factorExt (extRAssoc (upLFExt e) e') w)
      -- add substs
      â‰¡âŸ¨ â‰…-to-â‰¡ (cong-unboxâ‰…
        (lCtxPresTrans (upLFExt e) e' w) (rCtxPresTrans (upLFExt e) e' w)
        (â‰¡-subst-addable _ _ _) (â‰¡-substâ‚‚-addable _ _ _ _)) âŸ©
    unbox
      (subst (Î» Î“L â†’ Tm Î“L _) (lCtxPresTrans (upLFExt e) e' w) (wkTm (factorWk (extRAssoc (upLFExt e) e') w) t))
      (substâ‚‚ (CExt _) (lCtxPresTrans (upLFExt e) e' w) (rCtxPresTrans (upLFExt e) e' w) (factorExt (extRAssoc (upLFExt e) e') w))
      -- push subst on subterm inside
      â‰¡âŸ¨ congâ‚‚ unbox (subst-applicationâ€² (_ âŠ†_) (Î» w â†’ wkTm w t) (lCtxPresTrans (upLFExt e) e' w)) â‰¡-refl âŸ©
   unbox
      (wkTm (subst (_ âŠ†_) (lCtxPresTrans (upLFExt e) e' w) (factorWk (extRAssoc (upLFExt e) e') w)) t)
      (substâ‚‚ (CExt _) (lCtxPresTrans (upLFExt e) e' w) (rCtxPresTrans (upLFExt e) e' w) (factorExt (extRAssoc (upLFExt e) e') w))
      -- factorisation preserves transitivity
      â‰¡âŸ¨ congâ‚‚ unbox (congâ‚‚ wkTm (factorWkPresTrans (upLFExt e) e' w) â‰¡-refl) (factorExtPresTrans (upLFExt e) _ _) âŸ©
    unbox
      (wkTm (factorWk (upLFExt e) (factorWk e' w)) t)
      (extRAssoc (factorExt (upLFExt e) (factorWk e' w)) (factorExt e' w))
      -- apply equalities for absorption of upLFExt
      â‰¡âŸ¨ congâ‚‚ unbox (congâ‚‚ wkTm (â‰¡-sym (factorWkAbsorbsUpLFExt e (factorWk e' w))) â‰¡-refl) (congâ‚‚ extRAssoc (â‰¡-sym (factorExtAbsorbsUpLFExt e (factorWk e' w))) â‰¡-refl) âŸ©
    unbox
      (wkTm (subst (_ âŠ†_) (lCtxAbsorbsUpLFExt e (factorWk e' w)) (factorWk e (factorWk e' w))) t)
      (extRAssoc (substâ‚‚ (CExt _) (lCtxAbsorbsUpLFExt e (factorWk e' w)) (rCtxAbsorbsUpLFExt e (factorWk e' w)) (upLFExt (factorExt e (factorWk e' w)))) (factorExt e' w))
      -- pull out substs
      â‰¡âŸ¨ congâ‚‚ unbox (â‰¡-sym (subst-applicationâ€² (_ âŠ†_) (Î» x â†’ wkTm x t) (lCtxAbsorbsUpLFExt e (factorWk e' w)))) (ExtIsProp _ _) âŸ©
    unbox
      (subst (Î» Î“L â†’ Tm Î“L _) (lCtxAbsorbsUpLFExt e (factorWk e' w)) (wkTm (factorWk e (factorWk e' w)) t))
      (substâ‚‚ (Î» Î“L Î“R â†’ CExt _ Î“L (Î“R ,, _)) (lCtxAbsorbsUpLFExt e (factorWk e' w)) (rCtxAbsorbsUpLFExt e (factorWk e' w)) (extRAssoc (upLFExt (factorExt e (factorWk e' w))) (factorExt e' w)))
      -- remove substs
      â‰¡âŸ¨ â‰…-to-â‰¡ (cong-unboxâ‰…
        (â‰¡-sym (lCtxAbsorbsUpLFExt e (factorWk e' w))) (cong (_,, _) (â‰¡-sym (rCtxAbsorbsUpLFExt e (factorWk e' w))))
        (â‰¡-subst-removable _ _ _) (â‰¡-substâ‚‚-removable _ _ _ _)) âŸ©
    unbox
      (wkTm (factorWk e (factorWk e' w)) t)
      (extRAssoc (upLFExt (factorExt e (factorWk e' w))) (factorExt e' w))
      -- apply shift-unbox
      â‰ˆâŸ¨ âŸ¶-to-â‰ˆ (shift-unbox _ _ _) âŸ©
    unbox
      (wkTm (LFExtToâŠ† (factorExt e (factorWk e' w))) (wkTm (factorWk e (factorWk e' w)) t))
      (factorExt e' w)
      -- wkTm preserves composition
      â‰¡âŸ¨ congâ‚‚ unbox (wkTmPresâˆ™ _ _ _) â‰¡-refl âŸ©
    unbox
      (wkTm (factorWk e (factorWk e' w) âˆ™ LFExtToâŠ† (factorExt e (factorWk e' w))) t)
      (factorExt e' w)
      -- apply factorisationLemma
      â‰¡âŸ¨ congâ‚‚ unbox (congâ‚‚ wkTm (â‰¡-sym (factorisationLemma e _)) â‰¡-refl) â‰¡-refl âŸ©
    unbox
      (wkTm (LFExtToâŠ† e âˆ™ factorWk e' w) t)
      (factorExt e' w)
      -- wkTm preserves composition
      â‰¡âŸ¨ congâ‚‚ unbox (â‰¡-sym (wkTmPresâˆ™ _ _ _)) â‰¡-refl âŸ©
    unbox
      (wkTm (factorWk e' w) (wkTm (LFExtToâŠ† e) t))
      (factorExt e' w)
      â‰¡âŸ¨âŸ©
    wkTm w (unbox (wkTm (LFExtToâŠ† e) t) e') âˆ
  where
  open import Relation.Binary.Reasoning.Setoid (Tm-setoid Î“' a)

wkTmPresâ‰ˆ  : {t t' : Tm Î“ a} â†’ (w : Î“ âŠ† Î“') â†’ t â‰ˆ t' â†’ wkTm w t â‰ˆ wkTm w t'
wkTmPresâ‰ˆ w Îµ            = Îµ
wkTmPresâ‰ˆ w (injâ‚ x â—… r) = â‰ˆ-trans (invRed w x) (wkTmPresâ‰ˆ w r)
wkTmPresâ‰ˆ w (injâ‚‚ y â—… r) = â‰ˆ-trans (â‰ˆ-sym (invRed w y)) (wkTmPresâ‰ˆ w r)

wkSubPresâ‰ˆ  : {s s' : Sub Î” Î“} â†’ (w : Î” âŠ† Î”') â†’ s â‰ˆâ‚› s' â†’ wkSub w s â‰ˆâ‚› wkSub w s'
wkSubPresâ‰ˆ w â‰ˆâ‚›-refl         = â‰ˆâ‚›-refl
wkSubPresâ‰ˆ w (â‰ˆâ‚›-trans r r') = â‰ˆâ‚›-trans (wkSubPresâ‰ˆ w r) (wkSubPresâ‰ˆ w r')
wkSubPresâ‰ˆ w (â‰ˆâ‚›-sym r)      = â‰ˆâ‚›-sym (wkSubPresâ‰ˆ w r)
wkSubPresâ‰ˆ w (cong-`,â‰ˆâ‚› r r') = cong-`,â‰ˆâ‚› (wkSubPresâ‰ˆ w r) (wkTmPresâ‰ˆ w r')
wkSubPresâ‰ˆ w (cong-lockâ‰ˆâ‚› r) = cong-lockâ‰ˆâ‚› (wkSubPresâ‰ˆ _ r)
wkSubPresâ‰ˆ {Î”} {Î“} {Î”'} w (shift-lockâ‰ˆâ‚› {s = s} {e = e} {e' = e'}) = begin
  wkSub w (lock s (extRAssoc (upLFExt e) e'))
     â‰¡âŸ¨âŸ©
  lock
    (wkSub (factorWk (extRAssoc (upLFExt e) e') w) s)
    (factorExt (extRAssoc (upLFExt e) e') w)
    -- add substs
    â‰¡âŸ¨ HE.â‰…-to-â‰¡ (cong-lockâ‰… (lCtxPresTrans (upLFExt e) e' w) (rCtxPresTrans (upLFExt e) e' w) (â‰¡-subst-addable _ _ _) (â‰¡-substâ‚‚-addable _ _ _ _)) âŸ©
  lock
    (subst (Î» Î“L â†’ Sub Î“L _) (lCtxPresTrans (upLFExt e) e' w) (wkSub (factorWk (extRAssoc (upLFExt e) e') w) s))
    (substâ‚‚ (CExt _) (lCtxPresTrans (upLFExt e) e' w) (rCtxPresTrans (upLFExt e) e' w) (factorExt (extRAssoc (upLFExt e) e') w))
    -- push subst on subterm inside
    â‰¡âŸ¨ congâ‚‚ lock (subst-applicationâ€² (_ âŠ†_) (Î» w â†’ wkSub w s) (lCtxPresTrans (upLFExt e) e' w)) â‰¡-refl âŸ©
  lock
    (wkSub (subst (_ âŠ†_) (lCtxPresTrans (upLFExt e) e' w) (factorWk (extRAssoc (upLFExt e) e') w)) s)
    (substâ‚‚ (CExt _) (lCtxPresTrans (upLFExt e) e' w) (rCtxPresTrans (upLFExt e) e' w) (factorExt (extRAssoc (upLFExt e) e') w))
    -- factorisation preserves transitivity
    â‰¡âŸ¨ congâ‚‚ lock (congâ‚‚ wkSub (factorWkPresTrans (upLFExt e) e' w) â‰¡-refl) (factorExtPresTrans (upLFExt e) _ _) âŸ©
  lock
    (wkSub (factorWk (upLFExt e) (factorWk e' w)) s)
    (extRAssoc (factorExt (upLFExt e) (factorWk e' w)) (factorExt e' w))
    -- apply equalities for absorption of upLFExt
    â‰¡âŸ¨ congâ‚‚ lock (congâ‚‚ wkSub (â‰¡-sym (factorWkAbsorbsUpLFExt e (factorWk e' w))) â‰¡-refl) (congâ‚‚ extRAssoc (â‰¡-sym (factorExtAbsorbsUpLFExt e (factorWk e' w))) â‰¡-refl) âŸ©
  lock
    (wkSub (subst (_ âŠ†_) (lCtxAbsorbsUpLFExt e (factorWk e' w)) (factorWk e (factorWk e' w))) s)
    (extRAssoc (substâ‚‚ (CExt _) (lCtxAbsorbsUpLFExt e (factorWk e' w)) (rCtxAbsorbsUpLFExt e (factorWk e' w)) (upLFExt (factorExt e (factorWk e' w)))) (factorExt e' w))
    -- pull out substs
    â‰¡âŸ¨ congâ‚‚ lock (â‰¡-sym (subst-applicationâ€² (_ âŠ†_) (Î» x â†’ wkSub x s) (lCtxAbsorbsUpLFExt e (factorWk e' w)))) (ExtIsProp _ _) âŸ©
  lock
    (subst (Î» Î“L â†’ Sub Î“L _) (lCtxAbsorbsUpLFExt e (factorWk e' w)) (wkSub (factorWk e (factorWk e' w)) s))
    (substâ‚‚ (Î» Î“L Î“R â†’ CExt _ Î“L (Î“R ,, _)) (lCtxAbsorbsUpLFExt e (factorWk e' w)) (rCtxAbsorbsUpLFExt e (factorWk e' w)) (extRAssoc (upLFExt (factorExt e (factorWk e' w))) (factorExt e' w)))
    -- remove substs
    â‰¡âŸ¨ HE.â‰…-to-â‰¡ (cong-lockâ‰… (â‰¡-sym (lCtxAbsorbsUpLFExt e (factorWk e' w))) (â‰¡-sym (cong (_,, _) (rCtxAbsorbsUpLFExt e (factorWk e' w)))) (â‰¡-subst-removable _ _ _) (â‰¡-substâ‚‚-removable _ _ _ _)) âŸ©
  lock
   (wkSub (factorWk e (factorWk e' w)) s)
   (extRAssoc (upLFExt (factorExt e (factorWk e' w))) (factorExt e' w))
   -- apply shift-lockâ‰ˆâ‚›
   â‰ˆâŸ¨ shift-lockâ‰ˆâ‚› âŸ©
  lock
   (wkSub (LFExtToâŠ† (factorExt e (factorWk e' w))) (wkSub (factorWk e (factorWk e' w)) s))
   (factorExt e' w)
   -- wkSub preserves composition
   â‰¡âŸ¨ congâ‚‚ lock (wkSubPresâˆ™ _ _ _) â‰¡-refl âŸ©
  lock
   (wkSub (factorWk e (factorWk e' w) âˆ™ LFExtToâŠ† (factorExt e (factorWk e' w))) s)
   (factorExt e' w)
   -- apply factorisation lemma
   â‰¡âŸ¨ congâ‚‚ lock (congâ‚‚ wkSub (â‰¡-sym (factorisationLemma e _)) â‰¡-refl) â‰¡-refl âŸ©
  lock
   (wkSub (LFExtToâŠ† e âˆ™ factorWk e' w) s)
   (factorExt e' w)
   -- wkSub preserves composition
   â‰¡âŸ¨ congâ‚‚ lock (â‰¡-sym (wkSubPresâˆ™ _ _ _)) â‰¡-refl âŸ©
  lock
   (wkSub (factorWk e' w) (wkSub (LFExtToâŠ† e) s))
   (factorExt e' w)
   â‰¡âŸ¨âŸ©
  wkSub w (lock (wkSub (LFExtToâŠ† e) s) e') âˆ
  where
  open import Relation.Binary.Reasoning.Setoid (Sub-setoid Î”' Î“)

fact-extâ‰… : (e : CExt Î“ Î“L Î“R)
  â†’ e â‰… extRAssoc (upLFExt (factorSubâ‚›IdWk e)) (factorExtâ‚› e idâ‚›)
fact-extâ‰… e = â‰…-trans
  (â‰¡-subst-addable _ _ _)
  (â‰¡-to-â‰… (ExtIsPropâ€² e (extRAssoc (upLFExt (factorSubâ‚›IdWk e)) (factorExtâ‚› e idâ‚›))))

substTmPresId : (t : Tm Î“ a) â†’ t â‰ˆ substTm idâ‚› t
substTmPresId (var x)     = â‰¡-to-â‰ˆ (â‰¡-sym (substVarPresId x))
substTmPresId (lam t)     = cong-lamâ‰ˆ (substTmPresId t)
substTmPresId (app t u)   = cong-appâ‰ˆ (substTmPresId t) (substTmPresId u)
substTmPresId (box t)     = cong-boxâ‰ˆ (substTmPresId t)
substTmPresId (unbox t e) = fact-unboxâ‰ˆ t e
  where
  --
  coh-wkTm-substTm : (t : Tm Î“ a) (w : Î“ âŠ† Î“') â†’ wkTm w t â‰ˆ substTm (embWk w) t
  coh-wkTm-substTm {a = a} {Î“' = Î“'} t w = begin
    wkTm w t
      -- apply IH
      â‰ˆâŸ¨ wkTmPresâ‰ˆ w (substTmPresId t) âŸ©
    wkTm w (substTm idâ‚› t)
      -- apply naturality of substTm
      â‰¡âŸ¨ â‰¡-sym (nat-substTm t idâ‚› w) âŸ©
    substTm (wkSub w idâ‚›) t
      -- weakening id subst is same as embedding the weakening into a subst
      â‰¡âŸ¨ congâ‚‚ substTm {u = t} (wkSubId w) â‰¡-refl âŸ©
    substTm (embWk w) t âˆ
    where
    open import Relation.Binary.Reasoning.Setoid (Tm-setoid Î“' a)
  --
  fact-unboxâ‰ˆ : (t : Tm Î“L (â—» a)) (e : CExt Î“ Î“L Î“R)
    â†’ unbox t e â‰ˆ unbox (substTm (factorSubâ‚› e idâ‚›) t) (factorExtâ‚› e idâ‚›)
  fact-unboxâ‰ˆ {a = a} {Î“ = Î“} t e = begin
    unbox t e
      -- expand extension e
      â‰¡âŸ¨ â‰…-to-â‰¡ (cong-unboxâ‰… â‰¡-refl (extRUniq e (extRAssoc (upLFExt (factorSubâ‚›IdWk e)) (factorExtâ‚› e idâ‚›))) â‰…-refl (fact-extâ‰… e)) âŸ©
    unbox t (extRAssoc (upLFExt (factorSubâ‚›IdWk e)) (factorExtâ‚› e idâ‚›))
      -- apply shift-unbox
      â‰ˆâŸ¨ âŸ¶-to-â‰ˆ (shift-unbox _ _ _) âŸ©
    unbox (wkTm (LFExtToâŠ† (factorSubâ‚›IdWk e)) t) (factorExtâ‚› e idâ‚›)
      -- rewrite wkTm to substTm
      â‰ˆâŸ¨ cong-unbox1â‰ˆ (coh-wkTm-substTm t _) âŸ©
    unbox (substTm (embWk (LFExtToâŠ† (factorSubâ‚›IdWk e))) t) (factorExtâ‚› e idâ‚›)
      -- show that the subst is the factorisation of the id subst
      â‰¡âŸ¨ congâ‚‚ unbox (congâ‚‚ substTm {u = t} (â‰¡-sym (factorSubâ‚›IdWkIsFactorSubâ‚›Id e)) â‰¡-refl) â‰¡-refl âŸ©
    unbox (substTm (factorSubâ‚› e idâ‚›) t) (factorExtâ‚› e idâ‚›) âˆ
    where
    open import Relation.Binary.Reasoning.Setoid (Tm-setoid Î“ a)

rightIdSub : (s : Sub Î“ Î“') â†’ s â‰ˆâ‚› (s âˆ™â‚› idâ‚›)
rightIdSub []         = â‰ˆâ‚›-refl
rightIdSub (s `, t)   = cong-`,â‰ˆâ‚› (rightIdSub s) (substTmPresId t)
rightIdSub (lock s e) = fact-lockâ‰ˆ s e
  where
  --
  fact-lockâ‰ˆ : (s : Sub Î“L Î”) (e : CExt Î“ Î“L Î“R)
    â†’ lock s e â‰ˆâ‚› lock (s âˆ™â‚› factorSubâ‚› e idâ‚›) (factorExtâ‚› e idâ‚›)
  fact-lockâ‰ˆ {Î” = Î”} {Î“ = Î“} s e = begin
    lock s e
      -- expand extension e
      â‰¡âŸ¨ HE.â‰…-to-â‰¡ (cong-lockâ‰… â‰¡-refl (extRUniq e (extRAssoc (upLFExt (factorSubâ‚›IdWk e)) (factorExtâ‚› e idâ‚›))) â‰…-refl (fact-extâ‰… e)) âŸ©
    lock s (extRAssoc (upLFExt (factorSubâ‚›IdWk e)) (factorExtâ‚› e idâ‚›))
      -- apply shift-lockâ‰ˆâ‚›
      â‰ˆâŸ¨ shift-lockâ‰ˆâ‚› âŸ©
    lock (wkSub (LFExtToâŠ† (factorSubâ‚›IdWk e)) s) (factorExtâ‚› e idâ‚›)
      -- apply IH
      â‰ˆâŸ¨ cong-lockâ‰ˆâ‚› (wkSubPresâ‰ˆ _ (rightIdSub s)) âŸ©
    lock (wkSub (LFExtToâŠ† (factorSubâ‚›IdWk e)) (s âˆ™â‚› idâ‚›)) (factorExtâ‚› e idâ‚›)
      -- rewrite using coherence between weakening and composing substs (associativity, really)
      â‰¡âŸ¨ congâ‚‚ lock (coh-wkSub-âˆ™â‚› s idâ‚› (LFExtToâŠ† (factorSubâ‚›IdWk e))) â‰¡-refl âŸ©
    lock (s âˆ™â‚› wkSub (LFExtToâŠ† (factorSubâ‚›IdWk e)) idâ‚›) (factorExtâ‚› e idâ‚›)
      --  weakening of id subst is itself a weakening
      â‰¡âŸ¨ congâ‚‚ lock (cong (s âˆ™â‚›_) (wkSubId _)) â‰¡-refl âŸ©
    lock (s âˆ™â‚› (embWk (LFExtToâŠ† (factorSubâ‚›IdWk e)))) (factorExtâ‚› e idâ‚›)
      -- show that the weakening subst is the factorisation of the id subst
      â‰¡âŸ¨ congâ‚‚ lock (cong (s âˆ™â‚›_) (â‰¡-sym (factorSubâ‚›IdWkIsFactorSubâ‚›Id e))) â‰¡-refl âŸ©
    lock (s âˆ™â‚› factorSubâ‚› e idâ‚›) (factorExtâ‚› e idâ‚›) âˆ
    where
    open import Relation.Binary.Reasoning.Setoid (Sub-setoid Î“ (Î” ğŸ”’))

substVarPresâ‰ˆ : {s s' : Sub Î” Î“} (v : Var Î“ a) â†’ s â‰ˆâ‚› s' â†’ substVar s v â‰ˆ substVar s' v
substVarPresâ‰ˆ v      â‰ˆâ‚›-refl          = â‰ˆ-refl
substVarPresâ‰ˆ v      (â‰ˆâ‚›-trans r r')  = â‰ˆ-trans (substVarPresâ‰ˆ v r) (substVarPresâ‰ˆ v r')
substVarPresâ‰ˆ v      (â‰ˆâ‚›-sym r)       = â‰ˆ-sym (substVarPresâ‰ˆ v r)
substVarPresâ‰ˆ ze     (cong-`,â‰ˆâ‚› r r') = r'
substVarPresâ‰ˆ (su v) (cong-`,â‰ˆâ‚› r x)  = substVarPresâ‰ˆ v r

substTmPresâ‰ˆ : {s s' : Sub Î” Î“} (t : Tm Î“ a) â†’ s â‰ˆâ‚› s' â†’ substTm s t â‰ˆ substTm s' t
substTmPresâ‰ˆ (var v)     r = substVarPresâ‰ˆ v r
substTmPresâ‰ˆ (lam t)     r = cong-lamâ‰ˆ (substTmPresâ‰ˆ t (cong-`,â‰ˆâ‚› (wkSubPresâ‰ˆ fresh r) â‰ˆ-refl))
substTmPresâ‰ˆ (app t u)   r = cong-appâ‰ˆ (substTmPresâ‰ˆ t r) (substTmPresâ‰ˆ u r)
substTmPresâ‰ˆ (box t)     r = cong-boxâ‰ˆ (substTmPresâ‰ˆ t (cong-lockâ‰ˆâ‚› r))
substTmPresâ‰ˆ (unbox t e) r = {!!}

--------------------
-- Derived equations
--------------------
